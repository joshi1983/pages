!  Advanced Bar Charts.
!
DIM prob(6)

SET back "blue"
CLEAR

FOR chart = 1 to 6
    CALL Make_window(#1,chart,7)
    READ title$
    MAT READ prob
    CALL bars(title$,prob)
NEXT chart

DATA Northeast, .3, .4, .44, .52, .43, .38
DATA Southeast, .22, .23, .44, .52, .6, .67
DATA Central, .44, .42, .38, .36, .37, .32
DATA Northwest, .56, .58, .65, .67, .72, .74
DATA Southwest, .56, .59, .61, .62, .65, .67
DATA West, .44, .49, .67, .65, .72, .77
END


SUB bars(title$,prob())
    FOR i = Lbound(prob) to Ubound(prob)
        IF prob(i)<.5 then SET color "cyan" else SET color "magenta"
        BOX AREA i-.4,i+.4,.001,prob(i)
        SET color "white"
        BOX LINES i-.4,i+.4,.001,prob(i)
    NEXT i
    PLOT TEXT, at .5, .8: title$
END SUB

SUB Make_window(#1,chart,n)
    CLOSE #1
    LET left = mod(chart-1,3)/3 + .02
    LET bot  = .5 - Int((chart-1)/3)/2
    OPEN #1: screen left,left+.29, bot+.04,bot+.46
    SET BACK "blue"
    CLEAR
    SET color "white"
    BOX LINES 0,1,0,1
    PLOT 0,.5; 1,.5
    SET window 0,n,0,1
END SUB
