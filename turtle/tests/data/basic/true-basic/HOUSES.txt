! Draws three houses using pictures

SET WINDOW 0, 10, 0, 10
OPTION ANGLE degrees

SET BACK "black"
CLEAR

DRAW House with Scale(1) * Shift(7,6)
DRAW House with Scale(1.5) * Shift(1.8,4)
DRAW House with Rotate(45) * Scale(2,1.6) * Shift(6.2,1.8)

END

PICTURE House

    SET COLOR "red"
    PLOT AREA: -1,-1; -1,1; 1,1; 1,-1  ! The house itself

    SET COLOR "green"
    PLOT AREA: -1,1; 0,1.5; 1,1   ! Its roof
    PLOT AREA: -1,1; -1,1.4; -.8,1.4; -.8,1      ! and chimney

    SET COLOR "blue"
    PLOT AREA: -.15,-1; -.15,-.3; .15,-.3; .15,-1     ! A door

    DRAW Window with Shift(-.5,-.5)    ! and 4 windows
    DRAW Window with Shift(-.5,.5)
    DRAW Window with Shift(.5,.5)
    DRAW Window with Shift(.5,-.5)

END PICTURE

PICTURE Window

    SET COLOR "blue"
    PLOT AREA: -.1,-.2; -.1,.2; .1,.2; .1,-.2    ! The window
    SET COLOR "black"
    PLOT -.1,0; .1,0              ! and its mullions
    PLOT 0,-.2; 0,.2

END PICTURE
