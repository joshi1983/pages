<code>print clone []
; prints []

make "plist1 createPList2 [["x 3]]
print clone :plist1
; prints [x:=3]

make "y :plist1
; :plist1 and :y are the same.
; They don't just happen to be equal.
; They are 2 variable names for the same value.
; This point is important when you mutate the property list.

setProperty "y "x 4
; running setProperty "plist1 "x 4 would have the exact same effect.

type 'y = '
print :y
; prints y = [x:=4] as you'd most likely expect

type 'plist1 = '
print :plist1
; prints plist1 = [x:=4]
; This is something you might not expect.
; This demonstrates how :x and :plist1 are the same.

make "clonedPList1 clone :plist1
setProperty "clonedPList1 "x 5
; no other variable references clonedPList1's value 
; so this mutation won't show up like the 
; setProperty call several lines previous.

type 'clonedPList1 = '
print :clonedPList1
; prints clonedPList1 = [x:=5] as you'd expect.

type 'plist1 = '
print :plist1
; prints plist1 = [x:=4]
; Notice that the :plist1 was not changed.</code>
<code>; This example implements a couple procedures that
; clone in a similar way to the clone command.
; This example can help you see how the command works
; and potentially adapt it you want something a little different.

to cloneList :list1
	localmake "result []
	repeat count :list1 [
		queue2 "result item repcount :list1
	]
	output :result
end

to clonePropertyList :plist1
	localmake "result createPList
	localmake "keyValues plist "plist1
	for ["i 1 count :keyValues 2] [
		localmake "key item :i :keyValues
		localmake "value item :i + 1 :keyValues
		setProperty "result :key :value
	]
	output :result
end

print cloneList []
; prints []

make "plist1 createPList2 [["x 3]]
print clonePropertyList :plist1
; prints [x:=3]

make "clonedPList1 clonePropertyList :plist1
setProperty "clonedPList1 "x 5

type 'clonedPList1 = '
print :clonedPList1
; prints clonedPList1 = [x:=5]

type 'pList1 = '
print :plist1
; prints plist1 = [x:=3]

make "list1 []

make "clonedList1 cloneList :list1
setItem 1 "list1 3

type 'list1 = '
print :list1
; prints [3]

type 'clonedList1 = '
print :clonedList1
; prints clonedList1 = []</code>