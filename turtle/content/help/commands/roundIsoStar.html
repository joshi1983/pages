<code>to myArcLeft :angle :radius
	ifElse :radius = 0 [
		left :angle
	] [
		arcLeft :angle :radius
	]
end

; myRoundIsoStar procedure works exactly like the roundIsoStar command.
; The roundIsoStar command is available so you don't need to implement it yourself.
; Reviewing this implementation could help you implement something similar using procedures, though.
to myRoundIsoStar :radius1 :radius2 :numPoints :cornerRadius1 :cornerRadius2
	if and :radius1 = 0 :radius2 = 0 [
		stop
	]
	localmake "oldState turtleState
	if :radius1 < :radius2 [
		; swap radius1 with radius2.
		localmake "temp :radius1
		localmake "radius1 :radius2
		localmake "radius2 :temp
		right 180 / :numPoints
	]
	localmake "angle1 180 / :numPoints
	localmake "len1 :radius2 * sin :angle1
	localmake "len2 :radius2 * cos :angle1
	localmake "size1 hypot [:len1 :radius1 - :len2]
	localmake "angle2 90 - arcCos :len1 / :size1
	localmake "angle3 180 - 2 * (:angle1 + :angle2)

	localmake "arcAngle1 180 - :angle2
	localmake "len1 :cornerRadius1 / tan :arcAngle1
	if :size1 + :len1 < 0 [
		localmake "cornerRadius1 -:size1 * tan :arcAngle1
		localmake "len1 -:size1
	]
	localmake "size1 :size1 + :len1
	localmake "len2 abs :cornerRadius2 * tan :angle3 / 2
	if :size1 - :len2 < 0 [
		localmake "cornerRadius2 :size1 / tan :angle3 / 2
		localmake "len2 :size1
	]
	localmake "size1 :size1 - :len2
	localmake "radius1 :radius1 - :cornerRadius1 * ((1 / sin :arcAngle1) - 1)

	jumpForward :radius1
	right 90
	penUp
	localmake "arcAngle1 180 - 2 * :arcAngle1
	arcLeft :arcAngle1 / 2 :cornerRadius1
	polyStart
	repeat :numPoints [
		jumpForward :size1
		myArcLeft :angle3 :cornerRadius2
		jumpForward :size1
		arcLeft :arcAngle1 :cornerRadius1
	]
	polyEnd
	setTurtleState :oldState
end

; animation.time is used so you can interactively
; change this variable and see that the command and procedure draw the same way.
; To adjust time:
; 1. Click Set -> Animation Time.
; 2. Drag the progress bar around in the Animation Time dialog
; and watch the drawing change behind the dialog.
; This is the same for any animation.
make "radius1 1 + animation.time * 10
make "radius2 50
make "numPoints 5

circle :radius1
circle :radius2

setFillColor "red

isotoxalStar :radius1 :radius2 :numPoints

setFillColor "#8ff0
isotoxalStar :radius1 :radius2 :numPoints

; The yellow is roughly 50% opaque so
; if we see orange instead of yellow and
; nothing looks misaligned, they're overlapping perfectly.

setFillColor transparent
circle :radius1
circle :radius2
setPenColor "#800f
myRoundIsoStar :radius1 :radius2 :numPoints 10 10

to animation.snapshotstyle
	localmake "result createPList
	output :result
end

to animation.setup
	localmake "minutes 0
	localmake "seconds 10
	; Edit this if you want to change the length of your animation.
	localmake "result createPList
	setProperty "result "duration :minutes * 60 + :seconds
	output :result
end</code>