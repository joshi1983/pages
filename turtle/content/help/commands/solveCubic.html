<code>; The following code graphs the curve for:
; y = :a * :x * :x * :x + :b * :x * :x + :c * :x + :d.
; It marks the x-axis intersections with red circles.
; solveCubic :a :b :c :d is used
; to calculate the x values.

; adjust the a, b, c and d values to see
; the red points adjust correctly.
; To adjust animation.time, use the Set -> Animation Time feature.
make "a 1
make "b -5 + animation.time
make "c 3 - animation.time * 2
make "d -150 + animation.time * 20

to cubicXToY :x
	output :a * :x * :x * :x + :b * :x * :x + :c * :x + :d
end

; transforms the x-value to display it more clearly
to mathXToDrawingX :x
	output :x * 20
end

; Graph the curve.
setFillColor "black
repeat 100 [
	make "x (repcount - 50) * 0.2
	jumpTo [(mathXToDrawingX :x) cubicXToY :x]
	circle 3
]

; Show the solved intersection between
; the cubic curve and the x-axis.
make "xWhenYIsZero solveCubic :a :b :c :d
setFillColor "red
repeat count :xWhenYIsZero [
	make "x item repcount :xWhenYIsZero
	jumpTo [(mathXToDrawingX :x) 0]
	circle 8
	penUp
	; move away from dot enough to
	; make text more readable.
	setY yCor + 15
	penDown
	label str :x
]

to animation.snapshotstyle
	localmake "result createPList
	output :result
end

to animation.setup
	localmake "minutes 0
	localmake "seconds 10
	; Edit this if you want to change the length of your animation.
	localmake "result createPList
	setProperty "result "duration :minutes * 60 + :seconds
	output :result
end</code>