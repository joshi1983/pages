; Inspired by
; https://www.pinterest.ca/pin/830421618776212818/

to whiteSpikes :size
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "numSpikes 20
	localmake "angleOfSharpness 1
	localmake "size1 2 * :size * sin :angleOfSharpness
	localmake "colorStops createPList2 [
		[0 "#888] [1 "white]
	]
	setFillGradient createRadialGradient pos :size :colorStops
	setPenSize 0
	repeat :numSpikes [
		setHeading :oldHeading + (repcount + 0.5) * 360 / :numSpikes
		jumpTo :oldPos
		jumpForward :size
		right 180 - :angleOfSharpness
		polyStart
		jumpForward :size
		right :angleOfSharpness + 90
		jumpForward :size1
		polyEnd
	]
	setFillColor "black
	localmake "size :size * 0.8
	localmake "size1 2 * :size * sin :angleOfSharpness
	repeat :numSpikes [
		setHeading :oldHeading + (repcount + 0.5) * 360 / :numSpikes
		jumpTo :oldPos
		jumpForward :size
		right 180 - :angleOfSharpness
		polyStart
		jumpForward :size
		right :angleOfSharpness + 90
		jumpForward :size1
		polyEnd
	]
	jumpTo :oldPos
	setHeading :oldHeading
end

to outerArcs :size
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "size1 :size * 0.947
	localmake "angle9 20
	localmake "angle10 22.2
	localmake "arcsInfo1 [[-10.5 4.1] [-15 0.45] [-30 0.17] [-45.6 0.026822]]
	localmake "arcsInfo2 [[-18.3948 2.9] [-31.2 0] [-2 0.17] [-145 0] [18.3948 2.9]]
	localmake "numArcs 20
	localmake "colorStops createPList2 [
		[0 "black] [0.75 "black] [0.95 "#61D7FF] [0.99 "#CCF2FF]
		[1 "white]
	]
	setPenSize 0.1
	setPenColor "#8fff
	setFillGradient createRadialGradient pos :size1 :colorStops
	repeat :numArcs [
		setHeading :oldHeading + repcount * 360 / :numArcs - 20
		jumpTo :oldPos
		polyStart
		arcLines :arcsInfo1 :size
		arcLines reverse :arcsInfo1 :size
		polyEnd
	]
	setPenSize 0
	setFillColor "#8000
	; dark stripes
	repeat :numArcs [
		localmake "middleHeading :oldHeading + (repcount - 1) * 360 / :numArcs - :angle9
		setHeading :middleHeading
		jumpTo :oldPos
		polyStart
		arcLines :arcsInfo2 :size
		polyEnd
		setHeading :middleHeading + :angle10
		jumpTo :oldPos
		polyStart
		arcLines :arcsInfo2 -:size
		polyEnd
	]
	; restore original position and heading.
	jumpTo :oldPos
	setHeading :oldHeading
end

to leaf :size :angle
	localmake "oldHeading heading
	localmake "arcRadius :size / 2 / sin :angle
	left :angle
	polyStart
	arcRight :angle * 2 :arcRadius
	right 180 - :angle * 2
	arcRight :angle * 2 :arcRadius
	polyEnd
	setHeading :oldHeading
end

to forwardLeaf :angle :forwardDistance :color :size
	localmake "oldPos pos
	localmake "oldHeading heading
	left :angle
	jumpForward :forwardDistance
	setFillColor :color
	leaf :size 30
	jumpTo :oldPos
	setHeading :oldHeading
end

to tinyLeafs :size
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "numAngles 20
	localmake "size1 :size * 0.385
	localmake "size2 :size * 0.47
	localmake "size3 :size * 0.17
	localmake "angle1 20
	localmake "longLeafSize :size * 0.08
	localmake "longLeafAngle 25
	localmake "tinyLeafSize :size * 0.04
	localmake "tinyLeafSize2 :size * 0.03
	localmake "tinyLeafSize3 :size * 0.04
	localmake "leafSize4 :size * 0.06
	setPenSize 0
	repeat :numAngles [
		jumpTo :oldPos

		setHeading :oldHeading + repcount * 360 / :numAngles
		jumpForward :size1
		setFillColor "#8972FD
		leaf :size * 0.12 20
		left :angle1
		setFillColor "#D74B0F
		leaf :longLeafSize :longLeafAngle
		right :angle1 * 2
		leaf :longLeafSize :longLeafAngle
		left :angle1
		jumpForward :size * 0.08
		left :angle1
		setFillColor "#B3ADFF
		leaf :tinyLeafSize :longLeafAngle
		right :angle1 * 2
		leaf :tinyLeafSize :longLeafAngle
		left :angle1
		jumpBackward :size3
		right 90
		jumpBackward :leafSize4 * 0.5
		setFillColor "brown
		leaf :leafSize4 20

		jumpTo :oldPos
		setHeading :oldHeading + (repcount + 0.5) * 360 / :numAngles
		jumpForward :size2
		right 180
		forwardLeaf 40 :tinyLeafSize3 "aqua :tinyLeafSize2
		forwardLeaf 15 :tinyLeafSize2 "brown :tinyLeafSize
		forwardLeaf 0 :tinyLeafSize2 "brown :tinyLeafSize
		forwardLeaf -15 :tinyLeafSize2 "brown :tinyLeafSize
		forwardLeaf -40 :tinyLeafSize3 "aqua :tinyLeafSize2
	]
	jumpTo :oldPos
	setHeading :oldHeading
end

to redPeddles :size
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "numPeddles 20
	localmake "size1 :size * 0.3
	localmake "size2 :size * 0.76
	localmake "leafSize :size2 - :size1
	localmake "leafSize2 :leafSize * 0.95
	localmake "colorStops createPList2 [
		[0 "#a00] [1 "black]
	]
	setFillGradient createRadialGradient2 pos pos :size * 0.1 :colorStops "repeat
	setPenSize 0
	repeat :numPeddles [
		jumpForward :size1
		leaf :leafSize 30
		jumpTo :oldPos
		right 360 / :numPeddles
	]
	setHeading :oldHeading
	setLineJoinStyle "round
	setFillColor transparent
	repeat :numPeddles [
		jumpForward :size1
		setPenSize :size * 0.01
		setPenColor "white
		leaf :leafSize2 15
		setPenSize penSize * 0.8
		setPenColor "black
		leaf :leafSize2 15
		setPenSize 0
		jumpTo :oldPos
		right 360 / :numPeddles	
	]
end

to blueCircles :size
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "numCircles 20
	localmake "size1 :size * 0.5
	localmake "angle1 360 / :numCircles
	localmake "angle2 34
	localmake "angle3 180 + :angle1 - :angle2 * 2
	localmake "chordLength :size1 * 2 * sin :angle1 / 2
	localmake "arcRadius :chordLength / 2 / cos :angle2 - :angle1 * 0.5
	localmake "size2 :arcRadius + :size1 * (cos :angle1/2) - :arcRadius * ((sin :angle2 - :angle1 / 2) )
	localmake "gradientPosDistance :size * 0.452
	localmake "gradientRadius :size2 - :gradientPosDistance
	localmake "colorStops createPList2 [
		[0 "black] [0.2 "black] [0.8 "#50d]
		[0.85 "#8000FF] [1 "white]
	]
	setPenSize 0
	repeat :numCircles [
		setHeading :oldHeading + (repcount + 0.5) * :angle1
		jumpTo :oldPos
		forward :gradientPosDistance
		setFillGradient createRadialGradient pos :gradientRadius :colorStops
		jumpTo :oldPos
		setHeading :oldHeading + repcount * :angle1
		polyStart
		jumpForward :size1
		right :angle2
		arcRight :angle3 :arcRadius
		polyEnd
	]
	; darken the core area.
	jumpTo :oldPos
	localmake "colorStops createPList2 [
		[0 "black] [0.8 "black] [1 transparent]
	]
	setFillGradient createRadialGradient pos :size1 :colorStops
	circle :size1

	jumpTo :oldPos
	setHeading :oldHeading
end

to coreGradient :size
	localmake "coreRadius :size * 0.38
	localmake "colorStops createPList2 [
		[0 "black] [0.2 "#CC3D00] [0.285 "#F37139]
		[0.295 "#FFEAD1] [0.31 "#f77843] [0.32 "#f77843]
		[0.38 "#FFAD8A] [0.4 "#FFD5B3] [0.42 "#FDE8AF]
		[0.55 "#FCFDAF] [0.8 "#ff8d61] [0.9 "#F96124]
		[1 "black]
	]
	setFillGradient createRadialGradient pos :coreRadius :colorStops
	setPenSize 0
	circle :coreRadius
end

to coreSpikes :size
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "numSpikes 20
	localmake "coreRadius :size * 0.385
	localmake "colorStops createPList2 [
		[0 "#333] [0.9 "white] [0.95 "#400] [1 "black]
	]
	setFillGradient createRadialGradient pos :coreRadius :colorStops
	setPenSize 0
	repeat :numSpikes [
		jumpTo :oldPos
		setHeading :oldHeading + (repcount + 0.45) * 360 / :numSpikes
		polyStart
		forward :coreRadius
		right 90
		arcRight 2 :coreRadius
		polyEnd
	]
	jumpTo :oldPos
	setHeading :oldHeading
end

to whiteArcs :size
	localmake "numArcs 20
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "angle4 137
	localmake "size1 :size * 0.36
	localmake "arcsInfo1 [[-45 0.03] [-95 0.01] [-25 0.1]]
	localmake "arcsInfo2 [[15 0.05] [101.2 0.006] [28 0.11]]
	setPenSize 0
	setFillColor "white
	repeat :numArcs [
		setHeading :oldHeading + (repcount - 0.4) * 360 / :numArcs
		jumpTo :oldPos
		jumpForward :size1
		polyStart
		arcLines :arcsInfo1 :size
		left :angle4 ; point of reflection
		arcLines reverse :arcsInfo1 :size
		right 160
		arcLines :arcsInfo2 :size
		right 135.8 ; reflection point
		arcLines reverse :arcsInfo2 :size
		polyEnd
	]
	jumpTo :oldPos
	setHeading :oldHeading
end

to brownArcs :size
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "radius1 :size * 0.28
	localmake "arcRadius :size * 0.1
	localmake "arcRadius2 :size * 0.09
	localmake "numArcs 20
	localmake "colorStops createPList2 [
		[0 "#E1AB60] [0.4 "#E1AB60] [1 "#7D3B17]
	]
	setPenGradient createRadialGradient pos :radius1 :colorStops
	setPenSize :size * 0.003
	repeat :numArcs [
		
		setHeading :oldHeading + repcount * 360 / :numArcs
		jumpTo :oldPos
		jumpForward :radius1
		
		left 90
		ellipseArc 180 :arcRadius :arcRadius2 0
	]
	jumpTo :oldPos
	setHeading :oldHeading
end

to kaleidoscope22Flower :size
	outerArcs :size
	redPeddles :size
	whiteSpikes :size
	blueCircles :size
	coreGradient :size
	tinyLeafs :size
	brownArcs :size
	coreSpikes :size
	whiteArcs :size
end

setScreenColor "black
kaleidoscope22Flower 150