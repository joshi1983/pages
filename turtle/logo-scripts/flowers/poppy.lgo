; Inspired by:
; https://commons.wikimedia.org/wiki/File:Poppy.svg

to poppy :size
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "outerArcRadius :size * 0.59
	localmake "outerArcRadius2 :size * 0.059
	localmake "red1 "#e43232
	localmake "red2 "#c00
	localmake "green1 "#396
	localmake "green2 "#028000
	setPenColor :red2
	setFillColor :red1
	setPenSize :size * 0.03
	; move to centre the poppy on :oldPos.
	jumpForward :outerArcRadius * 0.62
	left 90
	jumpForward :outerArcRadius * 1.29
	setHeading :oldHeading

	; fill red part of poppy.
	polyStart
	repeat 4 [
		arcRight 130 :outerArcRadius
		arcRight 40 :outerArcRadius2
		arcLeft 120 :outerArcRadius2
		arcRight 40 :outerArcRadius2
	]
	polyEnd
	setHeading :oldHeading
	setFillColor :green1
	setPenColor :green2
	jumpTo :oldPos
	roundRegularStar :size * 0.32 8 :size * 0.05 :size * 0.05
	setHeading :oldHeading
end

poppy 200