to getCenterGradient :radius
	localmake "colorStops createPList2 [
		[0 "#FFFF50] [0.2 "#FFFF00] [0.45 "#DDC108]
		[0.53 "#DD7A08] [0.6 "#B0C606] [0.95 "#639900]
		[1 "#006D9D05]
	]
	output createRadialGradient pos :radius :colorStops
end

to getPetalGradient :radius
	localmake "colorStops createPList2 [
		[0 "#FFF] [0.6 "#FFF] [0.85 "#BAD6F2]
		[0.92 "#B0E6F2] [1 "#C08AE1]
	]
	output createRadialGradient pos :radius :colorStops
end

to drawCentralPattern :radius
	localmake "oldState turtleState
	localmake "numCircles 64
	localmake "radius1 :radius / 2
	localmake "colorStops createPList2 [
		[0 "#0800] [0.1 "#0800] [0.3 "#4800]
		[0.5 "#d800] [0.9 "#f350] [1 "#0000]
	]
	setPenSize :radius * 0.003
	setPenGradient createRadialGradient pos :radius :colorStops
	setFillColor transparent
	repeat :numCircles [
		circleRight :radius1
		right 360 / :numCircles
	]
	setTurtleState :oldState
end

; Inspired by photograph at:
; https://commons.wikimedia.org/wiki/File:White-wildflower-yellow-center_-_West_Virginia_-_ForestWander.jpg
; You can learn more about the species at:
; https://en.wikipedia.org/wiki/Erigeron_annuus
to daisyFleabaneFlower :height
	localmake "oldState turtleState
	localmake "oldHeading heading
	localmake "radius1 :height * 0.25
	localmake "numPetals 128
	localmake "petalLengths [:height / 2 - :radius1 (:height / 2 - :radius1) * 0.9]
	jumpForward :height / 2
	localmake "center pos
	setPenSize :height * 0.013
	setPenGradient getPetalGradient :height / 2
	setLineCap "round
	
	repeat :numPetals [
		setHeading :oldHeading + repcount * 360 / :numPetals
		localmake "petalLength item 1 + remainder repcount 2 :petalLengths
		forward :radius1 + :petalLength
		jumpTo :center
	]

	setPenSize 0
	setFillGradient getCenterGradient :radius1
	circle :radius1

	drawCentralPattern :radius1 * 0.97
	setTurtleState :oldState
end

setScreenColor "black
daisyFleabaneFlower 100