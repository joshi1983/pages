; inspired by
; https://www.pinterest.ca/pin/109071622215028204/

to radiatingLines :outerRadius :innerRadius :numLines
	repeat :numLines [
		; alternate the inner and outer radii slightly to make
		; the pattern more interesting.
		ifelse (remainder repcount 2) = 0 [
			localmake "alternatingOffset :outerRadius * 0.05
		] [
			localmake "alternatingOffset 0
		]
		jumpForward :innerRadius + :alternatingOffset
		forward :outerRadius - :alternatingOffset
		jumpBackward :outerRadius + :innerRadius
		right 360 / :numLines
	]
end

to centreCircle :size
	localmake "colorStops createPList2 [
		[0 "#b60] [0.6 "#740] [1 "#400]
	]
	setFillGradient createRadialGradient pos :size :colorStops
	setPenSize 0
	circle :size
end

to calcPetalSettings :length :width
	localmake "halfWidth :width * 0.5
	localmake "len1 max (:halfWidth * 1.01) :length / 3
	localmake "angle1 max 20 (1.01 * arcSin :halfWidth / :len1)
	localmake "len2 :len1 - :halfWidth / (tan :angle1)
	localmake "radius1 :len2 / sin (:angle1 * 0.5)
	localmake "len3 :radius1 * sin :angle1
	localmake "len4 :length - :len3
	localmake "len5 :halfWidth - :radius1 * (1 - cos :angle1)
	localmake "radius2 (:len4 * :len4 + :len5 * :len5) / (2 * :len5)
	localmake "angle2 :angle1 - (arcSin :len4 / :radius2)
	localmake "angle3 :angle1 - :angle2
	output [:halfWidth :radius1 :angle1 :radius2 :angle2 :angle3]
end

to petal :length :width
	localmake "oldState turtleState
	localmake "petalSettings calcPetalSettings :length :width
	localmake "halfWidth item 1 :petalSettings
	localmake "radius1 item 2 :petalSettings
	localmake "angle1 item 3 :petalSettings
	localmake "radius2 item 4 :petalSettings
	localmake "angle2 item 5 :petalSettings
	localmake "angle3 item 6 :petalSettings
	localmake "arcsInfo [
		[-:angle1 :radius1] [:angle2 0] [:angle3 :radius2]
	]
	jumpLeft :halfWidth
	setPenSize 0
	polyStart
	arcsLeft :arcsInfo 1
	right 180 ; symmetry
	arcsLeft reverse :arcsInfo 1
	polyEnd

	setTurtleState :oldState
end

to petalRing :innerRadius :petalLength :petalWidth :numPetals
	localmake "oldPos pos
	repeat :numPetals [
		jumpForward :innerRadius
		
		petal :petalLength :petalWidth
		jumpTo :oldPos
		right 360 / :numPetals
	]
end

to innerFlower :size
	localmake "oldHeading heading
	localmake "petalLength :size * 0.6
	localmake "petalWidth :petalLength * 0.27
	localmake "innerRadius :size - :petalLength
	localmake "colorStops createPList2 [
		[0 "brown] [0.18 "#b60] [0.35 "#fd3]
		[0.5 "#ffa] [0.7 "white] [1 "white]
	]
	setFillGradient createRadialGradient pos :size :colorStops
	circle :innerRadius * 1.04
	petalRing :innerRadius :petalLength :petalWidth 16
	localmake "colorStops createPList2 [
		[0 "brown] [0.5 "#840] [0.75 "#c90] [1 "#ff8]
	]
	setFillGradient createRadialGradient pos :size * 0.4 :colorStops
	isotoxalStar :size * 0.4 :size * 0.15 50

	centreCircle :size * 0.18
	setHeading :oldHeading
end

to outerFlower :size
	localmake "innerRadius :size * 0.7
	localmake "petalLength :size - :innerRadius
	localmake "petalWidth :petalLength * 0.3
	localmake "colorStops createPList2 [
		[0 "black] [0.2 "black] [0.42 "#44a]
		[0.55 "#070] [0.8 "#0d0] [1 "#af0]
	]
	setFillGradient createRadialGradient pos :size :colorStops
	circle :innerRadius
	localmake "numPetals 64
	petalRing :innerRadius * 0.98 :petalLength :petalWidth :numPetals
	setPenColor "#af0
	setPenSize :size * 0.002
	radiatingLines :size * 0.6 :size * 0.5 :numPetals
	setPenSize 0
	setFillColor "black
	repeat 32 [
		ellipse :size * 0.4 :size * 0.05
		right 360 / 32
	]
end

to outerMostFlower :size
	localmake "innerRadius :size * 0.65
	localmake "outerPetalRadius :size * 0.95
	localmake "petalLength :outerPetalRadius - :innerRadius
	localmake "petalWidth :petalLength * 0.22
	localmake "numPetals 64
	setPenColor "#080
	setPenSize :size * 0.002
	radiatingLines :size * 0.9 0 :numPetals
	right 180 / :numPetals
	setPenColor "white
	setPenSize :size * 0.001
	radiatingLines :size 0 :numPetals
	setPenSize 0
	localmake "colorStops createPList2 [
		[0 "black] [0.45 "black] [0.62 "#d60] [0.75 "yellow]
		[0.85 "white] [1 "white]
	]
	setFillGradient createRadialGradient pos :outerPetalRadius :colorStops
	circle :innerRadius
	petalRing :innerRadius * 0.98 :petalLength :petalWidth :numPetals
	setPenColor "#ff0
	setPenSize :size * 0.002
	radiatingLines :innerRadius * 1.1 0 :numPetals
end

to flower :size
	outerMostFlower :size
	right 360 / 128
	outerFlower :size * 0.65
	innerFlower :size * 0.35
end

setScreenColor "black
flower 100