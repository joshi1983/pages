; Inspired by
; https://publicdomainvectors.org/en/free-clipart/Vector-graphics-of-patterned-circles/29039.html
to crescent :outerRadius :innerRadius
	assert :outerRadius < :innerRadius
	localmake "innerArcAngle arcSin :outerRadius / :innerRadius
	right 90
	polyStart
	arcLeft 180 :outerRadius
	left 90 + :innerArcAngle
	arcRight :innerArcAngle * 2 :innerRadius
	polyEnd
end

to starMoonMandala :height
	localmake "oldState turtleState
	localmake "oldHeading heading
	localmake "radius1 :height * 0.209
	localmake "radius2 :height * 0.27
	localmake "radius3 :height * 0.331
	localmake "starRatio 0.6338
	localmake "bigStarSize :height * 0.142
	localmake "smallStarSize :height * 0.074
	localmake "largeMoonRadius :height * 0.12
	localmake "smallMoonRadius :height * 0.098
	localmake "moonAngles [90 130 55 90 130 50]
	localmake "arcAngles [52 26 26]
	setPenColor "black
	jumpForward :height / 2
	localmake "center pos
	setPenSize 0
	setFillColor penColor
	isotoxalStar :bigStarSize :bigStarSize * :starRatio 5
	setLineCap "butt
	repeat 6 [
		jumpForward :height * 0.427
		isotoxalStar :smallStarSize :smallStarSize * :starRatio 5		
		jumpTo :center
		right 360 / 6
	]
	repeat 6 [
		setHeading :oldHeading + 180 / 6 + repcount * 360 / 6
		jumpForward :height * 0.373 + :smallMoonRadius
		left item repcount :moonAngles
		jumpForward :smallMoonRadius
		left 180
		crescent :smallMoonRadius :largeMoonRadius	
		jumpTo :center
	]

	setHeading :oldHeading
	setFillColor transparent
	setPenSize :height * 0.031
	circle :radius1
	circle :radius3
	repeat 3 [
		setHeading :oldHeading - 26 + repcount * 360 / 3
		jumpForward :radius2
		right 90
		repeat count :arcAngles [
			arcRight (item repcount :arcAngles) :radius2
			penUp
			arcRight 5 :radius2
			penDown
		]
		jumpTo :center
	]

	setHeading :oldHeading
	setPenSize :height * 0.02
	repeat 3 [
		jumpForward :radius1
		forward :radius2 - :radius1
		jumpTo :center
		right 360 / 3
	]
	repeat 12 [
		jumpForward :radius3
		forward :height * 0.07
		jumpTo :center
		right 360 / 12
	]
	setTurtleState :oldState
end

starMoonMandala 100