; a drawing inspired by an image at:
; https://icones.pro/es/21-lugares-para-obtener-imagenes-vectoriales-gratuitas-para-sus-disenos/

; nearly black but subtly blue or violet as if we're seeing some
; air light up between us and the silouettes
make "dark "#080022

to drawNeedle :size
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "sign sign :size
	localmake "trunkDegrees 30
	localmake "trunkArcRadius abs :size * 72 / :trunkDegrees
	setFillColor :dark
	setPenSize 0
	polyStart
	arcLines [[10 0] [-:trunkDegrees 1] [-178 0] [:trunkDegrees 1]] 
		:trunkArcRadius * :sign
	setHeading towards :oldPos
	jumpForward distance :oldPos
	polyEnd
	setHeading :oldHeading
end

to drawBranch :size
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "numNeedles 100
	localmake "sign sign :size
	localmake "needleSize :size * 0.2
	localmake "branchThickness abs :size * 0.01
	localmake "delta abs :size / :numNeedles
	localmake "arcRadius :delta / 2 / sin 0.5
	setPenSize :branchThickness
	
	arcLines [[-:numNeedles 1]] :sign * :arcRadius
	jumpTo :oldPos
	setHeading :oldHeading
	repeat :numNeedles [
		jumpForward :delta
		right 91 * :sign
		drawNeedle :needleSize * (1 - repcount * 0.5 / :numNeedles)
		left 90 * :sign
	]
	setHeading :oldHeading
	jumpTo :oldPos
end

to drawTree :size
	localmake "oldPos pos
	localmake "oldHeading heading
	drawNeedle :size
	right 1
	jumpForward :size * 0.9
	right 19
	repeat 4 [
		drawBranch :size * 0.3
		right 30
	]
	right 100
	repeat 5 [
		drawBranch -:size * 0.3
		right 30
	]
	setHeading :oldHeading
	jumpTo :oldPos
end

to drawTree2 :size
	localmake "oldPos pos
	localmake "oldHeading heading
	drawNeedle -:size
	left 0.5
	jumpForward :size * 0.9
	right 19.5
	repeat 4 [
		drawBranch :size * 0.3
		right 30
	]
	right 100
	repeat 5 [
		drawBranch -:size * 0.3
		right 30
	]
	setHeading :oldHeading
	jumpTo :oldPos
end

to drawGround
	; Draws the ground as a graph of a sum of sin-waves.
	; The waveData represents the summed functions.
	; Enough wave functions added together leads to a natural appearance.
	; Each pair represents amplitude and wavelength.
	localmake "waveData [[5 0.5] [7 0.6] [13 0.8] [2 5]]
	setPenSize 0
	setFillColor :dark
	jumpTo [-245 -200]
	polyStart
	repeat 200 [
		localmake "x repcount * 2.515 - 253
		localmake "y -100
		repeat count :waveData [
			localmake "tuple item repcount :waveData
			localmake "y :y + (item 2 :tuple) * sin (:x * (item 1 :tuple))
		]
		localmake "p [:x :y]
		queue2 "p 0
		setHeading towards :p
		jumpForward distance :p
	]
	setHeading 180
	jumpForward 100
	polyEnd
end

to fillSkyColors :width :height
	setPenSize 0
	localmake "bottom pos
	jumpForward :height
	localmake "top pos
	localmake "colors createPList2 [
		[0 "#9800ff] [1 "#e87639]
	]
	setFillGradient createLinearGradient :top :bottom :colors "pad
	jumpBackward :height / 2
	rect :width :height
	setFillColor transparent
	jumpTo :bottom
end

setScreenColor :dark
setPenSize 0
jumpBackward 110
fillSkyColors 500 300
home
jumpForward 15

; set up radial gradient for the sun.
make "sunColors createPList2 [
	[0 "#ffc] [0.7 "#fea] [1 "#fc6]
] 
setFillGradient createRadialGradient pos 100 :sunColors
circle 100 ; draw the sun.

jumpBackward 170

right 90
jumpBackward 500
setPenSize 0
jumpForward 400
left 90
jumpForward 50

drawTree 150
right 30
drawTree 120
left 70
drawTree2 130
setHeading 90
forward 200
left 60
drawTree 105
left 30
drawTree2 175
left 35
drawTree2 80

home
setHeading 90
setXY -245 -104
drawGround