; Inspired by
; https://www.pinterest.ca/pin/137430226123605352/
; which is also at:
; https://dribbble.com/shots/10090344-Settings-Icon

make "color1 "#0f70e2

to settingsIcon :size
	repeat 6 [
		settingsIconArc :size
		right 360 / 6
	]
end

to getArcGradient :size
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "size1 :size * 0.1
	localmake "size2 :size1 + :size * 2 * sin 30
	localmake "colorStops createPList2 [
		[0 mix :color1 transparent 0.1] [1 :color1]
	]
	left 60
	jumpForward :size1
	localmake "fromPos pos
	jumpBackward :size2
	localmake "gradient createLinearGradient :fromPos pos :colorStops "pad
	jumpTo :oldPos
	setHeading :oldHeading
	output :gradient
end

to settingsIconArc :size
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "angle1 10
	localmake "angle2 79
	localmake "angle3 72
	localmake "angle4 39
	localmake "angle5 57.5
	localmake "arcRadius2 0.58
	setPenSize 0
	jumpForward :size
	setFillGradient getArcGradient :size
	left 90
	arcLeft :angle1 :size
	polyStart
	arcLines [[180 0] [-:angle1 * 2 1] [-:angle2 0] [:angle2 * 1.492 0.4]
		[-:angle2 0] [-:angle1 * 2 1] [-:angle3 0] [-:angle4 :arcRadius2]
		[-:angle5 0.8] [-:angle4 :arcRadius2]] :size
	polyEnd
	setPos :oldPos
	setHeading :oldHeading
end

setScreenColor "black
settingsIcon 100