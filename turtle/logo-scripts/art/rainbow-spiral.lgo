; Inspired by:
; https://www.pinterest.ca/pin/720505640370820635/

make "colors ["red "orange "yellow "green "aqua "blue "indigo]
make "thicknessRatio 0.15

to getColor :ratio
	localmake "ratio :ratio / (1 + 1 / count :colors)
	localmake "ratio sqrt :ratio
	localmake "colorIndex1 int (:ratio * count :colors)
	localmake "colorIndex2 remainder (:colorIndex1 + 1) (count :colors)
	localmake "c1 item :colorIndex1 + 1 :colors
	localmake "c2 item :colorIndex2 + 1 :colors
	localmake "ratio :ratio * count :colors
	localmake "ratio :ratio - int :ratio
	output mix :c2 :c1 :ratio
end

to quad :size :growthAngle
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "size1 :size * 0.5
	localmake "thickness :size * :thicknessRatio
	localmake "size2 :thickness / cos :growthAngle
	localmake "size3 :size + :size2 * 2 * sin :growthAngle
	left 90
	forward :size1
	polyStart
	right 90 - :growthAngle
	forward :size2
	right 90 + :growthAngle
	forward :size3
	right 90 + :growthAngle
	forward :size2
	polyEnd
	setPos :oldPos
	setHeading :oldHeading
end

to spiralArm :size :useColor1
	localmake "growthCoefficient 1.05
	localmake "growthAngle 1.5 * arcTan (:growthCoefficient - 1) * 0.5 / :thicknessRatio
	localmake "numIntervals 50
	localmake "size1 :size / (power :growthCoefficient :numIntervals)
	repeat :numIntervals [
		ifelse :useColor1 [
			setFillColor getColor (repcount - 1) / :numIntervals
		] [
			setFillColor getColor (:numIntervals - repcount) / :numIntervals
		]
		quad :size1 :growthAngle
		forward :size1 * :thicknessRatio * 0.7
		localmake "size1 :size1 * :growthCoefficient
		right 3.4
	]
end

to spiral :size
	localmake "oldPos pos
	localmake "oldHeading heading
	setPenSize 0
	repeat 6 [
		spiralArm :size (0 = remainder repcount 2)
		setPos :oldPos
		setHeading :oldHeading + repcount * 360 / 6
	]
end

setFillColor "red
spiral 150