; Inspired by:
; https://www.pinterest.ca/pin/161707442857042888/

make "colors ["white "black "green]

to gradient1 :size
	localmake "colorStops createPList2 [
		[0 item 1 :colors] [1 item 3 :colors]
	]
	localmake "gradient createRadialGradient pos :size :colorStops
	setFillGradient :gradient
end

; draws a narrow 4-sides shape
to narrowQuad :size
	drawArcLineShape [-0.05 0
		[[-30 0] [1] [-105 0] [0.38] [-91 0] [0.91]]
		] :size
end

; draws a narrow 4-sides shape
to narrowQuad2 :size
	drawArcLineShape [0 0
		[[-30 0] [1] [-105 0] [0.3] [-90 0] [0.93]]
		] :size
end

to endQuad :size
	drawArcLineShape [0 0
		[[1] [-100 0] [0.6] [-92 0] [1]]
		] :size
end

to tinyCircle :size
	gradient1 :size * 1.5
	circle :size
end

to strip :size
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "size1 :size * 0.18
	localmake "size2 :size * 0.4
	localmake "tinyCircleRadius :size2 * 0.12
	localmake "generalPenSize :size * 0.005
	setPenSize 0
	jumpForward :size1
	setFillColor transparent
	setPenSize :generalPenSize
	narrowQuad :size2 * 1.2
	gradient1 :size2
	narrowQuad2 :size2
	
	right 33
	jumpForward :size2 * 1.18
	tinyCircle :tinyCircleRadius
	right 100
	
	jumpForward :size2 * 0.24
	tinyCircle :tinyCircleRadius
	setHeading :oldHeading
	
	jumpForward :size * 0.12
	gradient1 :size * 0.25
	right 20
	endQuad :size * 0.25
	left 80
	
	jumpForward :size * 0.15
	right 72
	endQuad :size * 0.23

	jumpTo :oldPos
	setHeading :oldHeading
end

to greenParts :size
	localmake "numStrips 16
	repeat :numStrips [
		strip :size
		right 360 / :numStrips
	]
end

to gearPath :size
	localmake "oldState turtleState
	localmake "size1 :size * 0.275
	localmake "size2 :size * 0.25
	localmake "size3 :size * 0.08
	right 1.4
	polyStart
	repeat 16 [
		right 97.5
		forward :size1
		right 97
		forward :size2
		left 90
		forward :size3
		left 82
		forward :size2
	]
	polyEnd
	setTurtleState :oldState
end

to drawCentre :size
	localmake "oldPos pos
	localmake "innerSize :size * 0.6
	jumpForward :size
	setPenSize :size * 0.035
	setFillColor item 2 :colors
	gearPath :size
	setFillColor item 1 :colors
	
	jumpTo :oldPos
	jumpForward :innerSize
	gearPath :innerSize

	jumpTo :oldPos
end

to drawBrownStrip :size
	localmake "oldState turtleState
	localmake "size1 :size * 0.183
	localmake "size2 :size * 0.45
	localmake "size3 :size * 0.38
	localmake "generalPenSize :size * 0.01
	setPenSize :generalPenSize
	left 7.5
	jumpForward :size1
	
	polyStart
	right 31
	jumpForward :size2
	left 12
	jumpForward :size3 * 0.1
	arcLines [[0.342] [-100 0] [0.19] [-91 0] [0.29] [77 0]
		[0.145] [-89 0]] :size
	jumpForward :size * 0.08
	right 90
	jumpForward :size * 0.19
	setPenSize 0
	polyEnd
	setTurtleState :oldState
end

to brownParts :size
	setFillColor item 2 :colors
	repeat 16 [
		drawBrownStrip :size
		right 360 / 16
	]
end

to drawPattern :size
	brownParts :size
	drawCentre :size * 0.15
	greenParts :size
end

drawPattern 100