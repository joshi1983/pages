; Inspired by
; https://www.pinterest.ca/pin/83738874310664020/

to line :len
	penDown
	forward :len
	penUp
	back :len
	penDown
end

to emblem.1 :size
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "angle 360 / 6
	localmake "size1 2 * :size * sin (:angle / 2)
	setPenSize :size * 0.03
	penUp
	forward :size
	right 90 + 180 / 6
	penDown
	repeat 6 [
		forward :size1
		right 360 / 6
	]
	repeat 2 [
		penUp
		repeat 4 [
			left :angle
			back :size1 / 4
			right :angle
			ifelse repcount = 4 [
				line :size1 / 4
			] [
				line :size1 * ((4 - repcount) / 4)
			]
			penUp
		]
		setPos :oldPos
		setHeading :oldHeading
		back :size
		setHeading :oldHeading - 90 + :angle / 2
	]
	setHeading :oldHeading
	setPos :oldPos
	forward :size1 * 0.5 + :size1 * (cos :angle) / 4
	left 90
	forward :size1 * 3 / 4 * sin :angle
	repeat 4 [
		left :angle * 0.5
		line :size1 * repcount / 2 - :size1 / 4
		penUp
		right :angle * 0.5
		back :size1 / 2 * sin :angle
	]
	setPos :oldPos
	setHeading :oldHeading
	back :size1 / 8
	setHeading :oldHeading + :angle
	back :size1
	right :angle / 2
	forward :size1 / 4 * sin :angle
	left :angle / 2
	repeat 4 [
		line :size1 * 2 - :size1 * (repcount - 0.5) / 2
		penUp
		right :angle / 2
		forward :size1 / 2 * (sin :angle)
		left :angle / 2
	]
	setPos :oldPos
	setHeading :oldHeading + :angle
	forward :size1
	penDown
	back :size1 * 2
	penUp
	setPos :oldPos
	setHeading :oldHeading
end

to emblem.2 :size
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "numAngles 8
	repeat :numAngles [
		penDown
		arcRight 225 :size * 0.5
		penUp
		setPos :oldPos
		setHeading :oldHeading + repcount * 360 / :numAngles
	]
end

to emblem.3 :size
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "arcRadiusScale 0.8
	localmake "arcRadius :size * :arcRadiusScale * sin 45
	setFillColor "white
	right 45
	repeat 2 [
		penUp
		arcRight 90 :arcRadius
		penDown
		polyStart
		repeat 4 [
			right 90
			arcRight 180 :arcRadius
		]
		polyEnd
		penUp
		right 45
		setPos :oldPos
		localmake "arcRadius :arcRadius / :arcRadiusScale
	]
	penUp
	setHeading :oldHeading
end

to emblem.4 :size
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "size1 :size * 0.6
	localmake "size2 :size * 1
	localmake "arcRadius :size * 0.12
	localmake "startSize1 :arcRadius * cos 45
	penUp
	repeat 2 [
		back :startSize1
		left 90
		forward :size * 0.8
		right 135
		forward :size2 - :size1
		penDown
		forward :size1
		right 90
		forward :size2
		arcLeft 270 :arcRadius
		forward :size2
		right 90
		forward :size1
		penUp
		setPos :oldPos
		setHeading :oldHeading + 180
	]
	setPos :oldPos
	setHeading :oldHeading
end

to emblem.5 :size
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "arcRadius :size * 0.5
	left 30
	forward :size
	penDown
	right 210
	repeat 6 [
		arcLeft 180 :arcRadius
		left 180 * 2 / 3
	]
	penUp
	setHeading :oldHeading
	setPos :oldPos
	repeat 6 [
		forward :size * 0.5
		circle :size * 0.08
		penUp
		setPos :oldPos
		right 360 / 6
	]
	setHeading :oldHeading
end

to emblem.9 :size
	localmake "oldHeading heading
	localmake "oldPos pos
	localmake "numAngles 16
	repeat :numAngles [
		penDown
		forward :size * 0.7
		arcRight 154 :size * 0.3
		right 180
		arcLeft 150 :size * 0.18
		forward :size * 0.26
		penUp
		setPos :oldPos
		setHeading :oldHeading + repcount * 360 / :numAngles
	]
	setHeading :oldHeading
end

to emblem.88 :size
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "arcRadius :size * 0.14
	localmake "size1 :size * 1.3 - :arcRadius
	penUp
	forward (:size1 - :arcRadius) * sin 45
	left 45
	penDown
	repeat 4 [
		arcRight 270 :arcRadius
		forward :size1
	]
	penUp
	setPos :oldPos
	setHeading :oldHeading
end

to emblem.90 :size
	localmake "size1 :size * 1.1
	localmake "arcRadius :size * 0.13
	penUp
	back :arcRadius * cos 45
	left 90
	forward :size1 * sin 45
	left 45
	penDown
	repeat 2 [
		arcRight 270 :arcRadius
		forward :size1
		left 90
		forward :size1
	]
	
end

make "size 30
make "separation :size * 2.2
jumpLeft :separation * 3.5
emblem.1 :size
jumpRight :separation
emblem.2 :size
jumpRight :separation
emblem.3 :size
jumpRight :separation
emblem.4 :size
jumpRight :separation
emblem.5 :size
jumpRight :separation
emblem.9 :size
jumpRight :separation
emblem.88 :size
jumpRight :separation
emblem.90 :size