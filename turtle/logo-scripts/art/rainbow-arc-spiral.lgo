; Inspired by:
; https://www.pinterest.ca/pin/1064679168130697063/

to filledArc :angle :innerRadius :maxThickness
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "halfChordLength :innerRadius * sin (:angle / 2)
	localmake "innerChordDepth :innerRadius * (1 - cos (:angle / 2))
	localmake "outerChordDepth :maxThickness + :innerChordDepth
	localmake "outerLen1 hypot [:halfChordLength :outerChordDepth]
	localmake "outerAngle1 arcTan :halfChordLength / :outerChordDepth
	localmake "radius2 :outerLen1 * 0.5 / cos :outerAngle1
	localmake "angle2 2 * arcSin :halfChordLength / :radius2

	jumpForward :innerRadius
	right 90
	arcRight -:angle * 0.5 :innerRadius
	
	polyStart
	arcRight :angle :innerRadius
	left 180 - (:angle2 - :angle)* 0.5
	arcLeft :angle2 :radius2
	polyEnd
	jumpTo :oldPos
	setHeading :oldHeading
end

to colorWheelSpiral :size
	localmake "oldHeading heading
	localmake "numArcs 30
	localmake "minRadius :size * 0.1
	localmake "size1 :size - :minRadius
	localmake "maxThickness :size / :numArcs
	localmake "colors [
		"white "yellow "orange "red "navy "darkgreen 
		"brown "purple "indigo "black]
	setPenSize 0
	localmake "tempHeading :oldHeading
	; loop a couple times with different angles to hide 
	; any cracks between the colors that show screen color.
	repeat 2 [

		; loop through the colors.
		repeat count :colors [
			setFillColor item repcount :colors
			setHeading :tempHeading + repcount * 360 / count :colors

			; draw various arcs in the shpe of a spiral arm.
			repeat :numArcs [
				localmake "angle 30 + repcount * 150 / :numArcs
				localmake "radius :minRadius - :maxThickness + repcount * :size1 / :numArcs
				filledArc :angle :radius :maxThickness
				right 360 / :numArcs
			]
		]
		; change the angle of the pattern to reduce so more of the
		; screen/background color is filled in the next time.
		localmake "tempHeading :tempHeading + 18
	]
	setHeading :oldHeading
end

setScreenColor "black
colorWheelSpiral 150