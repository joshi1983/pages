to emojiBackground :radius
	localmake "oldState turtleState
	localmake "oldPos pos
	localmake "arcsInfo [[25 1.5] [100 0.28]]
	localmake "arcRadius3 :radius * 0.9094244
	localmake "angle3 110
	localmake "coreColor "#FDF03F
	localmake "outerColor1 "orange
	localmake "outerColor2 "#FF7B00
	localmake "colorStops createPList2 [
		[0 :coreColor] [0.75 mix :coreColor :outerColor1 0.8]
		[0.95 :outerColor1] [1 :outerColor2]
	]
	setFillGradient createRadialGradient pos :radius :colorStops
	setPenSize :radius * 0.002
	setPenColor "brown
	circle :radius

	; darken the upper part of the circle.
	localmake "colorStops createPList2 [
		[0 mix :outerColor1 transparent 0.3] [1 transparent]
	]
	jumpForward :radius
	localmake "fromPos pos
	jumpBackward :radius * 1.4
	setFillGradient createLinearGradient :fromPos pos :colorStops "pad
	setPenSize 0
	jumpTo :oldPos
	circle :radius

	; Draw some reflected light from above.
	localmake "colorStops createPList2 [[0 "#0fff] [1 "#dfff]]
	jumpForward :radius
	localmake "fromPos pos
	jumpBackward :radius * 1.2
	setFillGradient createLinearGradient  pos :fromPos :colorStops "pad
	jumpTo :oldPos
	right 90
	polyStart
	arcsLeft :arcsInfo :radius
	arcLeft :angle3 :arcRadius3
	arcsLeft reverse :arcsInfo :radius
	polyEnd
	setTurtleState :oldState
end

to fillHeart :height
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "size1 :height * 0.7

	; Some trigonometry used to calculate exact sizes for symmetry.
	localmake "arcRadius (:height - :size1 * cos 45) / (1 + cos 45)
	localmake "size2 (:size1 - :arcRadius * 2) 
	right 45
	polyStart
	jumpForward :size1
	arcLeft 180 :arcRadius
	jumpForward :size2
	right 90
	jumpForward :size2
	arcLeft 180 :arcRadius
	polyEnd
	jumpTo :oldPos
	setHeading :oldHeading
end

to redHeart :height
	localmake "oldPos pos
	localmake "height1 :height * 0.98
	localmake "height2 :height * 0.95
	setFillColor "#dfff
	jumpTo :oldPos
	jumpBackward :height * 0.5
	fillHeart :height2

	setFillColor "#8840
	jumpTo :oldPos
	jumpBackward :height * 0.43
	fillHeart :height1

	setFillColor "black
	jumpTo :oldPos
	jumpBackward :height * 0.45
	fillHeart :height1

	localmake "colorStops createPList2 [
		[0 "red] [0.4 "#E00000] [0.8 "darkRed] [1 "#750000]
	]
	setFillGradient createRadialGradient :oldPos :height2 * 0.7 :colorStops
	jumpTo :oldPos
	jumpBackward :height2 * 0.5
	fillHeart :height2
	jumpTo :oldPos
end

to fillSmile :size
	localmake "arcsInfo [
		[-90 0] [33 1.532] [40 0.4] [136 0.02] [-29 2.0013]
	]
	drawArcLineShape [0 0 
		combine :arcsInfo reverse :arcsInfo
	] :size
end

to redSmile :size
	localmake "oldPos pos

	setPenSize 0
	setFillColor "#dfff
	jumpBackward :size * 0.01
	fillSmile :size

	setFillColor "#8840
	jumpTo :oldPos
	jumpForward :size * 0.02
	fillSmile :size

	localmake "colorStops createPList2 [[0 "#610000] [1 "#DB0000]]
	jumpTo :oldPos
	jumpForward :size * 0.3
	setFillGradient createLinearGradient pos :oldPos :colorStops "pad
	jumpTo :oldPos
	fillSmile :size
end

to filledSquintyEye1 :size
	localmake "arcsInfo [
		[-90 0] [-95 0.5] [171.23 0] [30.09 2.293] [73.68 0.4915]
	]
	drawArcLineShape [0 0 
		combine :arcsInfo reverse :arcsInfo
	] :size
end

to coloredSquintyEye1 :size
	setFillColor "#CC8500
	filledSquintyEye1 :size
end

to loveEmoji :size
	localmake "oldPos pos
	localmake "eyeSeparation :size * 0.4
	localmake "eyeSize :size * 0.55
	emojiBackground :size
	jumpForward :size * 0.25
	jumpRight :eyeSeparation
	redHeart :eyeSize
	jumpLeft 2 * :eyeSeparation
	redHeart :eyeSize
	jumpTo :oldPos
	jumpBackward :size * 0.55
	redSmile :size * 0.55
	jumpTo :oldPos
end

to upperLip :height
	localmake "oldState turtleState
	localmake "arcsInfo1 [
		[-130 0.014] [20 0.8] [0.24] [-38 0.41] [-53 0.208743853]
		[41 0.07]
	]
	localmake "arcsInfo2 [
		[-30 0.04] [-23 0.42] [12 1.3] [48 0.31] [-27 0.329464112]
	]
	localmake "lipShape [-0.82 0.4685
		(combine [[70 0]] :arcsInfo1 reverse :arcsInfo1 :arcsInfo2 reverse :arcsInfo2)
	]
	drawArcLineShape :lipShape :height
	setTurtleState :oldState
end

to lowerLip :height
	localmake "oldState turtleState
	localmake "arcAngle6 32
	localmake "arcAngle10 76
	localmake "arcRadius6 :height * 0.45905422
	localmake "arcRadius10 :height * 0.692589935
	localmake "arcsInfo1 [
		[-10 0.25] [10 0.9] [-9 1] [-12 0.45] [-15 0.6]
	]
	localmake "arcsInfo2 [
		[-133 0.008] [17 0.6] [-6 1.9]
	]
	jumpForward :height * 0.421
	jumpLeft :height * 0.72
	right 70
	polyStart
	arcsLeft :arcsInfo1 :height
	arcLeft :arcAngle6 :arcRadius6 ; symmetry
	arcsLeft reverse :arcsInfo1 :height
	arcsLeft :arcsInfo2 :height
	arcRight :arcAngle10 :arcRadius10 ; symmetry
	arcsLeft reverse :arcsInfo2 :height
	polyEnd
	setTurtleState :oldState
end

; Inspired by:
; https://www.pinterest.ca/pin/390546598920799942/
to redLips :height
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "angle1 65
	localmake "angle2 2 * (90 - :angle1)
	localmake "angle3 63
	localmake "size1 :height * 0.83
	localmake "size2 :size1 / sin :angle1
	localmake "size3 :size1 / sin :angle3
	setPenSize 0
	setFillColor "black
	jumpLeft :size1
	jumpForward :height * 0.48
	polyStart
	right :angle1
	jumpForward :size2
	right :angle2
	jumpForward :size2
	setHeading :oldHeading + 180 + :angle3
	jumpForward :size3	
	circle 0.1
	polyEnd
	jumpTo :oldPos
	setHeading :oldHeading
	localmake "colorStops createPList2 [
		[0 "#D71414] [0.3 "#cc1010] [1 "#610000]
	]
	jumpForward :height
	localmake "topPos pos
	jumpBackward :height * 0.5
	setFillGradient createLinearGradient :topPos pos :colorStops "pad
	jumpTo :oldPos
	upperLip :height
	localmake "colorStops createPList2 [
		[0 "#FBACAC] [0.4 "#D71414] [1 "#cc1010]
	]
	jumpForward :height * 0.5
	setFillGradient createLinearGradient pos :oldPos :colorStops "pad
	jumpTo :oldPos
	lowerLip :height
end

to bigLipsEmoji :size
	localmake "oldPos pos
	localmake "eyeSize :size * 0.12
	localmake "eyeSeparation :size * 0.45

	emojiBackground :size
	jumpForward :size * 0.27
	jumpRight :eyeSeparation
	blackEye :eyeSize
	jumpLeft :eyeSeparation * 2
	blackEye :eyeSize

	jumpTo :oldPos
	jumpBackward :size * 0.65
	redLips :size * 0.5

	jumpTo :oldPos
end

to setBlushGradient :radius
	localmake "colorStops createPList2 [
		[0 "#5f00] [0.5 "#3f00] [1 "#0f00]
	]
	setFillGradient createRadialGradient pos :radius :colorStops
end

to blush :size
	localmake "oldPos pos
	localmake "eyeSize :size * 0.4
	localmake "eyeSeparation :size * 0.4
	localmake "blushSeparation :size * 0.65
	localmake "blushSize :size * 0.25
	emojiBackground :size
	setPenSize 0
	jumpBackward :size * 0.05
	jumpRight :blushSeparation
	setBlushGradient :blushSize
	circle :blushSize
	jumpLeft :blushSeparation * 2
	setBlushGradient :blushSize
	circle :blushSize
	
	jumpTo :oldPos
	jumpForward :size * 0.3
	jumpRight :eyeSeparation
	coloredSquintyEye1 :eyeSize
	jumpLeft :eyeSeparation * 2
	coloredSquintyEye1 :eyeSize
	jumpTo :oldPos
	jumpBackward :size * 0.6
	redSmile :size * 0.55

	jumpTo :oldPos
end

to fillSunglasses :size
	localmake "arcsInfo [
		[-90 0] [-85 0.08] [15 1.6] [55 0.18] [30 1] [55 0.18]
		[35 1] [70 0.08] [9 4] [26 0.421003915]
	]
	localmake "glassesShape [0 0
		(combine :arcsInfo [[-60 0.16]] reverse :arcsInfo)
	]
	drawArcLineShape :glassesShape :size
end

to sunglasses :size
	localmake "oldPos pos

	emojiBackground :size
	setPenSize 0

	setFillColor "white
	jumpForward :size * 0.34
	fillSunglasses :size * 0.85

	jumpBackward :size * 0.015
	setFillColor "black
	fillSunglasses :size * 0.85
	jumpTo :oldPos
	jumpBackward :size * 0.6
	redSmile :size * 0.55
	
	jumpTo :oldPos
end

to blackEye :size
	setFillColor "black
	circle :size
end

to smile :size
	localmake "oldPos pos
	localmake "eyeSize :size * 0.12
	localmake "eyeSeparation :size * 0.45

	emojiBackground :size
	jumpForward :size * 0.27
	jumpRight :eyeSeparation
	blackEye :eyeSize
	jumpLeft :eyeSeparation * 2
	blackEye :eyeSize

	jumpTo :oldPos
	jumpBackward :size * 0.6
	redSmile :size * 0.55

	jumpTo :oldPos
end

to flatLips :size
	localmake "oldState turtleState
	localmake "angle 13
	localmake "arcRadius :size * 3.5
	setLineCap "round
	right 90
	setPenSize 0
	arcRight :angle :arcRadius
	setPenSize :size * 0.19
	right 180
	arcLeft :angle * 2 :arcRadius

	setTurtleState :oldState
end

to coloredFlatLips :size
	localmake "oldState turtleState
	jumpBackward :size * 0.07
	setPenColor "white
	flatLips :size
	jumpForward :size * 0.14
	setPenColor "black
	flatLips :size
	setPenColor "darkRed
	jumpBackward :size * 0.07
	flatLips :size
	setTurtleState :oldState
end

to tuckerCarlsonHair :size
	localmake "oldState turtleState

	jumpLeft :size
	setPenSize 0
	left 35
	setFillColor "#4D1300
	polyStart
	arcLines [[-50 0.5] [-115 1.03] [35 0] [-122 0.59] [-80 0]
		[-50 0.5] [-8 2] [70 0] [10 2] [30 0.5] [-40 0.4] [60 0.3]
		[-65 0.35] [104 0] [-30 0.9]] :size
	polyEnd

	setTurtleState :oldState
end

to confusedEye :size
	localmake "oldState turtleState
	setFillColor "#A36A00
	setPenSize 0
	drawArcLineShape [0 0 [[-90 0] [30 2] [60 0.17] [60 0]
		[40 0.6] [16.3 0.2] [21 2.16]]] :size
	
	setTurtleState :oldState
end

to confusedBlush :size
	setBlushGradient :size
	circle :size
end

; Inspired by Tucker Carlson's confused/angry face
; This is a similar image:
; https://www.pinterest.ca/pin/777715429389580233/
; This expression is frequently used by Tucker 
; when listening to guests on his show.
to confused :size
	localmake "oldPos pos
	localmake "eyeSeparation :size * 0.65
	localmake "eyeSize :size * 0.45
	localmake "blushSize :size * 0.3
	localmake "blushSeparation :size * 0.6
	emojiBackground :size
	tuckerCarlsonHair :size
	jumpBackward :size * 0.35
	jumpRight :blushSeparation
	confusedBlush :blushSize
	jumpLeft :blushSeparation * 2
	confusedBlush :blushSize

	jumpTo :oldPos
	jumpBackward :size * 0.5
	coloredFlatLips :size * 0.28
	jumpTo :oldPos
	jumpRight :eyeSeparation
	confusedEye -:eyeSize
	jumpLeft :eyeSeparation * 2
	confusedEye :eyeSize
	jumpTo :oldPos
end

to dashedTatoo :len :width :numDashes
	localmake "oldState turtleState
	localmake "dashLength :len / 2 / :numDashes
	setPenSize :width
	repeat :numDashes [
		forward :dashLength
		jumpForward :dashLength
	]
	setTurtleState :oldState
end

; Inspired by
; https://www.etsy.com/ca/listing/644078565/6ix9ine-temporary-tattoos-set-6ix9ine
to tatoodFace :size
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "eyeSize :size * 0.12
	localmake "eyeSeparation :size * 0.45

	setFillColor transparent
	setPenSize :size * 0.01
	setPenColor "black
	emojiBackground :size
	jumpForward :size * 0.45
	setFontSize :size * 0.5
	right 90
	label "69
	setHeading :oldHeading
	jumpTo :oldPos
	jumpRight :size * 0.8
	jumpBackward :size * 0.3
	setFontSize :size * 0.3
	right 30
	label "13

	; eyebrow tatoo
	jumpTo :oldPos
	setHeading :oldHeading
	jumpForward :size * 0.5
	jumpLeft :size * 0.3
	left 90
	dashedTatoo :size * 0.3 :size * 0.1 20

	; Draw cross tatoo in center
	repeat 2 [
		jumpTo :oldPos
		setHeading :oldHeading + repcount * 90
		jumpBackward :size * 0.1
		forward :size * 0.2
	]

	; Draw beard-like tatoo.
	repeat 21 [
		jumpTo :oldPos
		setHeading :oldHeading + 120 + (repcount - 1) * 120 / 20
		jumpForward :size * 0.7
		forward :size * 0.3
	]

	jumpTo :oldPos
	setHeading :oldHeading - 90
	jumpLeft :size * 0.05
	jumpForward :size * 0.4
	repeat 5 [
		localmake "pos1 pos
		arcRight 150 :size * 0.3 * (6 - repcount) / 5
		jumpTo :pos1
		setHeading :oldHeading - 90
		jumpLeft :size * 0.02
		jumpForward :size * 0.13
	]
	

	jumpTo :oldPos
	setHeading :oldHeading
	jumpForward :size * 0.27
	jumpRight :eyeSeparation
	blackEye :eyeSize
	jumpLeft :eyeSeparation * 2
	blackEye :eyeSize

	jumpTo :oldPos
	jumpBackward :size * 0.6
	redSmile :size * 0.55

	jumpTo :oldPos
end

make "size 100
make "separation :size * 3.5
jumpLeft :separation * 3
loveEmoji :size
jumpRight :separation
bigLipsEmoji :size
jumpRight :separation
blush :size
jumpRight :separation
sunglasses :size
jumpRight :separation
smile :size
jumpRight :separation
confused :size
jumpRight :separation
tatoodFace :size