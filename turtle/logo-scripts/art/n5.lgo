; Inspired by
; https://www.pinterest.ca/pin/105482816264901265/

to drawBackground :size
	localmake "colorStops createPList2 [
		[0 "white] [0.6 "white] [1 screenColor]
	]
	setFillGradient createRadialGradient pos :size :colorStops
	setPenSize 0
	circle :size
end

to triangleCurve :size :numPoints
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "lineSize :size * 0.07 / :numPoints
	localmake "dotSize :lineSize * 2
	localmake "step :size / :numPoints
	localmake "points []
	repeat :numPoints [
		jumpForward :step
		queue2 "points pos
	]
	jumpTo :oldPos
	repeat :numPoints [
		setHeading :oldHeading + 60
		localmake "otherPoint item (:numPoints + 1 - repcount) :points
		jumpForward :step
		localmake "tempPos pos
		setPenSize :lineSize
		setPos :otherPoint
		setPenSize 0
		circle :dotSize
		jumpTo :tempPos
		circle :dotSize
	]
	jumpTo :oldPos
	circle :dotSize
	setHeading :oldHeading
end

to N5 :size
	localmake "oldState turtleState
	localmake "oldHeading heading
	localmake "oldPos pos
	localmake "oldPenColor penColor
	localmake "numDots 10
	localmake "size1 :size * 0.5
	localmake "size2 :size * cos 30
	
	setPenColor fillColor
	setPenSize :size * 0.01
	setFillColor transparent
	regularPolygon :size 6

	setHeading :oldHeading
	setFillColor penColor
	setPenColor :oldPenColor
	repeat 6 [
		triangleCurve :size1 :numDots
		right 180 / 6
		jumpForward :size2
		right 150
		triangleCurve :size1 :numDots
		left 60
		triangleCurve :size1 :numDots
		right 120
		triangleCurve :size1 :numDots
		jumpTo :oldPos
		setHeading :oldHeading + repcount * 360 / 6
	]

	setTurtleState :oldState
end

setPenColor "#f80 ; string/line color
setScreenColor "#ddd
drawBackground 150
setFillColor "#005 ; dot color
N5 100