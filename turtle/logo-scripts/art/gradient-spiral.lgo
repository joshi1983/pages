make "color1 "#D600D6
make "color2 "#00B3FF

to createForwardGradient :len :colorStops
	jumpBackward :len
	localmake "fromPos pos
	jumpForward :len * 2
	localmake "result createLinearGradient :fromPos pos :colorStops "pad
	jumpBackward :len
	output :result
end

; Inspired by
; https://www.pinterest.ca/pin/779122804299682521/
to circleSpiral :outerRadius
	localmake "oldState turtleState
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "numSides 7
	localmake "thickness :outerRadius * 0.5
	localmake "gradientAngle 4
	localmake "arcAngle 124
	localmake "colorStops createPList2 [
		[0 :color1] [1 :color2]
	]
	right 45
	jumpBackward :outerRadius
	localmake "fromPos pos
	jumpForward :outerRadius * 2
	setPenGradient createLinearGradient pos :fromPos :colorStops "pad
	setPenSize :thickness
	jumpTo :oldPos
	circle :outerRadius - :thickness * 0.5
	left 45
	localmake "colorStops createPList2 [
		[0 "#dfff] [0.49 "#0fff] [0.5 "#0000] [1 "black]
	]
	setPenSize 0
	setPenColor "black
	repeat :numSides [
		setHeading :oldHeading - :gradientAngle + repcount * 360 / :numSides
		jumpBackward :outerRadius * 0.25
		setFillGradient createForwardGradient :outerRadius * 0.8 :colorStops
		right :gradientAngle
		jumpTo :oldPos
		drawArcLineShape [0 0.5
			[[90 0] [:arcAngle 0.8] [14.3 0] [360 / :numSides 1]
			[165.5 0] [-:arcAngle 0.8] [-360 / :numSides 0.5]]
			] :outerRadius
	]
	setTurtleState :oldState
end

circleSpiral 100