; Inspired by
; https://www.pinterest.ca/pin/144889313002854876/

to curvedFlame :baseRadius
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "arcAngle 360 / 32
	localmake "size :baseRadius * 1.4
	left 90
	arcLeft :arcAngle :baseRadius
	right 180
	polyStart
	arcRight 2 * :arcAngle :baseRadius
	arcLines [[120 0] [-60 0.15] [90 0.15] [-80 0.12] [165 0]
		[80 0.13] [-200 0.03] [-50 0.08] [170 0] [90 0.08]
		[-80 0.05] [-100 0.03] [-70 0.08] [60 0.12] [-60 0.05]
		[165 0] [60 0.05] [-60 0.12] [70 0.13] [-60 0.07]
		[-150 0.05] [-40 0.1] [160 0] [90 0.1]] :size
	polyEnd
	jumpTo :oldPos
	setHeading :oldHeading
end

to sun :radius
	localmake "oldPos pos
	localmake "innerRadius :radius * 0.557
	localmake "flameBaseRadius :innerRadius * 1.05
	localmake "colorStops createPList2 [
		[0 "yellow] [1 "white]
	]
	setPenSize 0
	jumpBackward :innerRadius
	localmake "fromPos pos
	jumpForward :innerRadius * 2
	localmake "gradient createLinearGradient :fromPos pos :colorStops "pad 
	setFillGradient :gradient
	jumpTo :oldPos
	circle :innerRadius
	localmake "colorStops createPList2 [
		[0 "orange] [(:flameBaseRadius / :radius) "orange]
		[1 "red] 
	]
	localmake "gradient createRadialGradient pos :radius :colorStops
	setFillGradient :gradient
	repeat 16 [
		jumpForward :flameBaseRadius
		curvedFlame :flameBaseRadius
		right 360 / 16
		jumpTo :oldPos
	]
end

setScreenColor "black
sun 100