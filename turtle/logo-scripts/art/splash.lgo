; Inspired by
; https://www.pinterest.ca/pin/590604938648632531/

to streak :length :innerLength :thickness
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "halfThickness :thickness * 0.5
	localmake "outerLength :length - :innerLength
	localmake "outerLength1 :outerLength - :halfThickness
	localmake "angle1 arcTan :halfThickness / :outerLength1
	localmake "size1 :outerLength1 / cos :angle1
	localmake "squareSideLength :halfThickness / sin 45
	setPenSize 0
	jumpForward :innerLength
	right :angle1
	setFillColor "#ccc
	polyStart
	jumpForward :size1
	localmake "pos1 pos
	setHeading :oldHeading - 90
	jumpForward :halfThickness
	polyEnd
	setFillColor "#666
	polyStart
	jumpForward :halfThickness
	left 90 + :angle1
	jumpForward :size1
	polyEnd
	jumpTo :pos1
	setHeading :oldHeading - 45
	setFillColor "white
	polyStart
	repeat 3 [
		forward :squareSideLength
		left 90
	]
	polyEnd
	setPos :oldPos
	setHeading :oldHeading
end

to splash :radius :innerRadius
	localmake "numStreaks 16
	localmake "thickness :radius * 4.5 / :numStreaks
	repeat :numStreaks [
		streak :radius :innerRadius :thickness
		right 360 / :numStreaks
	]
end

setScreenColor "black
repeat 4 [
	splash 50 (repcount - 1) * 10
	right 90
	forward 120
	left 90
]