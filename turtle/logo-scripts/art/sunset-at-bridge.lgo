; Inspired by the logo at:
; https://www.pinterest.ca/pin/669488300869060188/
to sunsetGradient1 :radius
	localmake "oldPos pos
	jumpBackward :radius
	localmake "fromPos pos
	localmake "colorStops createPList2 [
		[0 "#931918] [0.3 "#c8401a] [0.6 "#c45e16]
		[0.8 "#e79d3c] [1 "#ffe290]
	]
	jumpForward :radius * 2
	localmake "result createLinearGradient :fromPos pos :colorStops "pad
	jumpTo :oldPos
	output :result
end

to sunsetGradientCircle :radius
	localmake "oldState turtleState
	localmake "gradients [
		sunsetGradient1 :radius
	]
	setPenSize 0
	repeat count :gradients [
		setFillGradient item repcount :gradients
		circle :radius
	]
	setTurtleState :oldState
end

to bridgeUpperRails :width :height
	localmake "oldState turtleState
	jumpLeft :width / 2
	localmake "pos1 pos
	localmake "oldHeading heading
	localmake "railThickness :height * 0.05
	localmake "postThickness :height * 0.2
	localmake "numPosts int (:width / (:height * 2)) 
	localmake "step1 :height / 4
	localmake "postSeparation :width / :numPosts
	setPenSize :railThickness
	jumpForward :height - :step1 * 0.5
	right 90
	repeat 3 [
		forward :width
		jumpBackward :width
		jumpRight :step1
	]
	jumpTo :pos1
	setHeading :oldHeading
	setPenSize :postThickness
	repeat :numPosts + 1 [
		forward :height
		jumpBackward :height
		jumpRight :postSeparation
	]
	setTurtleState :oldState
end

to bridgeSupports :size
	localmake "oldState turtleState
	localmake "oldHeading heading
	localmake "arcRadius :size * 0.6
	localmake "arcAngle1 45
	localmake "numSupports 14
	localmake "supportSeparation :size / :numSupports
	localmake "supportThickness :supportSeparation / 2
	setPenSize :size
	setLineCap "butt
	backward :size * 0.1
	jumpBackward :size * 0.01
	localmake "pos1 pos
	setPenSize 0
	left 90
	arcLeft :arcAngle1 :arcRadius
	right 180
	setPenSize :size * 0.05
	arcRight :arcAngle1 * 2 :arcRadius
	jumpTo :pos1
	setHeading :oldHeading
	jumpLeft :size / 2
	jumpForward :size * 0.03
	setPenSize :supportThickness
	repeat :numSupports + 1 [
		localmake "x (repcount - 1) * :supportSeparation - (:size / 2)
		localmake "supportHeight :size * 0.63 - sqrt :arcRadius * :arcRadius - :x * :x
		backward :supportHeight
		jumpForward :supportHeight
		jumpRight :supportSeparation	
	]
	setTurtleState :oldState
end

to bridge :width
	bridgeUpperRails :width :width * 0.05
	bridgeSupports :width
end

to woman :height
	localmake "oldState turtleState
	localmake "oldPos pos
	localmake "oldHeading heading
	jumpRight :height * 0.12
	setPenSize 0
	setFillColor "black
	; Draw head, hair and torso.
	drawArcLineShape [0 0
		[[0.3] [22 0.7] [-70 0.08] [0.04]
	[50 0] [0.04] [80 0.06] [-80 0.03] [0.13]
	[170 0.06] [-10 0.95] [-35 0] [90 0.07] [-60 0.18]
	[26 0.45] [0.16] [86 0.03] [0.02] [-90 0] [0.114586577]
	[91 0] [0.08] [90 0] [0.11] [-90 0] [0.05] [-90 0] [0.11]
	]] :height

	; draw the arms.
	jumpTo :oldPos
	setLineCap "round
	jumpForward :height * 0.72
	localmake "pos1 pos
	jumpRight :height * 0.095
	right 160
	setPenSize :height * 0.042
	arcRight 30 :height * 0.6
	jumpTo :pos1
	setHeading :oldHeading
	jumpLeft :height * 0.13
	left 165
	arcLeft 30 :height * 0.6
	setTurtleState :oldState
end

to man :height
	localmake "oldState turtleState
	localmake "oldHeading heading
	setPenSize 0
	setFillColor "black

	drawArcLineShape [0 0
		[[-1 0] [0.28] [-178 0] [0.28] [89 0]
	[0.1] [90 0] [0.35] [15 0.35] [-10 0.15] [-15 0.7]
	[-80 0.03] [95 0.015] [0.05] [80 0.05] [-13 0.2]
	[-30 0] [0.02] [-40 0] [0.04] [-120 0] [120 0.005]
	[0.04] [100 0.004] [-110 0] [205 0.054] [0.045]
	[50 0.005] [-53 0] [-10 0.2] [-90 0] [0.003] [60 0]
	[0.012] [-40 0] [20 0.25] [90 0.06] [-45 0.12]
	[-9 0.65] [12 0.2] [0.436]]
		] :height

	; draw arms
	jumpForward :height * 0.7
	localmake "pos1 pos
	jumpRight :height * 0.123
	right 170
	setPenSize :height * 0.045
	arcRight 40 :height * 0.5
	jumpTo :pos1
	setHeading :oldHeading
	jumpLeft :height * 0.12
	left 168
	setLineCap "round
	setPenSize penSize * 1.15
	forward :height * 0.15
	setPenSize penSize * 0.82
	forward :height * 0.18
	setTurtleState :oldState
end

to couple :manHeight
	localmake "separation :manHeight * 0.33
	jumpLeft :separation / 2
	woman :manHeight * 0.9
	jumpRight :separation
	man :manHeight
end

to bat1 :size
	localmake "oldState turtleState
	setPenSize 0
	setFillColor "black
	drawArcLineShape [0 0
		[[-40 0] [-50 0.5] [30 0] [-40 0.3] [160 0]
	[40 0.33] [-25 0] [40 0.5] [-70 0] [40 0.5]
	[-40 0] [50 0.3] [160 0] [-50 0.3] [45 0] [-43 0.6]
	]] :size
	setTurtleState :oldState
end

to bat2 :size
	localmake "oldState turtleState
	setPenSize 0
	setFillColor "black
	drawArcLineShape [0 0
		[[-70 0] [-70 0.45] [30 0] [-40 0.4] [150 0] [120 0.45]
	[-90 0] [120 0.45] [140 0] [-40 0.4] [45 0] [-70 0.41]]
		] :size
	setTurtleState :oldState
end

to randomBat :maxBatSize
	localmake "oldHeading heading
	localmake "minBatSize :maxBatSize * 0.5
	localmake "batSize mix :maxBatSize :minBatSize randomRatio
	localmake "angleRange 40
	localmake "rotation mix -:angleRange :angleRange randomRatio
	right :rotation
	ifelse randomRatio < 0.5 [
		bat1 :batSize
	] [
		bat2 :batSize
	]
	setHeading :oldHeading
end

to randomBatArcLeft :fromThickness :toThickness :arcRadius :angle :numBats :maxBatSize :levelHeading
	localmake "stepAngle :angle / :numBats
	setPenSize 0
	repeat :numBats [
		localmake "ratio repRatio
		localmake "thickness mix :toThickness :fromThickness  :ratio
		localmake "pos1 pos
		localmake "heading1 heading
		jumpLeft :thickness / 2
		jumpRight :thickness * randomRatio
		setHeading :levelHeading
		randomBat :maxBatSize
		setHeading :heading1
		jumpTo :pos1
		arcLeft :stepAngle :arcRadius
	]
end

to randomBatArcRight :fromThickness :toThickness :arcRadius :angle :numBats :maxBatSize :levelHeading
	localmake "stepAngle :angle / :numBats
	setPenSize 0
	repeat :numBats [
		localmake "ratio repRatio
		localmake "thickness mix :toThickness :fromThickness  :ratio
		localmake "pos1 pos
		localmake "heading1 heading
		jumpLeft :thickness / 2
		jumpRight :thickness * randomRatio
		setHeading :levelHeading
		randomBat :maxBatSize
		setHeading :heading1
		jumpTo :pos1
		arcRight :stepAngle :arcRadius
	]
end

to randomizedBats :size
	localmake "oldHeading heading
	localmake "maxBatSize :size * 0.03
	jumpForward :size * 0.4
	jumpLeft :size * 0.42
	right 150
	randomBatArcLeft :size * 0.5 :size * 0.15 :size * 0.35 75 190 :maxBatSize :oldHeading
	randomBatArcRight :size * 0.15 :size * 0.1 :size * 0.4 40 60 :maxBatSize :oldHeading
	randomBatArcRight :size * 0.1 :size * 0.1 :size * 0.23 210 100 :maxBatSize :oldHeading
end

to sunsetAtBridge :size
	localmake "oldState turtleState
	localmake "oldPos pos
	localmake "oldHeading heading
	sunsetGradientCircle :size * 0.5
	bridge :size
	jumpRight :size * 0.15
	couple :size * 0.09
	jumpTo :oldPos
	setHeading :oldHeading
	randomizedBats :size
	setTurtleState :oldState
end

setScreenColor "black
sunsetAtBridge 100