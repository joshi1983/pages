; draws a snowman with a smoking pipe, carot for nose, 
; and black buttons for nose.

; moves the turtle forward without drawing a trail
to penlessForward :len
	penup
	forward :len
	pendown
end

; size is length of the pipe
to drawPipe :size
	localmake "oldPos pos
	setpencolor "brown
	setpensize :size * 0.03
	pendown
	forward :size
	; draw the thicker end of the pipe.
	setpensize :size * 0.33
	back :size * 0.15
	penup
	setpos :oldPos ; restore original position.
end

; size represents radius of the mouth's arc
to drawMouth :size
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "smileAngle 90
	localmake "radius :size * 0.1
	setfillcolor "black
	setpensize 0
	left :smileAngle / 2
	; draw black spots to mark the lips
	repeat 5 [
		forward :size
		circle :radius
		setpos :oldPos
		right :smileAngle / 5
	]
	setHeading :oldHeading - :smileAngle * 0.2
	forward :size
	left 60
	drawPipe :size * 2
	; restore turtle to original position and heading
	setheading :oldHeading
	setpos :oldPos
end

to drawArmBranch :size
	localmake "oldPos pos
	localmake "oldHeading heading
	setpencolor "#620
	setpensize :size * 0.04
	pendown
	right 6
	forward :size * 0.3
	left 8
	forward :size * 0.64
	setpensize :size * 0.03
	right 60
	forward :size * 0.09
	back :size * 0.09
	left 90
	forward :size * 0.07	
	penup
	; restore turtle to original position and heading
	setpos :oldPos
	setHeading :oldHeading
end

; draws a snowman in the turtle's current direction
; size is roughly the height of the snowman from bottom to top of the hat
to drawSnowMan :size
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "eyeSeparation :size * 0.11
	localmake "eyeSize :size * 0.017

	setfillcolor "white
	setpencolor "PowderBlue
	setpensize :size * 0.003
	penlessForward :size * 0.159
	circle :size * 0.26
	penlessForward :size * 0.35
	circle :size * 0.2
	penlessForward :size * 0.26
	circle :size * 0.13
	setpensize 0

	; draw an orange carot for a nose.
	setfillcolor "orange
	setpencolor "brown
	setpensize :size * 0.002
	circle :size * 0.023
	right 180
	drawMouth :size * 0.065
	right 180

	setpensize 0
	forward :size * 0.04
	right 90
	forward :eyeSeparation / 2
	; draw the eyes.
	setfillcolor "black
	circle :eyeSize
	back :eyeSeparation
	circle :eyeSize
	forward :eyeSeparation / 2
	left 90
	forward :size * 0.06

	; draw the hat.
	pendown
	setpencolor "black
	setpensize :size * 0.3
	forward :size * 0.02
	setpencolor "darkGray
	forward :size * 0.001
	setpencolor "black
	setpensize :size * 0.15
	forward :size * 0.11
	penup

	; draw arms.
	setpos :oldPos
	setheading :oldHeading
	forward :size * 0.5
	right 103
	forward :size * 0.18
	drawArmBranch :size * 0.3
	back :size * 0.375
	right 150
	drawArmBranch :size * 0.28

	setPos :oldPos
	setHeading :oldHeading
end

setScreenColor "SkyBlue
drawSnowMan 100
