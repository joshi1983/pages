; Inspired by drawing at:
; https://ca.pinterest.com/pin/443393525835169842
;
; In case that link breaks,
; it was shared by pinterest user Happy Happy James
; https://ca.pinterest.com/happyhappyjames
to spunForDays :height
	localmake "oldState turtleState
	localmake "oldHeading heading
	localmake "colors ["aqua "blue "blue "violet "red]
	localmake "numRadii 15
	jumpForward :height / 2
	localmake "center pos
	setPenSize 0
	setFillColor first :colors
	roundRegularStar :height * 0.032 12 :height * 0 :height * 0.02
	setPenSize :height * 0.005
	repeat :numRadii [
		localmake "radius :height * mix 0.4903 0.05 repRatio
		localmake "spinAngle mix 70 25 power repRatio 1.5
		localmake "radius2 :radius * mix 0.03 0.07 repRatio
		localmake "arcAngle mix 30 30 repRatio
		setPenColor mixItems2 :colors repRatio
		setFillColor penColor
		repeat 6 [
			setHeading :oldHeading - :spinAngle + repcount * 360 / 6
			jumpBackward :radius
			circle :radius2
			right 90
			arcLeft :arcAngle :radius
			circle :radius2

			jumpTo :center
		]
	]
	setTurtleState :oldState
end

setScreenColor "black
spunForDays 100