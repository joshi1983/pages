; Inspired by
; https://www.pinterest.ca/pin/147985537743650440/

make "arcs1 [[-40 0.7] [30 1.005]]

to flamePath :size
	localmake "shape [0 0
		(combine flipAngles :arcs1 [[-172 0]] reverse :arcs1)
	]
	drawArcLineShape :shape :size
end

to flamePath2 :size
	localmake "shape [0 0
		(combine :arcs1 [[-170 0]] reverse flipAngles :arcs1)
	]
	drawArcLineShape :shape :size
end

to scaleRatio :ratio1 :brightestRatio :deltaRatio
	localmake "ratio2 :ratio1 * :deltaRatio
	localmake "ratio3 (:ratio1 - :brightestRatio) / (1 - :brightestRatio)
	output mix :ratio1 :ratio2 :ratio3
end

to ringOfFire :size
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "size :size * 1.075
	localmake "colors ["black "black "#f00 "#ff6 "#fff "#ff6 "#f00 "#000]
	localmake "ratio 0.85
	localmake "radius1 :size * :ratio
	localmake "radius2 :size
	localmake "brightestRadius 0.86
	localmake "colorStops createPList2 [
		[0 item 1 :colors] [0.75 item 2 :colors]
		[0.8 item 3 :colors] [0.85 item 4 :colors]
		[:brightestRadius item 5 :colors]
		[0.87 item 6 :colors] [0.92 item 7 :colors]
		[1 item 8 :colors]
	]
	setFillGradient createRadialGradient pos :radius1 :colorStops
	setPenSize 0
	circle :radius1
	localmake "colorStops createPList2 [
		[0 item 1 :colors] [0.75 * :ratio item 2 :colors]
		[0.8 * :ratio item 3 :colors] [0.85 * :ratio item 4 :colors]
		[:brightestRadius * :ratio item 5 :colors]
		[(scaleRatio 0.86 :brightestRadius :ratio) item 5 :colors]
		[(scaleRatio 0.87 :brightestRadius :ratio) item 6 :colors]
		[(scaleRatio 0.92 :brightestRadius :ratio) item 7 :colors]
		[1 item 8 :colors]
	]
	localmake "gradient createRadialGradient pos :radius2 :colorStops
	setFillGradient :gradient
	localmake "numFlames 64
	repeat :numFlames [
		jumpForward :brightestRadius * :ratio * :size
		left 5
		flamePath :size * (0.12 + 0.08 * (remainder repcount 2))
		right 15
		if 0 = remainder repcount 3 [
			flamePath2 :size * 0.22
		]
		right 15
		flamePath :size * 0.2
		jumpTo :oldPos
		setHeading :oldHeading + repcount * 360 / :numFlames
	]
end

setScreenColor "black
ringOfFire 200