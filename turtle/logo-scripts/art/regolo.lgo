; Inspired by
; https://www.pinterest.ca/pin/326440673000319768

to drawGradient :radius
	localmake "colorStops createPList
	setProperty "colorStops 0 "black
	setProperty "colorStops 0.02 "black
	setProperty "colorStops 0.05 "white
	setProperty "colorStops 0.6 "white
	setProperty "colorStops 1 screenColor
	localmake "gradient createRadialGradient pos pos :radius :colorStops "pad
	setFillGradient :gradient
	setPenSize 0
	circle :radius
end

to scaleRotatePoints :points :singlePoint :maxRotation :numScales
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "numPoints count :points
	repeat :numScales [
		localmake "ratio repRatio
		localmake "rotationRatio (sqrt abs (:ratio - 0.2)) / sqrt 0.8
		localmake "rotation (1 - :rotationRatio) * :maxRotation
		localmake "previousPoint []
		localmake "firstPoint []
		repeat :numPoints [
			localmake "point item repcount :points
			jumpTo :singlePoint
			setHeading :rotation + towards :point
			jumpForward :ratio * distance :point
			localmake "newPoint pos
			ifelse (count :previousPoint) <> 0 [
				setHeading towards :previousPoint
				
				localmake "x distance :previousPoint
				if :x <> 0 [
					forward :x
				]
			] [
				localmake "firstPoint pos
			]
			localmake "previousPoint :newPoint
		]
		jumpTo :newPoint
		setHeading towards :firstPoint
		forward distance :firstPoint
	]
	jumpTo :oldPos
	setHeading :oldHeading
end

to regolo :size
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "colors ["orange "red "maroon "#b06 
		"purple "#06f "blue "navy "green "limegreen]
	localmake "numColors count :colors
	localmake "size1 :size * 0.8
	localmake "thickOutlineSize :size * 0.02
	drawGradient :size * 1.3
	setPenSize :size * 0.003
	repeat :numColors [
		setPenColor item repcount :colors
		jumpTo :oldPos
		jumpForward :size
		localmake "points [:oldPos pos]
		jumpTo :oldPos
		right 360 / :numColors
		jumpForward :size1
		queue "points pos
		left 180 / :numColors
		jumpTo :oldPos
		jumpForward :size * 0.6
		scaleRotatePoints :points pos 50 40
		setHeading :oldHeading + repcount * 360 / :numColors
	]
	setPenColor "black
	repeat :numColors [
		jumpTo :oldPos
		setPenSize :size * 0.012
		forward :size
		localmake "p1 pos
		jumpTo :oldPos
		right 360 / :numColors
		jumpForward :size1
		setHeading towards :p1
		setPenSize :thickOutlineSize
		forward distance :p1
		setHeading :oldHeading + repcount * 360 / :numColors
	]
	; add black circles to polish the corners.
	setPenSize 0
	setFillColor "black
	repeat :numColors [
		setHeading :oldHeading + repcount * 360 / :numColors
		jumpTo :oldPos
		jumpForward :size
		circle :thickOutlineSize * 0.5
	]
	jumpTo :oldPos
	setHeading :oldHeading
end

setScreenColor "#ccc
regolo 100