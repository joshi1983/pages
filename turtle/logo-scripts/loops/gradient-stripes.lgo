
make "colors ["#f00 "#ff0 "#0f0 "#0ff "#00f "#f0f]

to shadedParallelogram :angle :width :length
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "colorStops createPList2 [
		[0 "#b000] [0.1 "#6000] [0.2 "#3000] [0.3 "#1000]
		[0.5 "#0000] [1 "#0000]
	]
	right 90 + :angle
	jumpForward :width / cos :angle
	setFillGradient createLinearGradient :oldPos pos :colorStops "pad
	setHeading :oldHeading
	jumpTo :oldPos
	jumpRight :width / 2
	parallelogram :width :length 180 + :angle
end

to colourStripes :width :height
	localmake "oldHeading heading
	localmake "numStripes max 3 3 * :width / :height
	localmake "step :width / :numStripes
	localmake "angle 30
	localmake "slantLength :height / cos :angle
	localmake "numStripesPerCycle count :colors
	jumpLeft :width / 2
	setPenSize 0
	repeat :numStripes [
		ifelse even? repcount [
			ifelse even? repcount / 2 [
				setFillColor "#444
			] [
				setFillColor "white
			]
		] [
			localmake "ratio (remainder repcount / 2 :numStripesPerCycle) / :numStripesPerCycle
			setFillColor mixItems :colors :ratio
		]
		localmake "pos1 pos
		polyStart
		jumpRight :step
		right :angle
		jumpForward :slantLength
		setHeading :oldHeading
		jumpLeft :step
		polyEnd
		shadedParallelogram :angle :step :slantLength

		jumpTo :pos1
		jumpRight :step
	]
end

setScreenColor "#eee
colourStripes 1000 100