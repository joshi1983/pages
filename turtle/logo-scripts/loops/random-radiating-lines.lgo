; Inspired by:
; https://www.pinterest.ca/pin/703756184761950

to randomBrightColor
	localmake "result []
	repeat 3 [
		queue2 "result 100 + random 155 
	]
	output :result
end

to randomRadiatingLines :width :height
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "pos1 [
		:width * (random 1000) / 1000
		:height * (random 1000) / 1000
	]

	; convert pos1 into a position that respects 
	; the current turtle position and heading.
	jumpLeft item 1 :pos1
	jumpForward item 2 :pos1
	localmake "pos1 pos
	localmake "divisionSize (:width + :height) / 2 / 200
	setPenSize :divisionSize * 0.2
	repeat 5000 [
		localmake "x :width * (random 1000) / 1000
		localmake "y :height * (random 1000) / 1000
		jumpTo :oldPos
		jumpLeft :x
		jumpForward :y
		setHeading towards :pos1
		setPenColor randomBrightColor
		forward :divisionSize * 5
		setHeading :oldHeading
	]
	jumpTo :oldPos
end

setScreenColor "black
randomRadiatingLines 800 100