to jamaicaFlagTriangles :height :width
	localmake "oldState turtleState
	localmake "greenColor "#007749
	localmake "crossThickness :height / 6
	localmake "angle arcTan :height / :width
	localmake "width1 :width - 2 * :crossThickness / cos :angle
	localmake "width2 :width1 / 2
	localmake "height2 :height - :crossThickness / cos :angle
	localmake "height1 :width1 / 2 * tan :angle
	setFillColor :greenColor
	isoTriangle :width1 :height1
	setFillColor "black
	jumpLeft :width / 2
	jumpForward :height / 2
	right 90
	isoTriangle :height2 :width2
	setTurtleState :oldState
end

; Mostly implemented by following instruction sheet at:
; https://en.wikipedia.org/wiki/Flag_of_Jamaica
to jamaicaFlagCustomAspectRatio :height :width
	localmake "oldState turtleState
	localmake "orangeColor "#ffb81c
	setLineCap "butt
	setPenSize :width
	setPenColor :orangeColor
	forward :height
	setPenSize 0
	right 180
	jamaicaFlagTriangles :height :width
	jumpForward :height
	left 180
	jamaicaFlagTriangles :height :width
	setTurtleState :oldState
end

; Inspired by
; https://en.wikipedia.org/wiki/Flag_of_Jamaica#/media/File:Flag_of_Jamaica.svg
to jamaicaFlag :height
	jamaicaFlagCustomAspectRatio :height :height * 2
end

; Inspired by
; https://en.wikipedia.org/wiki/Flag_of_Jamaica#/media/File:Naval_Ensign_of_Jamaica.svg
to jamaicaNavalEnsignia :height
	localmake "oldState turtleState
	localmake "width :height * 2
	localmake "crossThickness :height * 2 / 15
	localmake "subflagWidth (:width - :crossThickness) / 2
	setLineCap "butt
	setPenColor "white
	setPenSize :width
	forward :height
	setPenSize :crossThickness
	setPenColor "#C8102E
	backward :height
	jumpForward :height / 2
	jumpRight :width / 2
	left 90
	forward :width
	jumpBackward :subflagWidth / 2
	right 90
	jumpForward :crossThickness / 2
	jamaicaFlagCustomAspectRatio (:height - :crossThickness) / 2 :subflagWidth 
	setTurtleState :oldState
end

make "height 100
make "separation :height * 6
jumpLeft :separation / 2
jamaicaFlag :height
jumpRight :separation
jamaicaNavalEnsignia :height