; Inspired by a flag at:
; https://youtu.be/egy0k5I6jnY?t=658
;
; Another version of their flag is at:
; https://en.wikipedia.org/wiki/Principality_of_Outer_Baldonia#/media/File:Flag_of_Outer_Baldonia.svg
to baldoniaFlag :height
	localmake "oldState turtleState
	localmake "width :height * 3 / 2
	localmake "radius :height * 2 / 5
	jumpForward :height / 2
	setPenSize 0
	setFillColor "#068C72
	rect :width :height
	setFillColor "white
	circle :radius
	right 45
	jumpForward :radius
	localmake "startPos pos
	setFillColor "#547aba
	polyStart
	arcLines [[-90 0] [-40 1] [-120 0] [42 0.45] [60 0.07] [-130 0]
		[100 0.12] [-62 0] [0.14] [120 0.05] [-32 1.3] [-135 0] [-10 2]
		[35 1] [20 0.2] [18 1.5] [-150 0] [-22 1] [-30 0.6] [-5 3]
		[90 0.07] [0.07] [-40 0]  [50 0.13] [22 0.2]] :radius

	; Arc toward the starting point.
	localmake "finalRadius 0.7 * :radius
	localmake "arcAngle 2 * arcSin  (distance :startPos) / 2 / :finalRadius
	setHeading (towards :startPos) + :arcAngle / 2
	arcLeft :arcAngle :finalRadius
	polyEnd
	
	setTurtleState :oldState
end

baldoniaFlag 100