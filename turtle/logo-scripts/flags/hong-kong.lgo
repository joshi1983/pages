; Inspired by the logo at:
; https://upload.wikimedia.org/wikipedia/commons/5/5b/Flag_of_Hong_Kong.svg
; Referenced from:
; https://en.wikipedia.org/wiki/Hong_Kong

to star :size
	localmake "oldState turtleState
	localmake "scale (cos 18) + (sin 18) / (tan 180 / 5)
	jumpForward :size * :scale
	right 72 / 4
	polyStart
	repeat 5 [
		right 144
		jumpForward :size
		left 72
		jumpForward :size
	]
	polyEnd
	setTurtleState :oldState
end

to leaf :size
	localmake "oldState turtleState
	jumpForward :size * 0.0143
	jumpLeft :size * 0.0176
	left 48.1
	setPenSize 0
	polyStart
	arcRight 115 :size * 0.219
	arcLeft 99.8 :size * 0.26
	arcRight 105.8 :size * 0.1565
	left 154
	arcLeft 167 :size * 0.5135
	left 165.5
	arcRight 87.55 :size * 0.399
	right 99
	jumpForward :size * 0.0085
	right 81
	arcLeft 88 :size * 0.389
	polyEnd

	setTurtleState :oldState
end

to hongKongFlag :height
	localmake "oldHeading heading
	localmake "leafSize :height * 0.2931
	localmake "starSize :leafSize * 0.065
	localmake "width :height * 3 / 2
	localmake "flagRed "#ee1c25
	jumpForward :height / 2
	right 90
	jumpBackward :width / 2
	setPenColor :flagRed
	setPenSize :height
	forward :width
	jumpBackward :width / 2
	left 90
	localmake "pos1 pos
	setPenSize 0
	repeat 5 [
		setHeading :oldHeading + repcount * 360 / 5
		setFillColor "white
		leaf :leafSize
		setFillColor :flagRed
		jumpForward :leafSize * 0.6165
		jumpLeft :leafSize * 0.056
		left 23.5
		star :starSize
		jumpTo :pos1
	]
end

hongKongFlag 100
