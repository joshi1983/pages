; Made to look like:
; https://en.wikipedia.org/wiki/Union_Jack#/media/File:Flag_of_the_United_Kingdom.svg

make "blue "#012169
make "red "#C8102E
make "crossAngle arctan 0.5

to drawNarrowStrip :width :angle
	localmake "width :width * 0.5 * (1 - 5 / 30 / tan :angle)
	localmake "sizePrime :width * 3 / 30
	localmake "oldPos pos
	localmake "oldHeading heading

	localmake "size2 :sizePrime / sin :angle
	localmake "size3 (:width - :size2) / cos :angle
	localmake "size4 :sizePrime / cos :angle
	setpensize 0
	setfillcolor :red
	polystart
	forward :size2
	right :angle
	forward :size3
	right 90 - :angle
	forward :size4
	right 90 + :angle
	
	forward distance :oldPos
	polyend
	setPos :oldPos
	setHeading :oldHeading
end

to drawNarrowStrip2 :width :angle
	localmake "width :width * 0.5 * (1 - 5 / 30 / tan :angle)
	localmake "sizePrime :width * 3 / 30
	localmake "oldPos pos
	localmake "oldHeading heading

	localmake "size2 :sizePrime / sin :angle
	localmake "size3 :width / cos :angle
	setpensize 0
	setfillcolor :red
	polystart
	right 180 - :angle
	forward :size3
	left 180 - :angle

	forward :size2
	left :angle
	forward :size3
	polyend
	setPos :oldPos
	setHeading :oldHeading
end


to drawV :width
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "angle 90 - :crossAngle
	localmake "size1 :width * 0.5 / sin :angle
	right :angle
	pendown
	forward :size1
	setPos :oldPos
	left :angle * 2
	forward :size1
	penup
	setPos :oldPos
	setHeading :oldHeading
end

to unionJack :width
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "height :width * 0.5 
	localmake "size1 :height * 10 / 30
	localmake "size2 :height * 6 / 30
	setpencolor :blue
	right 90
	penup
	back :width / 2
	pendown
	setpensize :height
	forward :width
	setpensize :size1
	setpencolor "white
	back :width
	penup
	setPos :oldPos
	left 90
	forward :height * 0.5
	pendown
	back :height
	penup
	setPos :oldPos
	setpensize :size2
	whiteCross :width
	left 90
	back :width / 2
	pendown
	setpencolor :red
	setpensize :size2
	forward :width
	penup
	back :width / 2
	right 90
	forward :height / 2
	pendown
	back :height
	penup
	setPos :oldPos
	setHeading :oldHeading
	fd :size1 * 0.5
	left 90
	forward :size1
	drawNarrowStrip :width :crossAngle

	setHeading :oldHeading
	back :size1
	right 90
	drawNarrowStrip2 :width :crossAngle

	setPos :oldPos
	setHeading :oldHeading
	back :size1 * 0.5
	right 90
	forward :size1
	drawNarrowStrip :width :crossAngle

	left 90
	forward :size1
	left 90
	drawNarrowStrip2 :width :crossAngle

	setPos :oldPos
	setHeading :oldHeading
end

to whiteCross :width
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "height :width * 0.5
	localmake "s :width * 4 / 30
	localmake "w :s * cos :crossAngle
	localmake "h :s * sin :crossAngle
	localmake "size1 (:width * 0.5 - :w) / cos :crossAngle
	penup
	setpensize 1
	setpencolor "black
	fd :height / 2
	right 90
	fd :width / 2 - :w
	setfillcolor "white
	setpensize 0
	polystart

	repeat 2 [
		fd :w
		right 90
		fd :h
		right 90 - :crossAngle
		fd :size1
		left 180 - (:crossAngle * 2)
		fd :size1
		right 90 - :crossAngle
		fd :h
		right 90
		fd :w
		right :crossAngle
		fd :size1
		left :crossAngle * 2
		fd :size1
		right :crossAngle
	]

	polyend
	setPos :oldPos
	setHeading :oldHeading
end

setscreencolor "black
unionJack 300