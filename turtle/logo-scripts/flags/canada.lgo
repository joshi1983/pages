to drawLeaf :height
	localmake "size1 :height * 0.038
	localmake "size2 :height * 0.22
	localmake "size3 :height * 0.225
	localmake "size4 :height * 0.09
	localmake "size5 :height * 0.305
	localmake "size6 :height * 0.064
	localmake "size7 :height * 0.154
	localmake "size8 :height * 0.14
	localmake "size9 :height * 0.15
	localmake "size10 :height * 0.26
	localmake "size11 :height * 0.095
	localmake "size12 :height * 0.1785308
	localmake "smallCornerRadius :height * 0.01
	localmake "largeCornerRadius :height * 0.018
	right 90
	jumpForward :size1 * 0.5
	setPenSize 0
	polyStart
	left 92
	jumpForward :size2
	arcRight 102 :largeCornerRadius
	jumpForward :size3
	left 120
	jumpForward :size4
	arcRight 70.5 :smallCornerRadius
	jumpForward :size5
	left 117
	jumpForward :size6
	arcRight 85 :smallCornerRadius
	jumpForward :size7
	left 119
	jumpForward :size8
	arcRight 77 :smallCornerRadius
	jumpForward :size6
	left 113
	jumpForward :size9
	arcRight 147 :largeCornerRadius
	jumpForward :size10
	left 128
	jumpForward :size11
	arcRight 88 :smallCornerRadius
	jumpForward :size12
	; This is the point of symmetry in the flag.
	left 180 - 29.5 * 2
	jumpForward :size12
	arcRight 88 :smallCornerRadius
	jumpForward :size11
	left 128
	jumpForward :size10
	arcRight 147 :largeCornerRadius
	jumpForward :size9
	left 113
	jumpForward :size6
	arcRight 77 :smallCornerRadius
	jumpForward :size8
	left 119
	jumpForward :size7
	arcRight 85 :smallCornerRadius
	jumpForward :size6
	left 117
	jumpForward :size5
	arcRight 70.5 :smallCornerRadius
	jumpForward :size4
	left 120
	jumpForward :size3
	arcRight 102 :largeCornerRadius
	jumpForward :size2
	left 92
	jumpForward :size1
	polyEnd
end

; Canadian flag
; made to look like:
; https://upload.wikimedia.org/wikipedia/commons/d/d9/Flag_of_Canada_%28Pantone%29.svg
to canadaFlag :height
	localmake "oldState turtleState
	localmake "width :height * 2
	localmake "redColor "#d52b1e
	jumpForward :height / 2
	localmake "center pos
	right 90
	jumpBackward :width * 0.5
	setPenColor :redColor
	setPenSize :height
	forward :width * 0.25
	setPenColor "white
	forward :width * 0.5
	setPenColor :redColor
	forward :width * 0.25
	jumpTo :center
	left 90
	jumpBackward :height * (0.5 - 0.08)
	setFillColor :redColor
	drawLeaf :height * 0.836
	setTurtleState :oldState
end

; Inspired by
; https://commons.wikimedia.org/wiki/File:Flag_of_Acadia.svg
to acadiaFlag :height
	localmake "oldState turtleState
	localmake "width :height * 3 / 2
	localmake "colors ["#013896 "#fff "#cf142b]
	localmake "starSize1 :height * 0.185
	right 90
	jumpLeft :height / 2
	jumpBackward :width / 2
	setPenSize :height
	setLineCap "butt
	repeat count :colors [
		setPenColor item repcount :colors
		forward :width / count :colors
	]
	setFillColor "gold
	setPenSize 0
	jumpBackward :width * 5 / 6
	jumpLeft :height / 5
	left 90
	regularStar :starSize1 5
	setTurtleState :oldState
end

make "height 100
make "separation :height * 4
jumpLeft :separation * 0.5
canadaFlag :height
jumpRight :separation
acadiaFlag :height