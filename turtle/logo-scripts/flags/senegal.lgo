to star :size
	localmake "angle1 180 - 36/2
	localmake "angle2 90 - 36/2
	localmake "angle3 180 - 36
	localmake "size1 :size
	jumpForward :size * 1.38
	right :angle1
	setPenSize 0
	polyStart
	repeat 5 [
		jumpForward :size1
		left :angle2
		jumpForward :size1
		right :angle3
	]
	polyEnd
end

; Made to look like the flag of Senegal
; https://en.wikipedia.org/wiki/Flag_of_Senegal#/media/File:Flag_of_Senegal.svg
to senegalFlag :width
	localmake "oldState turtleState
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "height :width * 800 / 1200
	localmake "colors ["#0b7226 "#ffff00 "#bc0000]
	setPenSize :height
	setLineCap "butt
	right 90
	jumpBackward :width / 2
	repeat 3 [
		setPenColor item repcount :colors
		forward :width / 3
	]
	jumpTo :oldPos
	setFillColor item 1 :colors
	setHeading :oldHeading
	star :height * 0.12
	setTurtleState :oldState
end

senegalFlag 200