; Follows guidelines at
; https://en.wikipedia.org/wiki/Flag_of_North_Macedonia

to drawV :width :thickness :height
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "len1 hypot [:width * 0.5 :height]
	localmake "len2 hypot [(:width * 0.5 - :thickness) :height]
	localmake "angle1 arcSin :height / :len1
	localmake "angle2 180 - :angle1
	localmake "angle3 arcSin :height / :len2
	localmake "angle4 90 - :angle3
	left 90 - :angle1
	setPenSize 0
	polyStart
	jumpForward :len1
	right :angle2
	jumpForward :thickness
	right :angle3
	polyEnd

	jumpTo :oldPos
	setHeading :oldHeading + :angle4
	polyStart
	jumpForward :len2
	right :angle3
	jumpForward :thickness
	right :angle2
	polyEnd

	setHeading :oldHeading
	jumpTo :oldPos
end

to drawCross :width :thickness :height
	repeat 2 [
		drawV :width :thickness :height * 0.5
		right 180
	]
end

to northMacedoniaFlag :height
	localmake "oldState turtleState
	localmake "length :height * 2
	localmake "rayWidth1 :height * 0.2
	localmake "d :height * 2 / 7
	localmake "circlePenSize :d / 8
	localmake "redColor "#d91b21
	localmake "yellowColor "#f8ea2a
	stripes :length :height [:redColor]
	jumpForward :height / 2
	localmake "center pos
	right 90
	jumpBackward :length / 2
	setFillColor :yellowColor
	setPenSize 0
	isoTriangle :rayWidth1 :length * 0.5
	jumpForward :length
	right 180
	isoTriangle :rayWidth1 :length * 0.5
	jumpForward :length * 0.5
	right 90
	jumpForward :height * 0.5
	right 180
	isoTriangle :rayWidth1 :height * 0.5
	jumpForward :height
	right 180
	isoTriangle :rayWidth1 :height * 0.5
	jumpTo :center
	drawCross :length :height * 0.3 :height

	jumpTo :center
	setPenSize :circlePenSize
	setPenColor :redColor
	circle :d / 2 + :circlePenSize
	setTurtleState :oldState
end

; Inspired by flag at
; https://en.wikipedia.org/wiki/Flag_of_North_Macedonia#/media/File:Flag_of_Macedonia_(1944%E2%80%931946).svg
to peoplesRepublicMacedoniaFlag :height
	localmake "oldState turtleState
	localmake "width :height * 2
	localmake "redColor "#D00
	stripes :width :height [:redColor]
	jumpForward :height * 0.477
	setPenColor "#FFD700
	setPenSize :height * 0.03
	setLineJoinStyle "miter
	setFillColor transparent
	isotoxalStar :height * 0.263 :height * 0.132 5
	setTurtleState :oldState
end

setScreenColor "black
make "height 100
make "separation :height * 5
jumpLeft :separation / 2
northMacedoniaFlag :height
jumpRight :separation
peoplesRepublicMacedoniaFlag :height