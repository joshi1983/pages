make "kenyaRed "#b00

to drawSpear :length
	localmake "oldState turtleState
	localmake "arcsInfo [[90 0] [0.0105] [-90 0] [0.7735] [45 0]
	[-40 0.05] [-24.716 0.45519] [-70.284 0]]
	jumpBackward :length * 0.4623
	setLineJoinStyle "round
	setPenColor "black
	setPenSize :length * 0.0045
	setFillColor "white
	jumpForward penSize / 2
	polyStart
	arcLines :arcsInfo :length
	arcLines reverse :arcsInfo :length
	polyEnd
	setTurtleState :oldState
end

to drawSpears :length
	localmake "oldState turtleState
	localmake "angle 30
	left :angle
	drawSpear :length
	right :angle * 2
	drawSpear :length
	setTurtleState :oldState
end

to drawShield :height
	localmake "oldState turtleState
	localmake "center pos
	localmake "oldHeading heading
	localmake "topRadiusRatio1 0.055
	localmake "sideRadiusRatio 0.6762316
	localmake "arcsInfo1 [[33 :topRadiusRatio1] [16 0.26]
	[41 :sideRadiusRatio]
	]
	localmake "arcsInfo combine :arcsInfo1 reverse :arcsInfo1
	localmake "whiteHeight :height * 0.4385
	localmake "xGap :height * 0.021
	localmake "r :height * :topRadiusRatio1
	localmake "yOffset :r - sqrt :r * :r - :xGap * :xGap / 4
	localmake "yGap (:height - :whiteHeight) / 2 - :yOffset
	localmake "whiteArcAngle 42.5
	localmake "whiteArcRadius :whiteHeight / 2 / sin :whiteArcAngle / 2
	jumpBackward :height / 2
	right 90
	setFillColor :kenyaRed
	setPenSize 0
	polyStart
	arcLines :arcsInfo :height
	arcLines :arcsInfo :height
	polyEnd
	jumpTo :center
	setHeading :oldHeading
	setFillColor "white
	ellipse :height * 0.0416666 :height * 0.0625
	repeat 2 [
		localmake "sign sign repcount - 1.5
		jumpForward :yGap * :sign
		jumpLeft :xGap / 2
		repeat 2 [
			localmake "pos1 pos
			localmake "sign2 sign repcount - 1.5
			jumpBackward :whiteHeight / 2
			right :sign2 * :whiteArcAngle / 2
			polyStart
			arcLeft :sign2 * :whiteArcAngle :whiteArcRadius
			polyEnd
			setHeading :oldHeading
			jumpTo :pos1
			jumpRight :xGap
		]
		jumpTo :center
	]
	localmake "blackArcRadius :height * :sideRadiusRatio
	localmake "blackHeight :height / 2
	localmake "blackAngle 2 * arcSin :blackHeight / 2 / :blackArcRadius
	localmake "separation :height * 0.405
	setFillColor "black
	jumpLeft :separation / 2
	jumpBackward :blackHeight / 2
	repeat 2 [
		left :blackAngle / 2
		polyStart
		arcRight :blackAngle :blackArcRadius
		right 180 - :blackAngle
		arcRight :blackAngle :blackArcRadius
		polyEnd
		setHeading :oldHeading
		jumpRight :separation
	]
	setTurtleState :oldState
end

; Inspired by
; https://en.wikipedia.org/wiki/Flag_of_Cape_Verde#/media/File:Flag_of_Cape_Verde.svg
to kenyaFlag :height
	localmake "oldState turtleState
	localmake "oldHeading heading
	localmake "width :height * 3 / 2
	stripes :width :height ["#060 :kenyaRed "black]
	jumpForward :height / 2
	localmake "center pos
	repeat 2 [
		setHeading :oldHeading + 180 * repcount
		jumpForward :height * 0.15
		stripes :width :height * 0.05 ["white]
		jumpTo :center
	]
	localmake "spearLength :height * 0.756
	drawSpears :spearLength
	drawShield :height * 0.6
	setTurtleState :oldState
end

setScreenColor "#ddd
kenyaFlag 100