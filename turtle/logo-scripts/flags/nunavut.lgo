to drawRocks :height
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "rocksInfo [
		; lowest rock
		[0 0.005 [
			[-90 0] [2 5]
			[93 0.055]
			[5 1.4] [80 0.04] [14 1]
			[30 0.06] [50 0.13] [30 0.02]
			[24 0.2]
		]
		]
		; second from bottom rock
		[-0.076 0.219 [
			[-170 0] [80 0.03] [11 0.8] [-20 0.1]
			[20 0.17] [72 0.02] [12 0.4] [90 0.03]
			[-20 0.23] [18 0.5] [86 0.04]
			[10 0.5]
			]
		]
		; third rock from the bottom
		[0.1809 0.3469 [
			[10 0] [-9 0.7] [15 0.15] [73 0.05]
			[10 1] [72 0.05] [17 0.25] [83 0.03]
			[-10 0.4] [-15 0.3] [30 0.15]
			]
		]
		; horizontal rock
		[-0.3195 0.6633
			[
			[-90.5 0] [3.5 10] [2.9 1.8] [-104 0.02]
			[-33 0.2] [-45.3 0.07] [-3.05 10] [-72 0.07]
			[10 0.522] [-110 0.012]
			]
		]
		; top rock
		[0 0.994 [
			[90 0] [42 0.02] [11 0.5] [42 0.04]
			[8 1.45] [70 0.02] [21 0.32] [68 0.01]
			[10 1.22] [40 0.02] [12 0.5]
			]
		]
	]
	setPenSize :height * 0.013
	setLineCap "round
	setLineJoinStyle "round
	setPenColor "black
	setFillColor "#d51516
	repeat count :rocksInfo [
		localmake "rockInfo item repcount :rocksInfo
		jumpLeft :height * first :rockInfo
		jumpForward :height * item 2 :rockInfo
		localmake "arcsInfo item 3 :rockInfo
		polyStart
		arcsRight :arcsInfo :height
		polyEnd
		jumpTo :oldPos
		setHeading :oldHeading
	]
end

; Inspired by
; https://en.wikipedia.org/wiki/Flag_of_Nunavut#/media/File:Flag_of_Nunavut.svg
to nunavutFlag :height
	localmake "oldState turtleState
	localmake "width :height * 96 / 54
	localmake "starSize :height * 0.086

	; Draw the yellow and white background.
	jumpForward :height / 2
	right 90
	jumpBackward :width / 2
	stripes :height :width ["#fdd500 "white]

	; Draw the star.
	jumpLeft :height * 0.3
	jumpForward :width * 0.8875
	setPenSize 0
	setFillColor "#0164bb
	right 27
	regularStar :starSize 5

	; Draw the rocks.
	setTurtleState :oldState
	drawRocks :height

	setTurtleState :oldState
end

setScreenColor "black
nunavutFlag 100