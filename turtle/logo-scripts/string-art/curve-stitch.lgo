; Inspired by:
; https://www.pinterest.ca/pin/562809284689586742/

to gridPattern :size :angle
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "numLines 20
	localmake "step :size / :numLines
	localmake "points []
	penUp
	; store points along 1 direction.
	repeat :numLines [
		queue "points pos
		forward :step
	]
	right :angle
	penDown
	forward :size 
	; draw line through one side of end-points.
	penUp
	back :size
	localmake "rightHeading heading
	repeat :numLines [
		localmake "fromPos1 pos
		localmake "toPos item repcount :points

		; draw line between fromPos1 and 
		; corresponding point from :points.
		setHeading towards :toPos
		penDown
		forward distance :toPos
		penUp
		setPos :fromPos1
		setHeading :rightHeading
		forward :step
	]
	; restore original position and heading.
	setPos :oldPos
	setHeading :oldHeading
end

to design :size
	localmake "oldPos pos
	localmake "oldHeading heading
	setPenSize :size * 0.002
	setPenColor "#f72
	repeat 3 [
		penUp
		forward :size
		right 180
		penDown
		gridPattern :size 60
		setPos :oldPos
		right 180 + 360 / 3
	]
	setPenColor "#2bf
	repeat 3 [
		gridPattern :size 120
		gridPattern :size -120
		right 360 / 3
	]
	setPenColor "#5f2
	penUp
	repeat 3 [
		forward :size
		right 120
		penDown
		gridPattern :size 60
		penUp
		setPos :oldPos
		right 360 / 3
	]
	penUp
	setPos :oldPos
	setHeading :oldHeading
end

setScreenColor "black
design 150