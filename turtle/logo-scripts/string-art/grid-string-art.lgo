; Inspired by:
; https://www.pinterest.ca/pin/1477812369704617/

to gridPattern :size1 :size2 :angle
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "numLines 20
	localmake "step1 :size1 / :numLines
	localmake "step2 :size2 / :numLines
	localmake "points []
	penUp
	; store points along 1 direction.
	repeat :numLines [
		queue "points pos
		forward :step1
	]
	right :angle
	penDown
	forward :size2
	; draw line through one side of end-points.
	penUp
	back :size2
	localmake "rightHeading heading
	repeat :numLines [
		localmake "fromPos1 pos
		localmake "toPos item repcount :points

		; draw line between fromPos1 and 
		; corresponding point from :points.
		setHeading towards :toPos
		penDown
		forward distance :toPos
		penUp
		setPos :fromPos1
		setHeading :rightHeading
		forward :step2
	]
	; restore original position and heading.
	setPos :oldPos
	setHeading :oldHeading
end

to gridStringArt :size1 :size2 :angle :numGrids
	repeat :numGrids [
		gridPattern :size1 :size2 :angle
		right 360 / :numGrids
	]
end

to gridStringArtFull :size
	setPenSize :size * 0.006
	setPenColor "white
	gridStringArt :size :size -90 8
	setPenColor "red
	gridStringArt :size :size 90 8
end

to gridStringArtFull2 :size
	setPenSize :size * 0.006
	setPenColor "white
	gridStringArt :size :size -60 6
	setPenColor "red
	gridStringArt :size :size 60 6
end

to gridStringArtFull3 :size
	localmake "size1 :size
	localmake "size2 :size * tan 30
	setPenSize :size * 0.006
	setPenColor "white
	gridStringArt :size1 :size2 -90 6
	setPenColor "red
	gridStringArt :size1 :size2 90 6
end


setScreenColor "black
right 90
gridStringArtFull 100
forward 300
gridStringArtFull2 85
forward 300
gridStringArtFull3 100
