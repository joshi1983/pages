; Inspired by a design from Phan Bá Thành.
; Phan's facebook profile is at:
; https://www.facebook.com/thanh.ba86

to triangle :size
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "numLines 24
	localmake "step :size / :numLines
	localmake "angle 135
	localmake "angle2 5
	penUp
	left :angle - :angle2
	forward :size
	localmake "toPos pos
	setPos :oldPos
	repeat :numLines [
		penUp
		setHeading :oldHeading + :angle + :angle2
		forward :step
		localmake "tempPos pos
		penDown
		setHeading towards :toPos
		forward distance :toPos
		penUp
		setPos :tempPos
	]
	setPos :oldPos
	setHeading :oldHeading
end

to innerPattern :size
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "numLines 12
	localmake "result []
	penUp
	repeat :numLines [
		setHeading :oldHeading + 360 * repcount / :numLines
		setPos :oldPos
		forward :size
		queue "result pos
	]
	repeat :numLines [
		penUp
		localmake "fromPos item repcount :result
		localmake "toPoints [
			item 1 + remainder (repcount + 4) :numLines :result
			item 1 + remainder (repcount + :numLines - 4) :numLines :result
		]
		repeat count :toPoints [
			localmake "toPos item repcount :toPoints
			setPos :fromPos
			setHeading towards :toPos
			penDown
			forward distance :toPos
		]
	]
	penUp	
	setPos :oldPos
	output :result
end

to solarOctagon :size
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "tradius :size * 0.83
	localmake "triangleSize :size * 0.38
	localmake "innerRadius :size * 0.45
	localmake "numLines 48
	setLineJoinStyle "round
	setFillColor "black
	setPenSize 0
	circle :size
	setFillColor transparent
	setPenColor "#b40
	setPenSize :size * 0.05
	circle :size * 0.91
	setPenColor "#740
	setPenSize :size * 0.03
	circle :size * 0.84
	setPenColor "#ff8
	circle :size * 0.87
	setPenSize :size * 0.015
	circle :innerRadius
	setPenSize :size * 0.003
	repeat 8 [
		penUp
		setPos :oldPos
		forward :tradius
		triangle :triangleSize
		right 360 / 8
	]
	setPos :oldPos
	localmake "innerPoints innerPattern :size * 0.1
	localmake "factor (count :innerPoints) / :numLines
	repeat :numLines [
		localmake "h :oldHeading + 360 * repcount / :numLines
		localmake "toPoints [
			item 1 + int remainder int (11 + repcount * :factor) (count :innerPoints) :innerPoints
			item 1 + int remainder (int repcount * :factor) (count :innerPoints) :innerPoints
		]
		setHeading :h
		penUp
		setPos :oldPos
		forward :innerRadius
		localmake "endPoint pos
		repeat count :toPoints [
			localmake "toP item repcount :toPoints
			setPos :endPoint
			setHeading towards :toP
			penDown
			forward distance :toP
		]
	]
	penUp
	setPos :oldPos
	setHeading :oldHeading
end

solarOctagon 150