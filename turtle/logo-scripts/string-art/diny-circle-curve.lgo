; Inspired by:
; https://www.pinterest.ca/pin/970385050929856363/

make "dotColor "red
make "stringColor "#048

to slice :radius :angle
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "numRadialDots 50
	localmake "numEdgeDots :numRadialDots / 2
	localmake "step :radius / :numRadialDots
	localmake "angleStep :angle / :numEdgeDots
	localmake "dots []
	setPenSize 0
	penUp
	repeat 2 [
		repeat :numRadialDots [
			forward :step
			queue "dots pos
		]
		if repcount = 1 [
			repeat :numEdgeDots [
				setPos :oldPos
				right :angleStep
				forward :radius
				queue "dots pos
			]
			setPos :oldPos
			setHeading :oldHeading + :angle
			forward :radius
			right 180
		]
	]
	setFillColor :dotColor
	repeat count :dots [
		setPos item repcount :dots
		circle :radius * 0.007
	]
	setPenSize :radius * 0.0015
	setPos :oldPos
	setHeading :oldHeading
	repeat :numRadialDots [
		setHeading :oldHeading
		forward :step
		localmake "fromPos pos
		localmake "toPos item repcount + :numRadialDots :dots
		setHeading towards :toPos
		penDown
		forward distance :toPos
		penUp
		setPos :fromPos
	]
	setPos :oldPos
	repeat :numEdgeDots - 1 [
		setHeading :oldHeading + :angle
		forward :step
		localmake "fromPos pos
		localmake "toPos item :numRadialDots + :numEdgeDots - repcount :dots
		setHeading towards :toPos
		penDown
		forward distance :toPos
		penUp
		setPos :fromPos
	]

	setPos :oldPos
	setHeading :oldHeading
end

to dinyCircleCurve :radius
	setPenColor :stringColor
	setPenSize :radius * 0.3
	circle :radius + penSize * 0.5
	repeat 8 [
		slice :radius 45
		right 45
	]
end

dinyCircleCurve 150