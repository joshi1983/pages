; Inspired by:
; https://www.pinterest.ca/pin/17732992273331837/

to forwardToList :len
	localmake "lineSegment [xCor yCor]
	penUp
	forward :len
	queue "lineSegment xCor
	queue "lineSegment yCor
	queue "lineSegment penColor
	output :lineSegment
end

to drawV :deltaX :deltaY
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "angle arcTan :deltaX / :deltaY
	localmake "len hypot [:deltaX :deltaY]
	left :angle
	penDown
	localmake "segment1 forwardToList :len
	penUp
	setPos :oldPos
	right :angle * 2
	penDown
	localmake "segment2 forwardToList :len

	penUp
	setPos :oldPos
	setHeading :oldHeading
	output [:segment1 :segment2]
end

to ratioItem :ratio :list1
	localmake "index 1 + int (:ratio * count :list1) 
	localmake "index min (count :list1) (max 1 :index)
	output item :index :list1
end

to squareStarStringDrawing :size
	localmake "oldPos pos
	localmake "innerSize :size * 0.5
	localmake "colors ["#d05 "#f8b "white]
	localmake "numStrings 20
	localmake "innerDelta :innerSize / :numStrings
	localmake "outerDelta :size * 0.5 / :numStrings
	setPenSize :size * 0.012
	localmake "lineSegments []

	repeat 4 [
		penUp
		setPos :oldPos
		localmake "deltaY :size
		localmake "deltaX :size * 0.5
		right 90
		repeat 1 + :numStrings [
			setPenColor ratioItem ((repcount - 1) / (:numStrings - 1)) :colors
			penDown
			queue "lineSegments (drawV :deltaX :deltaY)
			penUp
			localmake "deltaX :deltaX + :outerDelta
			localmake "deltaY :deltaY - :innerDelta
			forward :innerDelta
		]
	]
	right 45
	repeat 4 [
		penUp
		right 90
		setPos :oldPos
		localmake "mainHeading heading
		forward :size * sqrt 2
		right 135
		forward :size * 0.5 - :outerDelta
		localmake "testHeading heading
		localmake "testPos pos
		setHeading :mainHeading
		setPos :oldPos
		repeat 2 + :numStrings [
			setPenColor ratioItem ((repcount - 1) / (:numStrings - 1)) :colors
			penDown
			if repcount <> 1 [
				queue "lineSegments (drawV :deltaX :deltaY) 
			]
			penUp
			forward :innerDelta
			localmake "mainPos pos
			setPos :testPos
			setHeading :testHeading
			forward :outerDelta
			localmake "testPos pos
			setPos :mainPos
			setHeading :mainHeading
			localmake "r distance :testPos
			localmake "angle (towards :testPos) - :mainHeading + 90
			localmake "deltaX :r * cos :angle
			localmake "deltaY :r * sin :angle
		]
	]
	; draw all the line segments.
	localmake "midOffset (count :lineSegments) / 2
	repeat count :lineSegments [
		ifelse 0 = remainder repcount 2 [
			localmake "pair (item repcount / 2 :lineSegments)
		] [
			localmake "pair (item :midOffset + int repcount / 2  :lineSegments)
		]
		repeat 2 [
			localmake "lineSegment item repcount :pair
			penUp
			setXY (item 1 :lineSegment) (item 2 :lineSegment)
			setPenColor item 5 :lineSegment
			penDown
			setXY (item 3 :lineSegment) (item 4 :lineSegment)
		]
	]
end

setScreenColor "black
squareStarStringDrawing 200