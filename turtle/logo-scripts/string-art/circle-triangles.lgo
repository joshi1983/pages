; inspired by:
; https://www.pinterest.ca/pin/970385050929856363/

to circleTriangles :size
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "numDots 96
	localmake "numSides 8
	localmake "numPointsPerSide :numDots / 4
	localmake "dotRadius :size * 0.015
	setPenSize 0.1
	setFillColor "red
	penUp
	localmake "points []
	repeat :numDots [
		setHeading :oldHeading + 360 * repcount / :numDots
		forward :size
		penDown
		circle :dotRadius
		queue "points pos
		penUp
		setPos :oldPos
	]
	repeat :numSides [
		localmake "pointIndex int (repcount * :numDots / :numSides)
		localmake "from item :pointIndex :points
		repeat :numPointsPerSide [
			setPos :from
			localmake "to1 item 1 + (remainder repcount - 2 + :pointIndex + :numPointsPerSide :numDots) :points
			setHeading towards :to1
			penDown
			forward distance :to1
			penUp
		]
	]
	; restore original position and heading.
	setPos :oldPos
	setHeading :oldHeading
end

circleTriangles 150