; Inspired by
; https://www.pinterest.ca/pin/1196337391651767/
; The animation looks most like the pinterest image at 2 seconds.

to circleStringArt :radius :time
	localmake "oldPos pos
	localmake "numPoints 3000
	localmake "points []
	circle :radius
	repeat :numPoints [
		jumpTo :oldPos
		jumpForward :radius
		queue "points pos
		right 360 / :numPoints
	]
	repeat :numPoints [
		localmake "index 1 + remainder int (repcount * :time) :numPoints
		localmake "toPoint item :index :points
		jumpTo item repcount :points
		setHeading towards :toPoint
		forward distance :toPoint
	]
	jumpTo :oldPos
end

setPenSize 0.01
circleStringArt 100 power animation.time 2

to animation.snapshotstyle
	output createPList2 [
		["zoom.scale 2.6]
	]
end

to animation.setup
	localmake "minutes 0
	localmake "seconds 10
	; Edit this if you want to change the length of your animation.
	output createPList2 [
		["duration :minutes * 60 + :seconds]
	]
end