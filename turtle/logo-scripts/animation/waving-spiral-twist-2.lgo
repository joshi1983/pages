; Inspired by
; https://www.pinterest.ca/pin/10836855341071636
to spiralOfCirclesWaving :height :timeRatio
	localmake "oldState turtleState
	localmake "oldHeading heading
	localmake "numArms 12
	localmake "numCirclesPerArm 40
	localmake "maxTwistAngle 40 * (radSin pi * 2 * :timeRatio)
	localmake "minRadius :height * 0.005
	localmake "maxRadius :height * 0.08
	setPenSize :height * 0.002
	localmake "maxCircleDistance :height / 2 - :minRadius - penSize / 2
	localmake "minCircleDistance :height * 0.11
	jumpForward :height / 2
	localmake "center pos
	repeat :numArms [
		localmake "armStartAngle :oldHeading + repcount * 360 / :numArms
		repeat :numCirclesPerArm [
			jumpTo :center
			setHeading :armStartAngle + :maxTwistAngle * interpolateRatio easeIn repRatio
			jumpForward mix :maxCircleDistance :minCircleDistance repRatio
			circle mix :minRadius :maxRadius repRatio
		]
	]
	setTurtleState :oldState
end

setScreenColor "black
setPenColor "white
make "timeRatio animation.time / animation.duration
make "timeRatio clamp :timeRatio 0 1
spiralOfCirclesWaving 100 :timeRatio

to animation.snapshotstyle
	localmake "result createPList
	setProperty "result "zoom.scale 6
	setProperty "result "position.y 50
	output :result
end

to animation.setup
	localmake "minutes 0
	localmake "seconds 2
	; Edit this if you want to change the length of your animation.
	localmake "result createPList
	setProperty "result "duration :minutes * 60 + :seconds
	setProperty "result "thumbnailTime 0.5
	output :result
end