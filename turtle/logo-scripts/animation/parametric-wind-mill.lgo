; Inspired by
; https://www.pinterest.ca/pin/75364993757667153/
; Parametric equations are shown in the image.
; 
to tToPoint :t
	output [
		(power (radCos 9.9 * :t) 2) * radSin (radSin (10 * :t))
		10 / 9 * radSin (9.9 * :t)
	]
end

to transformUsingTurtleState :p
	localmake "oldPos pos
	jumpLeft first :p
	jumpForward last :p
	localmake "result xyCor
	jumpTo :oldPos
	output :result
end

to drawBlade :scale
	localmake "oldState turtleState
	setPenSize :scale * 0.007
	setPenColor "#500
	jumpRight :scale * 0.2
	localmake "oldPos pos

	localmake "oldHeading heading
	; Increase the 3000 to get a smoother, more accurate curve.
	; Decrease to sacrifice accuracy for execution speed and fewer line segments.
	repeat 3000 [
		localmake "t 12 * (repRatio - 0.5)
		localmake "p vectorScale (tToPoint :t) :scale
		setHeading :oldHeading
		jumpTo :oldPos
		localmake "p transformUsingTurtleState :p
		ifelse 1=repcount [
			jumpTo :p
		] [
			jumpTo :prevPos
			setHeading towards :p
			forward distance :p
		]
		localmake "prevPos pos
	]
	setTurtleState :oldState
end

to drawParametricWindMill :scale
	repeat 4 [
		drawBlade :scale
		right 360 / 4
	]
end

make "timeRatio clamp animation.time / animation.duration 0 1 
make "timeRatio interpolateRatio easeInOut :timeRatio
right 360 * 3 * :timeRatio
drawParametricWindMill 100

to animation.snapshotstyle
	localmake "result createPList
	setProperty "result "zoom.scale 3
	output :result
end

to animation.setup
	localmake "minutes 0
	localmake "seconds 3
	; Edit this if you want to change the length of your animation.
	localmake "result createPList
	setProperty "result "duration :minutes * 60 + :seconds
	output :result
end