; Inspired by images at:
; https://www.alancsmith.co.uk/logo/svgs.html
to twistedStripeCircle :height :timeRatio
	localmake "oldState turtleState
	localmake "oldHeading heading
	localmake "numStripes 64
	localmake "numRadii 6
	localmake "delta 180 / 2 / :numStripes
	localmake "twist 10 * sin 360 * :timeRatio
	jumpForward :height / 2
	localmake "center pos
	setPenSize 0
	setFillColor "black
	repeat :numStripes [
		localmake "startHeading :oldHeading + repcount * 360 / :numStripes
		localmake "points1 []
		localmake "points2 []
		repeat :numRadii [
			localmake "radius :height / 2 * mix 1 0.1 repRatio
			localmake "startHeading2 :startHeading + :twist * sign (remainder repcount 2) - 0.5
			setHeading :startHeading2 - :delta
			jumpForward :radius
			queue2 "points1 pos
			jumpTo :center
			setHeading :startHeading2 + :delta
			jumpForward :radius
			queue2 "points2 pos
			jumpTo :center
		]
		localmake "points combine :points1 reverse :points2
		polyStart
		repeat count :points [
			localmake "nextPoint item repcount :points
			setHeading towards :nextPoint
			jumpForward distance :nextPoint
		]
		polyEnd
		jumpTo :center
	]
	setTurtleState :oldState
end

make "timeRatio clamp animation.time / animation.duration 0 1
make "timeRatio interpolateRatio easeInOut :timeRatio
twistedStripeCircle 100 :timeRatio

to animation.snapshotstyle
	localmake "result createPList
	output :result
end

to animation.setup
	localmake "minutes 0
	localmake "seconds 5
	; Edit this if you want to change the length of your animation.
	localmake "result createPList
	setProperty "result "duration :minutes * 60 + :seconds
	setProperty "result "thumbnailTime 1
	output :result
end