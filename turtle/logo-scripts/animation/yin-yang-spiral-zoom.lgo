; Inspired by image at:
; https://www.facebook.com/photo/?fbid=10232966597411210
; This animation explains how the area
; of black divided by area of the circle is 1/3.

to arcShape :radius
	localmake "radius1 :radius / 2
	right 180
	polyStart
	arcLeft 180 :radius1
	arcLeft 180 :radius
	left 180
	arcRight 180 :radius1
	polyEnd
	right 180
end

to getVanishingPoint :height
	localmake "oldState turtleState
	repeat 100 [
		jumpLeft :height / 4
		localmake "height :height / 2		
		left 90
	]
	localmake "result pos
	setTurtleState :oldState
	output :result
end

to yinYangSpiral :height :numIterations :minTextYOffset :minTextXOffset :maxFontSize
	localmake "oldState turtleState
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "radius :height / 2 - penSize
	setFillColor "black
	circle :radius
	setFillColor "#ccd2f4
	localmake "msg 'A = 1'
	localmake "ratio 1
	repeat :numIterations [
		localmake "msg (word :msg ' - 1/2^' str 2 * repcount - 1)
		localmake "ratio :ratio - (power 2 -(repcount * 2 - 1))
		arcShape :radius
		jumpLeft :radius / 2
		localmake "radius :radius / 2		
		left 90
	]
	setFillColor mix fillColor "black :numIterations - int :numIterations
	setPenColor "red
	arcShape :radius
	jumpTo :oldPos
	setHeading :oldHeading + 90
	jumpBackward :height / 2
	jumpRight :height * 0.4
	setPenSize 0
	setX max xCor :minTextXOffset
	setY max yCor :minTextYOffset
	setFillColor "white
	localmake "fs :height * 0.17 / (max 1 :numIterations) 
	localmake "fs min :maxFontSize :fs
	localmake "textBackdropOffset -:fs * 0.2
	localmake "textBackdropWidth :fs * 2.5
	setFontSize :fs
	if :numIterations > 4 [
		localmake "ratio2 1 - clamp (:numIterations - 4) / 5 0 1
		setFillColor mix "white transparent :ratio2
		if :ratio2 < 0.5 [
			localmake "ratio2 1 - :ratio2 * 2
			setFontSize fontSize * mix 2 1 :ratio2
			localmake "textBackdropOffset mix :fs * -1.2 :textBackdropOffset :ratio2
		]
	]
	jumpLeft :textBackdropOffset
	setLineCap "round
	setPenColor mix "black "#6000 1 - 1 / (max 1 :numIterations)
	setPenSize :textBackdropWidth
	localmake "len :height
	forward :len
	jumpBackward :len
	jumpRight :textBackdropOffset
	setPenSize 0
	label :msg
	jumpRight fontSize
	setFillColor "white
	label word 'A = ' str :ratio
	setTurtleState :oldState
end

setScreenColor "#c4cfd1
make "timeRatio1 interpolateRatio easeInOut animation.clampedTimeRatio
make "numIterations 22 * :timeRatio1
make "size 100
make "fontSizeLimit max 3 :size * 0.17 / (max 1 :numIterations) 
if :timeRatio1 > 0.3 [
	make "translateRatio clamp (:timeRatio1 - 0.3) / 0.4 0 1
	make "translateRatio interpolateRatio easeInOut :translateRatio
	if :timeRatio1 > 0.5 [
		make "timeRatio (:timeRatio1 - 0.5) / 0.5
		make "timeRatio interpolateRatio easeInOut :timeRatio
		make "size :size * power 880000 :timeRatio
	]
	make "vanishingPoint vectorScale getVanishingPoint :size -1
	jumpTo mix :vanishingPoint [0 0 0] :translateRatio
]
setPenSize min 0.2 :size * 0.002
yinYangSpiral :size :numIterations -60 -90 :fontSizeLimit

to animation.snapshotstyle
	output createPList2 [
		["zoom.scale 3.8]
	]
end

to animation.setup
	localmake "minutes 0
	localmake "seconds 20
	; Edit this if you want to change the length of your animation.
	output createPList2 [
		["duration :minutes * 60 + :seconds]
	]
end