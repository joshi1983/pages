; Inspired by animation at
; https://www.pinterest.ca/pin/417427459221337913/
;
; Also, resembles a drawing from a book called:
; Computer art and animation : a user's guide to Atari logo
; The book was published in 1984.
; The similar drawing is found in page 83.
; You can see it at:
; https://archive.org/details/ataribooks-computer-art-and-animation/page/n89/mode/2up

make "colors ["green "yellow "orange "red "brown "purple
"blue "navy "black "aqua]

to regularPolygonFromCorner :sideLength :numSides
	polyStart
	repeat :numSides [
		jumpForward :sideLength
		right 360 / :numSides
	]
	polyEnd
end

to tumblingPolygons :size :numTumbledCycles
	localmake "oldState turtleState
	jumpLeft :size
	; Convert size from size of the 12-sided polygon to 
	; length of 1 side of the polygon.
	localmake "size :size * 2 * tan 180 / 12
	jumpBackward :size * 0.5

	; Convert the number of cycles from number of rotations of the 
	; outer polygon to number of sides tumbled through.
	localmake "numTumbledCycles :numTumbledCycles * 12

	; Move the turtle around the 12-sided polygon to the intended 
	; starting position and heading.
	repeat int :numTumbledCycles [
		left 360 / 12
		jumpBackward :size
	]
	localmake "numTumbledCycles :numTumbledCycles - int :numTumbledCycles
	localmake "numTumbledCycles :numTumbledCycles * 3 / 4
	localmake "heading1 heading
	setPenSize :size * 0.03
	setLineJoinStyle "round
	for ["numSides 12 3 -1] [
		localmake "ratio (12 - :numSides) / 9
		setHeading :heading1 + :numTumbledCycles * :ratio * 360 / :numSides 
		setFillColor item :numSides - 2 :colors
		regularPolygonFromCorner :size :numSides
	]
	setTurtleState :oldState
end

tumblingPolygons 100 animation.time * 0.1

to animation.snapshotstyle
	localmake "result createPList
	setProperty "result "zoom.scale 2.5
	output :result
end

to animation.setup
	localmake "minutes 0
	localmake "seconds 10
	; Edit this if you want to change the length of your animation.
	localmake "result createPList
	setProperty "result "duration :minutes * 60 + :seconds
	output :result
end