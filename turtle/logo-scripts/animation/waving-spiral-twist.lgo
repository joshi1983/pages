; Inspired by:
; https://www.pinterest.ca/pin/836262224571910087/

to fillCurvedPath :size
	localmake "oldState turtleState
	localmake "size1 :size * 0.297
	localmake "size2 :size * 0.2
	localmake "angle1 150
	localmake "angle2 :angle1 - 360 / 6
	jumpForward :size
	right 90
	arcRight :angle1 * -0.5 :size1
	polyStart
	repeat 6 [
		arcRight :angle1 :size1
		arcLeft :angle2 :size2
	]
	polyEnd
	setTurtleState :oldState
end

to wavingSpiral :size
	localmake "numContours 80
	localmake "step :size / :numContours
	setPenSize 0
	repeat :numContours [
		ifelse even? repcount [
			setFillColor "white
		] [
			setFillColor "black
		]
		fillCurvedPath :size
		localmake "size :size - :step
		right 0.6 * animation.time
	]
end

setFillColor "red
wavingSpiral 150

to animation.snapshotstyle
	output createPList2 [
		["zoom.scale 4]
	]
end

to animation.setup
	localmake "minutes 0
	localmake "seconds 5
	; Edit this if you want to change the length of your animation.
	output createPList2 [
		["duration :minutes * 60 + :seconds]
	]
end