; Inspired by:
; https://www.pinterest.ca/pin/379569074858644324/

make "colors ["#f00 "#ff0 "#0f0 "#0ff "#00f "#f0f]

to rainbowGradient :size :timeRatio
	localmake "oldPos pos
	localmake "numSamples 2 * count :colors
	localmake "colorStops createPList
	repeat :numSamples [
		setProperty "colorStops repcount / :numSamples mixItems :colors remainder (repRatio + :timeRatio) 1
	]
	jumpForward :size
	localmake "fromPos pos
	jumpBackward :size * 2
	localmake "result createLinearGradient :fromPos pos :colorStops "pad
	jumpTo :oldPos
	output :result
end

to spiroManiac :size :timeRatio
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "numArcs 10
	localmake "midArcAngle 40
	localmake "angleDelta 2
	localmake "arcAngle2 :midArcAngle * 2 - :angleDelta * mix -1.7 -4.6 :timeRatio
	localmake "arcAngle3 :midArcAngle * 2 + :angleDelta * mix 3 -0.5 :timeRatio
	localmake "startAngle mix 176 178.5 :timeRatio 
	localmake "endAngle mix 174.5 181.5 :timeRatio 
	localmake "midArcRadius :size * 1.62
	localmake "arcRadius1 :midArcRadius * mix 1.077 1.02 :timeRatio
	localmake "arcRadius2 :midArcRadius * mix 0.95 0.99 :timeRatio
	localmake "endWidth1 :size * 0.005
	localmake "endWidth2 :size * mix 0.001 0.01 :timeRatio
	localmake "innerRadius :size * 0.4
	localmake "halfChordSize :midArcRadius * sin :midArcAngle
	setPenSize 0
	setFillColor "white
	repeat :numArcs [
		setHeading :oldHeading + (repcount - 1) * 360 / :numArcs
		jumpForward :innerRadius
		left 60
		setFillGradient rainbowGradient :halfChordSize :timeRatio
		arcRight :midArcAngle :midArcRadius
		jumpRight :endWidth1
		right :startAngle
		polyStart
		arcLeft :arcAngle2 :arcRadius1
		jumpRight :endWidth2
		jumpRight :endWidth2
		right :endAngle
		arcRight :arcAngle3 :arcRadius2
		polyEnd
		jumpTo :oldPos
	]
end

setScreenColor "black
spiroManiac 250 animation.time / animation.duration

to animation.snapshotstyle
	localmake "result createPList
	output :result
end

to animation.setup
	localmake "minutes 0
	localmake "seconds 3
	; Edit this if you want to change the length of your animation.
	localmake "result createPList
	setProperty "result "duration :minutes * 60 + :seconds
	output :result
end