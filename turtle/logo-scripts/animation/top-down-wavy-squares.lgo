; Draws a square centered on the initial turtle position
to square :size
	jumpBackward :size / 2
	setPenSize :size
	forward :size
	jumpBackward :size / 2
end

; Inspired by
; https://www.youtube.com/watch?v=afADEZdsQVA
; This is very different, though.
to topDownWaveySquares :size :timeRatio
	localmake "oldState turtleState
	localmake "minSize :size * 0.02
	localmake "maxSize :minSize * 4.3
	localmake "gridSize (:size - :maxSize) / 10
	localmake "waveLengthScale :size / 3
	localmake "centerAngle :timeRatio * -720
	setPenColor "black
	setLineCap "butt
	jumpForward :size / 2 - :maxSize / 2
	jumpLeft :gridSize * 20
	for ["x -20 20] [
		localmake "pos1 pos
		for ["y -5 5] [
			localmake "sizeRatio (1 + sin :centerAngle + :waveLengthScale * hypot [:x :y]) / 2
			localmake "squareSize mix :minSize :maxSize :sizeRatio
			square :squareSize
			jumpBackward :gridSize
		]
		jumpTo :pos1
		jumpRight :gridSize
	]
	setTurtleState :oldState
end

make "timeRatio animation.time / animation.duration
make "timeRatio clamp :timeRatio 0 1
topDownWaveySquares 100 :timeRatio

to animation.snapshotstyle
	localmake "result createPList
	setProperty "result "zoom.scale 3
	output :result
end

to animation.setup
	localmake "minutes 0
	localmake "seconds 3
	; Edit this if you want to change the length of your animation.
	localmake "result createPList
	setProperty "result "duration :minutes * 60 + :seconds
	output :result
end