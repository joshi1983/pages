; Inspired by:
; https://www.pinterest.ca/pin/702069029418970918/

to drawPeddle :size
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "angle1 30
	localmake "angle2 15
	localmake "angle3 30
	localmake "angle4 120
	localmake "size1 :size * 1.42925
	localmake "size2 :size * 0.6

	setfillcolor penColor
	setPenSize 0
	polystart
	left :angle1
	arcRight :angle2 :size1
	arcRight :angle3 :size2
	arcRight :angle2 :size1
	right :angle4 ; point of symmetry
	arcRight :angle2 :size1
	arcRight :angle3 :size2
	arcRight :angle2 :size1
	polyend
	setPos :oldPos
	setHeading :oldHeading
end

to drawPaddedPeddle :size
	localmake "oldPos pos
	localmake "oldHeading heading
	pendown
	setPenSize :size * 0.015
	forward :size * 0.15
	drawPeddle :size * 0.9
	setPos :oldPos
	setHeading :oldHeading
end

to wheatArcSettings :angle :size
	localmake "peddleAngle 7
	localmake "numPeddles (int (:angle / :peddleAngle)) - 1
	localmake "peddleSize :size * 0.2
	localmake "peddleAngle2 30
	localmake "peddleAngle3 :peddleAngle2 * 1.5
	localmake "stemThickness :size * 0.01
	output [:peddleAngle :numPeddles :peddleSize :peddleAngle2 :peddleAngle3 :stemThickness]
end

to singlePeddleSettings :angle :size
	localmake "settings wheatArcSettings :angle :size
	localmake "stemThickness item 6 :settings
	localmake "peddleSize item 3 :settings
	localmake "angle2 :angle - 11.4781
	output [:stemThickness :peddleSize :angle2 5.739]
end

to arcRightSinglePeddle :angle :size
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "settings singlePeddleSettings :angle :size
	localmake "stemThickness item 1 :settings
	localmake "peddleSize item 2 :settings
	localmake "angle2 item 3 :settings
	localmake "angle3 item 4 :settings
	penDown
	if :angle2 > 0 [
		setPenSize :stemThickness
		arcRight :angle2 + 1 :size
		penUp
		arcRight -1 :size
	]
	right :angle3
	drawPeddle :peddleSize

	penup
	setPos :oldPos
	setHeading :oldHeading	
end

to arcLeftSinglePeddle :angle :size
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "settings singlePeddleSettings :angle :size
	localmake "stemThickness item 1 :settings
	localmake "peddleSize item 2 :settings
	localmake "angle2 item 3 :settings
	localmake "angle3 item 4 :settings
	penDown
	if :angle2 > 0 [
		setPenSize :stemThickness
		arcLeft :angle2 + 1 :size
		penUp
		back :size * pi * 2 / 360
	]
	left :angle3
	drawPeddle :peddleSize

	penup
	setPos :oldPos
	setHeading :oldHeading	
end

to wheatArcRight :angle :size
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "settings wheatArcSettings :angle :size
	localmake "peddleAngle item 1 :settings
	localmake "numPeddles item 2 :settings
	localmake "peddleSize item 3 :settings
	localmake "peddleAngle2 item 4 :settings
	localmake "peddleAngle3 item 5 :settings
	arcRightSinglePeddle :angle :size
	penup
	setPos :oldPos
	setHeading :oldHeading
	pendown
	repeat :numPeddles [
		left :peddleAngle2
		pendown
		drawPaddedPeddle :peddleSize
		right :peddleAngle2 + :peddleAngle3
		drawPaddedPeddle :peddleSize
		left :peddleAngle3
		penup
		arcRight :peddleAngle :size
	]
	setPenSize 0
	setPos :oldPos
	setHeading :oldHeading
end

to wheatArcLeft :angle :size
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "settings wheatArcSettings :angle :size
	localmake "peddleAngle item 1 :settings
	localmake "numPeddles item 2 :settings
	localmake "peddleSize item 3 :settings
	localmake "peddleAngle2 item 4 :settings
	localmake "peddleAngle3 item 5 :settings
	arcLeftSinglePeddle :angle :size
	penup
	setPos :oldPos
	setHeading :oldHeading
	pendown
	repeat :numPeddles [
		right :peddleAngle2
		pendown
		drawPaddedPeddle :peddleSize
		left :peddleAngle2 + :peddleAngle3
		drawPaddedPeddle :peddleSize
		right :peddleAngle3
		penup
		arcLeft :peddleAngle :size
	]
	setPenSize 0
	setPos :oldPos
	setHeading :oldHeading
end

to stemmedWheatArcSettings :angle :size
	localmake "settings wheatArcSettings :angle :size
	localmake "stemStartAngle 5
	localmake "stemAngle 10
	localmake "stemStartRadius :size * 0.15
	localmake "stemThickness item 6 :settings
	output [:stemStartAngle :stemAngle :stemStartRadius :stemThickness 35]
end

to stemmedWheatArcLeft :angle :size
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "settings stemmedWheatArcSettings :angle :size
	localmake "stemStartAngle item 1 :settings
	localmake "stemAngle item 2 :settings
	localmake "stemStartRadius item 3 :settings
	setPenSize item 4 :settings
	penup
	arcLeft :stemStartAngle :size
	localmake "oldPos2 pos
	penDown
	left 180
	arcLeft (item 5 :settings) :stemStartRadius
	penUp
	setHeading :oldHeading - :stemStartAngle
	setPos :oldPos2
	penDown
	arcLeft :stemAngle - :stemStartAngle :size

	if :angle > :stemAngle [
		wheatArcLeft :angle - :stemAngle :size
	]
	penup
	setPos :oldPos
	setHeading :oldHeading
end

to stemmedWheatArcRight :angle :size
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "settings stemmedWheatArcSettings :angle :size
	localmake "stemStartAngle item 1 :settings
	localmake "stemAngle item 2 :settings
	localmake "stemStartRadius item 3 :settings
	setPenSize item 4 :settings
	penup
	arcRight :stemStartAngle :size
	localmake "oldPos2 pos
	penDown
	right 180
	arcRight (item 5 :settings) :stemStartRadius
	penUp
	setHeading :oldHeading + :stemStartAngle
	setPos :oldPos2
	penDown
	arcRight :stemAngle - :stemStartAngle :size

	if :angle > :stemAngle [
		wheatArcRight :angle - :stemAngle :size
	]
	penup
	setPos :oldPos
	setHeading :oldHeading
end

setPenColor "#fc0
setScreenColor "black
make "angle 30 + animation.time * 70
penUp
back 200
left 90

stemmedWheatArcRight :angle 200
right 180
stemmedWheatArcLeft :angle 200

to animation.snapshotstyle
	localmake "result plistCreate
	output :result
end

to animation.setup
	localmake "minutes 0
	localmake "seconds 2
	; Edit this if you want to change the length of your animation.
	output :minutes * 60 + :seconds
end