make "darkColor "#780

; Inspired by:
; https://ca.pinterest.com/pin/431571576807036673/
; Mentioned several times at:
; https://temporarytemples.co.uk/project/swarraton-1st-august-2021
; This was reported on: January 8, 2021
; Located at Swarraton, Hampshire
to swarratonCropCircles :height
	localmake "oldState turtleState
	localmake "oldHeading heading
	localmake "lightColor "#ef8
	localmake "outerRadius :height * 0.38
	localmake "innerRadius :height * 0.27
	localmake "innerRadius2 :height * 0.21
	localmake "gapAngle 3
	localmake "innerThickness :height * 0.04
	setPenSize :height * 0.01
	setPenColor :lightColor
	setFillColor :darkColor
	jumpForward :height / 2
	localmake "center pos
	circle (:height - penSize) / 2
	setPenSize 0
	setFillColor :lightColor
	circle :innerRadius
	setPenSize :height * 0.005
	setFillColor transparent
	circle :outerRadius
	setPenSize 0
	setFillColor :lightColor
	repeat 8 [
		localmake "radius :height * item 1 + remainder repcount 2 [0.05 0.085]
		jumpForward :outerRadius
		circle :radius
		right 360 / 8
		jumpTo :center
	]
	setPenSize :innerThickness
	setPenColor :darkColor
	setLineCap "butt
	left :gapAngle / 2
	jumpForward :innerRadius2
	right 90
	repeat 8 [
		penUp
		arcRight :gapAngle + 360 / 16 :innerRadius2
		penDown
		arcRight 360 / 16 - :gapAngle :innerRadius2
	]
	setPenSize :height * 0.002
	setPenColor :lightColor
	setFillColor transparent
	jumpTo :center
	repeat 4 [
		circle :innerRadius2 + (repRatio - 0.5) * 0.6 * :innerThickness
	]

	setPenSize :innerThickness
	setPenColor :darkColor
	jumpForward :innerRadius2
	right 90
	penUp
	repeat 8 [
		penUp
		arcRight :gapAngle :innerRadius2
		penDown
		arcRight 360 / 16 - :gapAngle :innerRadius2
		penUp
		arcRight 360 / 16 :innerRadius2
		penDown
	]
	setFillColor transparent
	setLineJoinStyle "round
	localmake "arcAngle 40
	localmake "gap :height * 0.025
	localmake "innerRadius3 :height * 0.14
	setPenSize :height * 0.01
	repeat 2 [
		setHeading :oldHeading + 90 + repcount * 180
		jumpTo :center
		jumpForward :gap
		localmake "pos1 pos
		jumpForward :innerRadius3 - :gap
		left 90
		arcLeft :arcAngle / 2 :innerRadius3
		right 180
		polyStart
		arcRight :arcAngle :innerRadius3
		setHeading towards :pos1
		jumpForward distance :pos1
		polyEnd
	]
	setTurtleState :oldState	
end

setScreenColor :darkColor
swarratonCropCircles 100