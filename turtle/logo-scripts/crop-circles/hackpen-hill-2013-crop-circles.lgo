make "darkColor "#780

; Inspired by image at:
; https://ca.pinterest.com/pin/531987774706766640/
; Also shown at:
; https://mx.pinterest.com/pin/AbVIi6B5IGQApdgxO8pcf0FDN5I1pLZlIKSU8c-Q2EsJoGPbAF4jsSg/
; and mentioned at:
; https://www.sott.net/article/265214-Two-mysterious-and-elaborate-crop-circles-appear-in-a-week-just-miles-apart
; Reported in August 11, 2013
; Located at Hackpen Hill, England
to hackpenHill2013CropCircles :height
	localmake "oldState turtleState
	localmake "oldHeading heading
	localmake "lightColor "#ef8
	localmake "littleCircleDistance :height * 0.24
	localmake "littleCircleRadius :height * 0.04
	localmake "angle1 20
	localmake "sideLength1 :height * 0.45
	localmake "offset1 :littleCircleRadius * 0.5
	jumpForward :height / 2
	localmake "center pos
	setPenSize 0
	setFillColor :lightColor
	circle :height / 2
	setFillColor :darkColor
	repeat 6 [
		setHeading :oldHeading + (repcount - 1) * 360 / 6
		jumpForward :littleCircleDistance - :littleCircleRadius
		localmake "littleCircleBottom pos
		jumpForward :littleCircleRadius
		localmake "littleCircleCenter pos
		jumpForward :offset1
		localmake "straightVertex pos
		jumpTo :center
		left :angle1
		polyStart
		jumpForward :sideLength1
		setHeading towards :straightVertex
		localmake "len1 distanceToCircle :littleCircleCenter :littleCircleRadius
		jumpForward :len1
		localmake "chordLength distance :littleCircleBottom
		localmake "arcAngle 2 * 2 * arcSin :chordLength / 2 / :littleCircleRadius
		setHeading (towards :littleCircleCenter) + 90
		arcLeft :arcAngle :littleCircleRadius
		setHeading (towards :straightVertex) + 180
		jumpForward :len1
		polyEnd
		jumpTo :center
	]
	setPenSize :height * 0.002
	setPenColor :lightColor
	setFillColor transparent
	circle :littleCircleDistance
	setTurtleState :oldState
end

setScreenColor :darkColor
hackpenHill2013CropCircles 100