make "darkColor "#780
make "lightColor "#ef8

to drawCrescent :chordLength :innerAngle
	localmake "oldState turtleState
	localmake "outerRadius :chordLength / 2
	localmake "innerRadius :chordLength / 2 / sin :innerAngle / 2
	polyStart
	arcLeft :innerAngle :innerRadius
	right 90 + (:innerAngle) / 2
	arcRight 180 :outerRadius
	polyEnd
	setTurtleState :oldState
end

to drawMainCircle :height
	localmake "oldState turtleState
	localmake "oldHeading heading
	jumpForward :height / 2
	localmake "center pos
	setPenSize 0
	setFillColor :lightColor
	circle :height / 2
	setFillColor :darkColor
	repeat 9 [
		localmake "heading1 :oldHeading + repcount * 360 / 9
		setHeading :heading1
		jumpForward :height * 0.49
		jumpLeft :height * 0.01
		polyStart
		right 148
		jumpForward :height * 0.3

		; Turning 180 - 360 / 9 causes 2 sides of the triangle 
		; to be exactly parallel to edges of the neighbouring triangles.
		;  The exact parallel lines looks better than being slightly misaligned.
		right 180 - 360 / 9
		jumpForward :height * 0.27
		polyEnd
		jumpTo :center
		setHeading :heading1
		jumpRight :height * 0.03
		jumpBackward :height * 0.057
		right 60.5
		drawCrescent :height * 0.308 150
		jumpTo :center
	]
	setTurtleState :oldState
end

; I'm calling this a signature only because that's what
; its relative size and subtlety would indicate in a more
; conventional work of art.
to drawSignature :height
	localmake "oldState turtleState
	setPenSize 0
	setFillColor :lightColor
	polyStart
	arcLines [[-90 0] [75 0.12] [24 0.3] [0.21] [-80 0] [160 0.12]
	[-80 0] [0.1] [-88 0] [0.12] [68 0] [0.1] [105 0] [0.1545] [-85 0]
	[0.04] [-92 0] [0.18] [100 0] [0.08] [80 0] [0.23] [92 0] [0.339]
	[-69.1 0] [138.1 0.12] [-69.05 0] [0.2] [-70 0.1] [-100 0.05]
	[180 0.045] [30 0.193] [41.05 0.13]] :height
	polyEnd
	setTurtleState :oldState
end

; Inspired by:
; https://ca.pinterest.com/pin/779333910535629548/
; Also, shown at:
; https://temporarytemples.co.uk/wp-content/uploads/2014/07/1999_07_18_cherhill-wiltshire-wheat-oh-35mm.jpg
; and referenced from:
; on https://temporarytemples.co.uk/crop-circles/1999-crop-circles
; Reported July 18, 1999
; Located at Cherhill, Wiltshire
to cherhill1999CropCircles :height
	localmake "oldState turtleState
	localmake "oldPos pos
	jumpLeft :height * 0.0965
	drawMainCircle :height

	jumpTo :oldPos
	jumpRight :height * 0.59
	jumpForward :height * 0.275
	left 62
	drawSignature :height * 0.173
	setTurtleState :oldState
end

setScreenColor :darkColor
cherhill1999CropCircles 100