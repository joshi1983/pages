; Inspired by image at:
; https://www.facebook.com/share/p/1BVJRq1aVZ/
;
; In case the post gets deleted,
; it was posted by Ghee B Kim:
; https://www.facebook.com/ghee.b.kim
to septagonLoop :height
	localmake "oldState turtleState
	localmake "oldHeading heading
	localmake "numPoints 7
	localmake "largeRadius :height / (1 + cos 180 / :numPoints)
	localmake "smallRadius :height * 0.08
	localmake "smallCircleOffset :height * 0.27
	localmake "flatOffset :largeRadius * cos 360 / :numPoints
	localmake "topAngle1 (180 - 360 / :numPoints * 2) / 2
	localmake "thickness :height * 0.005
	localmake "topTriangleHeight :largeRadius - :flatOffset - :thickness / 2 / sin :topAngle1
	localmake "startOffsetX :topTriangleHeight * tan :topAngle1
	localmake "largeThickness 2 * (:flatOffset - :smallCircleOffset)
	localmake "largeThickness2 :largeThickness - :thickness / 2
	localmake "chord sqrt :smallRadius * :smallRadius - :largeThickness2 * :largeThickness2 / 4
	localmake "chordLen1 :chord * 2
	localmake "len1 (:largeRadius * (sin 360 / :numPoints) + :chord
	 - :thickness / 2 / cos :topAngle1)
	localmake "arcAngle 2 * arcCos :chord / :smallRadius
	localmake "color1 "red
	jumpForward :height - :largeRadius
	localmake "center pos

	setHeading :oldHeading
	setFillColor :color1
	setPenSize 0
	regularStar :largeRadius :numPoints
	setFillColor "white
	repeat :numPoints [
		jumpForward :smallCircleOffset
		circle :smallRadius
		right 360 / :numPoints
		jumpTo :center
	]

	right 180
	setPenSize :largeThickness
	setPenColor :color1
	setFillColor transparent
	regularPolygon :smallCircleOffset / cos 180 / :numPoints :numPoints

	setPenSize :thickness
	setPenColor "black
	setLineJoinStyle "round
	setLineCap "round
	repeat :numPoints [
		setHeading :oldHeading + repcount * 360 / :numPoints
		jumpForward :flatOffset
		jumpRight :startOffsetX
		left :topAngle1
		arcLines [[:topTriangleHeight / cos :topAngle1]
			[180 - :topAngle1 * 2 0] [:len1]
			[-:chordLen1] [-:arcAngle / 2 0]
			[:arcAngle + 180 :smallRadius] [180 - :arcAngle / 2 0]
			[:chordLen1]
		] 1
		jumpTo :center
	]

	setTurtleState :oldState
end

septagonLoop 100