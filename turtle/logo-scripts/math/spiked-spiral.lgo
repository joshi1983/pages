; Inspired by
; https://www.facebook.com/photo/?fbid=1720458468476205
;
; That original design was made by Daniel Seibert.
; Daniel's facebook profile is at:
; https://www.facebook.com/danieltseibert

to spiral :height
	localmake "oldState turtleState
	localmake "oldHeading heading
	localmake "numSpikes 15000
	jumpForward :height / 2
	setPenSize :height * 0.002
	setPenColor "black
	localmake "center pos
	repeat 2 [
		localmake "heading1 :oldHeading + repcount * 360 / 2
		localmake "points []
		repeat :numSpikes [
			localmake "angle 7000 * repRatio
			localmake "radius :height * 0.00545 * sqrt :angle
			localmake "scaledAngle :angle * 22
			localmake "scale power cos :scaledAngle 10
			localmake "scaledAngle remainder :scaledAngle 360
			if between? :scaledAngle 90 270 [
				localmake "scale -:scale
			]
			localmake "radius :radius + :radius * 0.12 * :scale
			setHeading :heading1 + :angle
			jumpForward :radius
			queue2 "points pos
			jumpTo :center
		]
		repeat count :points [
			localmake "nextPoint item repcount :points
			setHeading towards :nextPoint
			forward distance :nextPoint
		]
		jumpTo :center
	]
	setTurtleState :oldState
end

spiral 100