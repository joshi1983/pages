to createRedBlackGradient :radius :flipColors :firstStopRatio
	localmake "colors ["red "black]
	if :flipColors [
		localmake "colors reverse :colors
	]
	localmake "colorStops createPList2 [
		[:firstStopRatio first :colors] [1 last :colors]
	]
	output createRadialGradient pos :radius :colorStops
end

to drawBackground :height
	localmake "oldState turtleState
	localmake "oldPos pos
	jumpForward :height / 2
	setPenGradient createRedBlackGradient :height / sqrt 2 false 0
	jumpTo :oldPos
	setLineCap "butt
	setPenSize :height
	forward :height
	setTurtleState :oldState
end

to drawInnerCircles :maxRadius
	localmake "oldState turtleState
	localmake "step :maxRadius * 0.052
	setPenSize :maxRadius * 0.01
	setPenColor "black
	setFillColor "white
	localmake "r :maxRadius
	circle :r
	localmake "r :r - :step
	setFillColor "red
	circle :r
	localmake "r :r - :step
	setFillGradient createRedBlackGradient :r - :step * 1.3 false 0
	circle :r
	setTurtleState :oldState
end

to drawOuterShapes :height :innerHeight :outerHeight :width1 :width2 :color
	localmake "oldState turtleState
	localmake "oldPos pos
	jumpBackward :innerHeight / 2
	stripes :innerHeight :innerHeight [:color]
	repeat 2 [
		jumpTo :oldPos
		jumpBackward :height / 2
		stripes :width1 :height [:color]
		right 90
	]
	repeat 4 [
		jumpTo :oldPos
		jumpForward :height / 2 - :outerHeight
		stripes :width2 :outerHeight [:color]
		right 90
	]
	setTurtleState :oldState
end

to drawOuterStraightShapes :height
	localmake "redHeight :height * 0.989
	localmake "blackHeight :height * 0.943
	drawOuterShapes :height :height * 0.772 :redHeight * 0.088 :redHeight * 0.183 :redHeight * 0.44 "black
	drawOuterShapes :redHeight :redHeight * 0.771 :redHeight * 0.076 :redHeight * 0.172 :redHeight * 0.43 "red
	drawOuterShapes :blackHeight :blackHeight * 0.76 :blackHeight * 0.0315 :blackHeight * 0.1315 :blackHeight * 0.4 "black
end

to drawOuterCircles :maxRadius
	localmake "oldState turtleState
	setPenSize 0
	setFillColor "white
	circle :maxRadius
	setPenColor "black
	setPenSize :maxRadius * 0.019
	setFillColor "red
	circle :maxRadius * 0.965
	setFillColor "white
	circle :maxRadius * 0.9075
	setTurtleState :oldState
end

to drawRadiatingLeafPattern :radius
	localmake "oldState turtleState
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "gradientOffset :radius * 0.6
	localmake "arcsInfo [[-160 0] [50 0.3] [-92.30759747 0.14] [0.6874493805]]
	setPenSize 0
	repeat 8 [
		localmake "heading1 :oldHeading + repcount * 360 / 8
		setHeading :heading1
		jumpForward :gradientOffset
		setFillGradient createRedBlackGradient :radius - :gradientOffset true 0.4
		jumpForward :radius - :gradientOffset
		polyStart
		arcLines :arcsInfo -:radius
		setHeading :heading1 + 180 / 8
		arcLines reverse :arcsInfo -:radius
		polyEnd
		jumpTo :oldPos
	]
	setFillColor "#810100
	localmake "innerRadius :radius * 0.815
	localmake "arcAngle 90
	localmake "chordLength :innerRadius * 2 * (sin 180 / 8)
	localmake "arcRadius :chordLength / 2 / sin :arcAngle / 2
	jumpForward :innerRadius
	right 90 + :arcAngle - 180 / 8
	polyStart
	repeat 8 [
		arcLeft :arcAngle :arcRadius
		right 360 / 8 + :arcAngle
 	]
	polyEnd
	setTurtleState :oldState
end

to drawTriangles :maxRadius
	localmake "oldState turtleState
	localmake "colors ["white "black "red "white "black]
	localmake "minRadius :maxRadius * 0.26
	setPenColor "black
	setPenSize :maxRadius * 0.007
	right 180
	repeat count :colors [
		localmake "r mix :minRadius :maxRadius repRatio
		setFillColor item repcount :colors
		regularPolygon :r 3
	]
	setTurtleState :oldState
end

; Inspired by
; https://en.wikipedia.org/wiki/Yantra#/media/File:Kali_Yantra.jpg
to kaliYantra :height
	localmake "oldState turtleState
	drawBackground :height
	jumpForward :height / 2
	drawOuterStraightShapes :height * 0.927
	drawOuterCircles :height * 0.319
	drawRadiatingLeafPattern :height * 0.28646
	drawInnerCircles :height * 0.19
	drawTriangles :height * 0.158
	setFillColor "red
	setPenSize 0
	circle :height * 0.008
	setTurtleState :oldState
end

kaliYantra 100