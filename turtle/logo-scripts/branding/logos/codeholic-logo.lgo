; Inspired by logo at:
; https://www.youtube.com/@TheCodeholic
;
; That youtube channel has some good tutorials related to web development.
to codeholicLogo :height
	localmake "oldState turtleState
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "slashHeight :height * 0.29
	localmake "bracketThickness :height * 0.042
	setPenSize 0
	jumpForward :height
	setFillGradient createLinearGradient :oldPos pos createPList2 [
		[0 "#098796] [1 "#09415c]
	] "pad
	drawArcLineShape [0.432 -0.305
		[[22 0] [30 0.53] [30 0.4] [30 0.45]
		[80 0.51] [30 0.5] [30 0.49] [25 0.4]
		[30 0.48] [29.5 0.5] [105 0] [0.2] [63.5 0]
		[-50 0.3] [-80 0.272] [-40 0.36] [-64 0.27] [-60 0.29]]
	] :height
	jumpBackward :height / 2
	localmake "center pos
	jumpBackward :slashHeight / 2
	jumpLeft :height * 0.03
	parallelogram :height * 0.033 :slashHeight 13

	repeat 2 [
		jumpTo :center
		setHeading :oldHeading + (repcount + 0.5) * 360 / 2
		jumpBackward :height * 0.2
		parallelogram :bracketThickness :height * 0.155 30
		parallelogram :bracketThickness :height * 0.155 -30
	]
	setTurtleState :oldState
end

codeholicLogo 100