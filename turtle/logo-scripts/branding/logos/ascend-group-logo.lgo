; Inspired by logo at:
; https://en.wikipedia.org/wiki/File:Ascend_Group_Co_Ltd.jpg
to ascendGroupLogo :height
	localmake "oldState turtleState
	localmake "circleThickness :height * 0.262
	localmake "rightHeight :height * 0.96
	setFillColor transparent
	jumpForward :height / 2
	jumpLeft :height * 0.075
	localmake "center pos
	setPenSize :circleThickness
	setPenColor "#e42326
	circle (:height - penSize) / 2

	jumpRight :height * 0.405
	jumpBackward :rightHeight / 2
	stripes penSize :rightHeight [penColor]
	jumpLeft penSize / 2
	jumpForward distanceToCircle :center :height / 2
	setPenSize 0
	setFillColor "#79212d
	localmake "arcAngle1 (towards :center) + 90 - heading
	right :arcAngle1
	polyStart
	arcLeft :arcAngle1 * 2 :height / 2
	polyEnd
	setTurtleState :oldState
end

ascendGroupLogo 100