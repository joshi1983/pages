; Inspired by
; https://en.wikipedia.org/wiki/Hewlett-Packard#/media/File:HP_logo_2012.svg
to hpLogo :height
	localmake "oldState turtleState
	localmake "oldHeading heading
	localmake "radius :height / 2
	localmake "cornerRadiusRatio 0.126
	localmake "width :height * 0.08
	localmake "heightRatio1 0.79
	jumpForward :height / 2
	localmake "center pos
	left 178.3
	jumpForward :radius
	setPenSize 0
	setFillColor "#0096D6
	polyStart
	arcLines [[90 0] [184.2 1] [67.5 0] [0.599] [110 0] [0.1892]
	[-110 :cornerRadiusRatio] [0.75] [-70 0] [0.21] [-110 0] [:heightRatio1]
	[110 0] [0.16] [70 0] [:heightRatio1] [-70 0] [0.21] [-110 0]] :radius
	jumpForward distanceToCircle :center :radius
	setHeading (towards :center) + 90
	arcLines [[150.8 1]
	[83.4917 0] [1.499] [-69.8 0] [0.403] [-110 :cornerRadiusRatio]
	[0.662] [-70 :cornerRadiusRatio] [0.285]] :radius
	polyEnd
	right 110
	jumpForward :height * 0.07
	setHeading :oldHeading
	jumpRight :width / 2
	parallelogram :width :height * 0.325 20
	setTurtleState :oldState
end

hpLogo 100