; Inspired by
; https://www.youtube.com/watch?v=flD-aRMwcjs
; That 23 second clip does a great job of explaining
; the logo, what it is for, and the person it is to resemble.

; There is a comment from Helen Tudor-Douglas claiming her mother drew 
; the original and Alfred Hitchcock removed one of her arcs 
; leading to the design in the video.

; To learn more about Alfred Hitchcock Presents, click
; https://en.wikipedia.org/wiki/Alfred_Hitchcock_Presents
; Some episodes are available for free on youtube.

to alfredHitchcockPresentsLogo :height
	localmake "oldState turtleState
	localmake "oldPos pos
	localmake "oldHeading heading
	setLineCap "round
	setPenSize :height * 0.008
	localmake "arcs [
		[-0.255 0 16 53 0.85]
		[0.004 0.46 -70 110 0.151]
		[-0.1005 0.677 55 -64 0.08]
		[-0.072 0.78 -13 72 0.155]
		[0.022 0.96 70 28 0.45]
		[0.065 0.959 74.5 16 0.4]
		[0.1 0.953 79 5 0.37]
	]

	repeat count :arcs [
		localmake "arcInfo item repcount :arcs
		jumpTo :oldPos
		jumpRight :height * item 1 :arcInfo
		jumpForward :height * item 2 :arcInfo
		right item 3 :arcInfo
		localmake "arcAngle item 4 :arcInfo
		localmake "arcRadius :height * item 5 :arcInfo
		ifelse :arcAngle < 0 [
			arcLeft -:arcAngle :arcRadius
		] [
			arcRight :arcAngle :arcRadius
		]
		setHeading :oldHeading
	]

	jumpTo :oldPos
	setHeading :oldHeading
	jumpLeft :height * 0.082
	jumpForward :height * 0.54
	left 128
	; upper lip
	forward :height * 0.03

	jumpTo :oldPos
	setHeading :oldHeading
	jumpLeft :height * 0.0625
	jumpForward :height * 0.5067
	left 95
	; lower lip
	forward :height * 0.033

	setTurtleState :oldState
end

alfredHitchcockPresentsLogo 100