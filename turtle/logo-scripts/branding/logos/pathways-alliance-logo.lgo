; Inspired by logo for a company at:
; https://pathwaysalliance.ca
to pathwaysAllianceLogo :height
	localmake "oldState turtleState
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "gradientWidth :height * 0.7
	localmake "colorStops createPList2 [
		[0 "#00dae5] [1 "#0c4ce9]
	]
	localmake "outerArcRadius :height * 0.47
	localmake "innerArcRadius :height * 0.385
	localmake "angle1 53
	localmake "angle2 39.64
	localmake "arcs [
		[150 150]
		[155 155]
	]
	setPenSize 0
	jumpLeft :gradientWidth * 0.2
	localmake "left pos
	left 20
	jumpRight :gradientWidth
	setFillGradient createLinearGradient :left pos :colorStops "pad

	jumpTo :oldPos
	setHeading :oldHeading
	drawArcLineShape [0 0.58
		[[-:angle1 0]
		[:angle2 0.74] [:angle1 - :angle2 - 90 0] [0.06]
		[-90 0] [-40 0.62] [-15 1] [20 1] [18.78 0.61] [-73.8 0]
		[0.065] [-90 0] [-35 0.49] [-21 1.11] 
	]
	] :height

	jumpForward :height * 0.5
	localmake "center pos
	repeat 2 [
		localmake "arcInfo item repcount :arcs
		setHeading :oldHeading + 6.5 + repcount * 185
		jumpBackward :outerArcRadius
		right 90
		polyStart
		arcLeft first :arcInfo :outerArcRadius
		left 80
		jumpForward distanceToCircle :center :innerArcRadius
		setHeading (towards :center) - 90
		arcRight item 2 :arcInfo :innerArcRadius 
		polyEnd
		jumpTo :center
	]
	setTurtleState :oldState
end

pathwaysAllianceLogo 100