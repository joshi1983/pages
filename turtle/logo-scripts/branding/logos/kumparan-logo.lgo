; Inspired by logo and favicon at:
; https://kumparan.com
to kumparanLogo :height
	localmake "oldState turtleState
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "bottomArcs [[50 0.431] [40 0.295] [0.245] [145 0]
		[-35 0.27] [60 0.18] [0.005] [-80 0.1561588235]]
	localmake "topArcs [
		[20 0] [54 0.18] [-55 0.2] [-19 0] [0.3] [-70 0.0405]
		[-25 0.09] [-30 0.3] [-35 0.09] [-20 0.1551305]
	]
	right 90
	setPenSize 0
	setFillColor "#00a2b1
	polyStart
	arcLines :bottomArcs :height
	arcLines reverse :bottomArcs :height
	polyEnd
	setFillColor "#ee3b25
	jumpTo :oldPos
	setHeading :oldHeading
	jumpForward :height * 0.418
	localmake "middle pos
	repeat 2 [
		localmake "sign sign repcount - 1.5
		jumpTo :middle
		setHeading :oldHeading
		jumpLeft :sign * :height * 0.147
		polyStart
		arcLines :topArcs :sign * :height
		polyEnd
	]
	setTurtleState :oldState
end

kumparanLogo 100