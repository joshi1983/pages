; Inspired by logo at:
; https://en.wikipedia.org/wiki/Acorn_Archimedes#/media/File:Archimedes_Computer_Logo_Recreation_cropped.svg
to archimedesLogo :height
	localmake "oldState turtleState
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "thickness :height * 0.07
	localmake "angle1 37.9
	localmake "thickness2 :thickness / cos :angle1
	localmake "lines1 [
		[0.46] [45 0] [90 0.30363]
	]
	stripes :height :height ["#019ade]
	jumpForward :height
	localmake "top1 pos
	jumpRight 100
	localmake "top2 pos

	jumpTo :oldPos
	jumpRight :height * 0.316
	setPenSize 0
	setFillColor "#f2ce2e
	localmake "startPos pos

	polyStart
	arcLines :lines1 :height
	left 45 - :angle1
	jumpForward distanceToLine :oldPos :startPos
	polyEnd

	jumpTo :startPos
	setHeading :oldHeading
	setFillColor "#f6564a
	polyStart
	arcLines :lines1 :height
	right 180 + :angle1 - 45
	jumpForward distanceToLine :top2 :top1
	polyEnd

	jumpTo :startPos
	setHeading :oldHeading
	stripes :thickness :height ["white]
	jumpForward :height
	jumpLeft (:thickness2 - :thickness) / 2
	left 180
	setFillColor "white
	parallelogram :thickness2 :height / cos :angle1 :angle1

	jumpTo :startPos
	setHeading :oldHeading
	setLineJoinStyle "round
	setLineCap "butt
	setPenSize :thickness
	setPenColor "white
	arcLines :lines1 :height

	setTurtleState :oldState
end

archimedesLogo 100