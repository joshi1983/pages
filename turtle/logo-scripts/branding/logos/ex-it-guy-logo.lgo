; Inspired by logo at:
; https://www.youtube.com/@ex-itguy
to exITGuyLogo :height
	localmake "oldState turtleState
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "headRadius :height * 0.098

	setPenColor "#338000
	setPenSize :height * 0.095
	setLineCap "round
	setLineJoinStyle "miter
	jumpForward :height * 0.56
	jumpLeft :height * 0.423
	right 90
	forward :height * 0.175
	left 50
	forward :height * 0.23
	setHeading :oldHeading + 90
	forward :height * 0.31
	right 59
	forward :height * 0.18

	setFillColor penColor
	jumpTo :oldPos
	setHeading :oldHeading
	setPenSize 0
	drawArcLineShape [-0.285 0
		[[-90 0] [0.1] [62.5 0.05] [0.44] [-149 0] [0.22]
		[85.5 0.04] [0.41] [90 0] [90 0.1] [0.22] [-86 0.02]
		[0.19] [21 0.1] [0.3] [90 0] [0.22] [91 0] [0.21] [-50 0.05]]
	] :height

	jumpForward :height - :headRadius
	jumpLeft :height * 0.1
	circle :headRadius
	setTurtleState :oldState
end

setScreenColor "#c6e9af
exITGuyLogo 100