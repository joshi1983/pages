make "computerHopeBlue "#0378e2

; Inspired by logo at:
; https://www.computerhope.com
to computerHopeLogo :height :backgroundColor :foregroundColor
	localmake "oldState turtleState
	localmake "oldHeading heading
	localmake "radius :height / 2
	localmake "thickness :height * 0.186
	localmake "tinyThickness :height * 0.05
	localmake "angle 18.3
	stripes :height :height [:backgroundColor]
	jumpForward :radius
	localmake "center pos
	setPenSize :thickness
	setPenColor :foregroundColor
	setFillColor transparent
	circle :radius - penSize / 2
	setPenSize 0
	setFillColor :foregroundColor
	jumpRight :height * 0.065
	right :angle
	localmake "chordLength distanceToCircle :center :radius
	right 180
	localmake "len2 distanceToCircle :center :radius
	localmake "chordLength :chordLength + :len2
	localmake "arcAngle 2 * arcSin :chordLength / 2 / :radius
	localmake "chordPos pos
	jumpForward :len2
	setHeading (towards :center) + 90
	polyStart
	arcLeft :arcAngle :radius
	polyEnd
	setHeading :oldHeading - 90 + :angle
	jumpTo :chordPos
	jumpRight :height * 0.35
	stripes :height * 0.3 :tinyThickness [:backgroundColor]
	jumpBackward :height * 0.243
	jumpLeft :height * 0.35
	setFillColor :backgroundColor
	parallelogram :height * 0.892 :tinyThickness / cos :angle -:angle 
	jumpTo :center
	setHeading :oldHeading + 8
	setPenSize :tinyThickness
	setPenColor :backgroundColor
	setLineCap "butt
	jumpBackward :radius - :thickness - :tinyThickness / 2
	right 90
	arcLeft 18 :radius * 1.5
	setTurtleState :oldState
end

make "height 100
make "separation :height * 5
jumpLeft :separation / 2
computerHopeLogo :height :computerHopeBlue "white
jumpRight :separation
computerHopeLogo :height "white :computerHopeBlue