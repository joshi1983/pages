; Inspired by
; https://commons.wikimedia.org/wiki/File:Audible_logo.svg
to audibleLogo :height
	localmake "oldState turtleState
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "angle1 58
	localmake "size1 :height * 0.8089642 / sin :angle1
	localmake "size2 :height * 0.137
	setPenSize 0
	setFillColor "#F8991C
	left :angle1
	polyStart
	jumpForward :size1
	setHeading :oldHeading
	jumpForward :size2
	right 180 - :angle1
	jumpForward :size1
	setHeading :oldHeading + :angle1
	jumpForward :size1
	setHeading :oldHeading
	jumpBackward :size2
	polyEnd

	jumpTo :oldPos
	setHeading :oldHeading
	jumpForward :height * 0.25
	localmake "center pos
	localmake "arcsInfo [
		[111.2 96.2 161 0.245 0.287]
		[113.2 96.8 166 0.505 0.521]
		[114.3 104 171 0.725 0.75]
	]
	jumpForward :height * 0.014
	localmake "startPos pos
	right :angle1
	repeat count :arcsInfo [
		localmake "arcInfo item repcount :arcsInfo
		localmake "radius1 :height * last :arcInfo
		localmake "radius2 :height * item 4 :arcInfo
		localmake "angle2 item 1 :arcInfo
		localmake "angle3 item 2 :arcInfo
		jumpTo :startPos
		setHeading :oldHeading + :angle1
		jumpForward distanceToCircle :center :radius1
		setHeading (towards :center) + 90
		polyStart
		arcLeft :angle2 :radius1
		left item 3 :arcInfo
		arcRight :angle3 :radius2
		polyEnd
	]
	setTurtleState :oldState
end

audibleLogo 100