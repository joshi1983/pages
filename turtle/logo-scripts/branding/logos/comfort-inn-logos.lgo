to backgroundGradient :height :separation
	localmake "oldState turtleState
	localmake "oldPos pos
	localmake "width :separation * 4
	localmake "colorStops createPList2 [
		[0 "black] [0.1 "black] [0.4 "#005f9f]
		[0.6 "#005f9f] [0.9 "white] [1 "white]
	]
	jumpRight :separation * 2
	setPenGradient createLinearGradient :oldPos pos :colorStops "pad
	setLineCap "butt
	setPenSize :width
	jumpTo mix pos :oldPos 0.5 ; go to middle.
	forward :height
	setTurtleState :oldState
end

; Inspired by logo at
; https://logos.fandom.com/wiki/Comfort_Inn
; More specifically:
; https://static.wikia.nocookie.net/logopedia/images/6/65/Comfort-inn-3.svg/revision/latest/scale-to-width-down/1000
to comfortInnLogo1981 :height
	localmake "oldState turtleState
	localmake "oldHeading heading
	localmake "radius :height / 2
	localmake "slicesInfo [
		[90 22.5 "#ce4036]
		[14 -11 "#dc7f32]
		[-20 -40.2 "#e8b236]
		[-51.3 -90 "#fcf125]
	]
	setPenSize 0
	jumpForward :radius
	localmake "center pos
	repeat count :slicesInfo [
		localmake "sliceInfo item repcount :slicesInfo
		setFillColor item 3 :sliceInfo
		localmake "fromAngle first :sliceInfo
		localmake "toArcAngle item 2 :sliceInfo
		localmake "angleDelta :fromAngle - :toArcAngle
		localmake "len :radius * 2 * cos :fromAngle
		setHeading :oldHeading + 90 + :toArcAngle
		jumpForward :radius
		right 90
		polyStart
		ifelse :fromAngle = 90 [
			arcRight 2 * :angleDelta :radius
		] [
			arcRight :angleDelta :radius
			right 90 - :fromAngle
			jumpForward :len
			right 90 - :fromAngle
			arcRight :angleDelta :radius
		]
		polyEnd
		jumpTo :center
	]
	setTurtleState :oldState
end

; Inspired by logo at
; https://logos.fandom.com/wiki/Comfort_Inn
; More specifically:
; https://static.wikia.nocookie.net/logopedia/images/6/68/Comfort_Inn.svg
to comfortInnLogo2015 :height
	localmake "oldState turtleState
	localmake "oldHeading heading
	localmake "radius :height / 2
	localmake "stripesInfo [
		[37
			[
				[99 1] [166 0] [-52 0.82] [-2 1] [7 3.5] [23.7 0.888]
			]
			"#d81f27
		]
		[11.5
			[
				[20.5 1] [80 0] [-32 0.66] [-10 2.5] [10 2]
				[52.81 0.68] [15.58288 0] [23 1] [150 0]
				[-50 0.73] [-25 1.2] [7 4] [44.289 0.5965155]
			]
			"#e67927
		]
		[-16.3
			[
				[22.5 1] [116 0] [-30 0.5] [-20 1.1] [-4 6]
				[20 1.2] [57.5355 0.66] [32.99536 0] [22.5 1]
				[125 0] [-35 0.59] [-40 0.95] [-5 2] [9 3]
				[49.77 0.4326177]
			]
			"#e6aa26
		]
		[-23.3
			[
				[180 0] [-130.8 1] [-66.7 0] [-60 0.68]
				[-10 1.05] [-3 2] [8 3] [20 0.9] [35.85 0.3473]
			]
			"#ffe40e
		]
	]
	jumpForward :radius
	localmake "center pos
	setPenSize 0
	repeat count :stripesInfo [
		localmake "stripeInfo item repcount :stripesInfo
		setHeading :oldHeading - 90 - first :stripeInfo
		jumpForward :radius
		left 90
		setFillColor last :stripeInfo
		polyStart
		arcsLeft (item 2 :stripeInfo) :radius
		polyEnd
		jumpTo :center
	]
	setTurtleState :oldState
end

; Inspired by logo at
; https://logos.fandom.com/wiki/Comfort_Inn
; More specifically:
; https://static.wikia.nocookie.net/logopedia/images/d/d0/Comfort_Inn_2018.svg
to comfortInnLogo2018 :height
	localmake "oldState turtleState
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "radius :height * 0.4
	setPenSize 0
	setFillColor "#ff5e03
	jumpForward :height * 0.27
	jumpRight :height * 0.402
	polyStart
	jumpBackward :height * 0.0117
	left 129.5
	jumpForward :height * 0.3
	arcsRight [[40 0.7] [30 0.81] [40 1.4] [70 0.8]] :radius
	jumpForward :height * 0.4
	right 179
	arcLeft 160 :radius
	polyEnd

	setFillColor "#ffb901
	jumpTo :oldPos
	setHeading :oldHeading
	jumpForward :height * 0.745
	jumpLeft :height * 0.14
	right 20
	polyStart
	arcsRight [[50 0.37] [50 0.3] [21 0.33]] :height
	right 90
	jumpForward :height * 0.33
	arcRight 83 :height * 0.02
	arcLeft 60 :height * 0.3
	polyEnd

	setPenColor "#fd9c11
	setPenSize :height * 0.035
	setLineCap "round
	jumpTo :oldPos
	setHeading :oldHeading
	jumpRight :height * 0.389
	jumpForward :height * 0.27
	left 108
	arcsRight [[40 0.4] [60 0.42] [40 0.34] [80 0.306] [18 0.5]] :height 	
	setTurtleState :oldState
end

make "height 100
make "separation :height * 3
jumpLeft :separation / 2
backgroundGradient :height :separation
comfortInnLogo1981 :height
jumpRight :separation
comfortInnLogo2015 :height
jumpRight :separation
comfortInnLogo2018 :height