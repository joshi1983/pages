to drawShape :width :height1 :height2
	localmake "oldState turtleState
	localmake "oldHeading heading
	localmake "angle arcTan :width / (:height2 - :height1)
	jumpLeft :width / 2
	polyStart
	jumpForward :height1
	right :angle
	jumpForward :width / sin :angle
	setHeading :oldHeading
	jumpBackward :height2
	polyEnd
	setTurtleState :oldState
end

; Inspired by logo at
; https://www.forth.com/forth/
to forthIncLogo :height
	localmake "oldState turtleState
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "slope1 0.89
	localmake "slope2 1.13
	localmake "stripeWidth1 :height * 0.033
	localmake "stripeWidth2 :height * 0.029
	localmake "maxLen :height - :stripeWidth1 * 8 * :slope1
	localmake "yOffset :height * 0.545
	localmake "xOffset :height * 0.051
	localmake "rowOpenings [
		[1 3]
		[0 2]
		[1 4]
		[1 3]
		[3]
	]
	jumpLeft :xOffset
	setFillColor "#772221
	setPenSize 0
	repeat 5 [
		localmake "minLen :maxLen - :stripeWidth1 * :slope1
		drawShape :stripeWidth1 :minLen :maxLen
		localmake "maxLen :maxLen + :slope1 * 2 * :stripeWidth1
		jumpRight :stripeWidth1 * 2
	]
	jumpTo :oldPos
	jumpForward :yOffset
	jumpRight :height * 0.582
	left 90
	localmake "maxLen :height * 0.89
	repeat 5 [
		localmake "minLen :maxLen - :stripeWidth2 * :slope2
		drawShape :stripeWidth2 :maxLen :minLen
		localmake "maxLen :maxLen + :slope2 * 2 * :stripeWidth2
		jumpLeft :stripeWidth2 * 2	
	]
	jumpTo :oldPos
	setHeading :oldHeading
	jumpForward :yOffset
	jumpLeft :xOffset
	jumpBackward :stripeWidth2 / 2
	repeat count :rowOpenings [
		localmake "rowInfo item repcount :rowOpenings
		localmake "pos1 pos
		repeat count :rowInfo [
			localmake "index item repcount :rowInfo
			jumpRight :stripeWidth1 * 2 * :index
			stripes :stripeWidth1 :stripeWidth2 ["white]
			jumpTo :pos1
		]
		jumpBackward :stripeWidth2 * 2
	]	
	setTurtleState :oldState
end

; Inspired by logo at
; https://forth-standard.org/
to forthStandardLogo :height
	localmake "oldState turtleState
	localmake "oldPos pos
	localmake "separation1 :height * 0.435
	localmake "separation2 :height * 0.325
	localmake "darkColor "#424242
	localmake "width1 :height * 0.24
	localmake "height1 :height * 0.195
	stripes :height :height ["#ef5350]
	jumpLeft :separation1 / 2
	jumpForward :height * 0.27
	repeat 2 [
		localmake "pos1 pos
		repeat 2 [
			stripes :width1 :height1 [:darkColor]
			jumpRight :separation1
		]
		jumpTo :pos1
		jumpForward :separation2
	]
	setFillColor :darkColor
	setPenSize 0
	jumpTo :oldPos
	jumpRight :height * 0.092
	jumpForward :height * 0.16
	polyStart
	jumpForward :height * 0.043
	jumpRight :height * 0.065
	jumpForward :height * 0.1
	jumpRight :height * 0.125
	jumpBackward :height * 0.085
	jumpLeft :height * 0.06
	jumpBackward :height * 0.058
	polyEnd
	setTurtleState :oldState
end

make "height 100
make "separation :height * 4
jumpLeft :separation / 2
forthIncLogo :height
jumpRight :separation
forthStandardLogo :height