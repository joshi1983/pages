; Inspired by logo at:
; https://royalfloors.ca
to royalFloorsLogo :height
	localmake "oldState turtleState
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "x1 0.179
	localmake "lines1 [
		[142 0] [0.183] [-76.5 0.033]
		[0.263] [142.6 0] [0.237] [61 0]
		[0.048] [119 0] [0.14] [-143 0]
		[0.12] [7 0.3] [68.5 0.065]
		[0.1336599] [-50.6 0]
	]
	localmake "thickness 0.056
	localmake "angle1 137
	localmake "angle2 65
	localmake "rAngle1 28.5
	localmake "gap :height * 0.016
	setPenSize 0
	setFillColor "#d9a23d
	drawArcLineShape [-:x1 1
		combine :lines1 reverse :lines1
	] :height
	setFillColor "black
	jumpForward :height * 0.471
	jumpRight :height * 0.06
	localmake "rCenter pos
	jumpForward :height * 0.223
	jumpRight :height * 0.092
	polyStart
	localmake "arcRadius1 distance :rCenter
	localmake "startPos pos
	arcLines [[-90 0] [0.417] [-:angle1 0] [:thickness / sin :angle1]
		[:angle1 - 180 0]] :height
	jumpForward distanceToCircle :rCenter :arcRadius1
	setHeading (towards :rCenter) + 90
	localmake "arcAngle1 2 * arcSin (distance :startPos) / 2 / :arcRadius1
	arcLeft :arcAngle1 :arcRadius1
	polyEnd

	jumpTo :rCenter
	setHeading :oldHeading + 90
	jumpRight :thickness / 2 * :height
	jumpForward distanceToCircle :rCenter :arcRadius1
	polyStart
	localmake "startPos pos
	arcLines [[0.114] [:angle2 0] [:thickness / sin :angle2] [180 - :angle2 0]] :height
	jumpForward distanceToCircle :rCenter :arcRadius1
	setHeading (towards :rCenter) - 90
	localmake "arcAngle2 2 * arcSin (distance :startPos) / 2 / :arcRadius1
	arcRight :arcAngle2 :arcRadius1
	polyEnd

	localmake "arcRadius2 :arcRadius1 - :gap
	jumpTo :oldPos
	setHeading :oldHeading
	jumpRight :height * 0.2625
	polyStart
	localmake "startPos pos
	left :rAngle1
	jumpForward distanceToCircle :rCenter :arcRadius2
	setHeading (towards :rCenter) + 90
	localmake "rAngle2 (towards :rCenter) - :oldHeading
	arcLeft :rAngle2 + 360 - 180 :arcRadius2
	arcLines [[0.5] [:rAngle1 + 90 0]
		[0.282] [90 - :rAngle1 0] [0.295]
		[90 - :rAngle1 0] [0.194] [90 + :rAngle1 0]
		[0.067] [90 - :rAngle1 0] [0.129] [:rAngle1 - 90 0]
		[0.225] [:rAngle1 - 90 0] [0.15] [-:rAngle1 - 90 0]
		] :height
	localmake "d distance :rCenter
	localmake "a (towards :rCenter) - (:oldHeading + 90)
	arcLines [[cos :a] [-180 sin :a] [0.07] [90 + :rAngle1 0]] :d
	localmake "prevPos pos
	jumpForward distanceToLine :oldPos :startPos
	polyEnd

	jumpTo :prevPos
	setHeading :oldHeading - 90
	jumpForward :gap / cos :rAngle1
	polyStart
	arcLines [[0.306] [90 - :rAngle1 0]] :height
	jumpForward distanceToLine :startPos :oldPos
	setHeading :oldHeading + 90
	jumpForward :thickness * :height / cos :rAngle1
	left 90 - :rAngle1
	jumpForward :height * 0.28
	right 90 - :rAngle1
	jumpForward :height * 0.307
	polyEnd

	setTurtleState :oldState
end

royalFloorsLogo 100