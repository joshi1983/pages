; If you want to use flickr's logo, see their guidelines:
; https://www.flickrhelp.com/hc/en-us/articles/4404071066260-Brand-guidelines
; The following code is for educational purposes.
; This WebLogo code doesn't draw exactly the same logo 
; but was traced to look very similar.

make "letterBottomGapRatio 0.015515

; Inspired by the circles shown at
; https://www.flickr.com/
;
; https://en.wikipedia.org/wiki/Flickr
; The flickr logo is explained at:
; https://logos-world.net/flickr-logo/
; The logos-world article says, "The symbol consists of two circles: blue and pink. 
; The blue circle symbolizes professionalism, trust, and reliability, 
; while the pink circle symbolizes passion and creativity. 
; These attributes are essential for a platform where users share 
; photos and interact with the community."
to flickrCirclesSymbol :height
	localmake "oldState turtleState
	localmake "separation :height * 1.25
	jumpForward :height / 2
	jumpLeft :separation / 2
	setPenSize 0
	setFillColor "#0063dc
	circle :height / 2
	jumpRight :separation
	setFillColor "#ff0084
	circle :height / 2
	setTurtleState :oldState
end

to flickrCirclesOnBlackBackground :height
	stripes :height * 3.5 :height ["black]
	flickrCirclesSymbol :height
end

to drawF :height
	localmake "oldState turtleState
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "baseWidth :height * 0.224
	setPenSize 0
	jumpForward :letterBottomGapRatio * :height
	jumpLeft :baseWidth / 2
	polyStart
	arcLines [[0.71] [-20 0.42] [-50 0.185] [-9 0.3] [-25 0.48]
	[-80.95 0] [0.1606] [-105 0] [15 0.16] [4.9 0.4] [65 0.071] [13 0.1]
	[12.05 0.2] [0.7176992]] :height
	polyEnd

	; Draw horizontal part
	jumpTo :oldPos
	setHeading :oldHeading
	jumpForward :height * 0.52585
	jumpRight :height * 0.0135
	stripes :height * 0.516 :height * 0.16277706 [fillColor]
	setTurtleState :oldState
end

to drawL :height
	localmake "oldPos pos
	localmake "gap :height * :letterBottomGapRatio
	jumpForward :gap
	stripes :height * 0.2245 :height - :gap * 2 [fillColor]
	jumpTo :oldPos
end

to drawI :height
	localmake "oldState turtleState
	jumpForward :height * :letterBottomGapRatio
	setLineCap "butt
	setPenColor fillColor
	setPenSize :height * 0.2245
	
	forward :height * 0.6728
	jumpForward :height * 0.11
	forward :height * 0.1707
	setTurtleState :oldState
end

to drawC :height
	localmake "oldState turtleState
	jumpRight :height * 0.258
	jumpForward :height * 0.03
	setPenSize 0
	polyStart
	arcLines [[109 0] [-30 0.55] [-30 0.36] [-40 0.31] [-23 0.46]
	[-40 0.29] [-43 0.42] [-15.7 0.5] [-72.45 0] [0.1715] [-118 0]
	[30 0.23] [23 0.23] [50 0.15] [35 0.23] [50 0.15] [53.7 0.26]] :height
	polyEnd
	setTurtleState :oldState
end

to drawK :height
	localmake "oldState turtleState
	localmake "oldHeading heading
	localmake "gap :height * :letterBottomGapRatio
	localmake "angle 33.875
	localmake "len1 :height * 0.4388
	localmake "len2 :height * 0.3826
	jumpForward :gap
	jumpLeft :height * 0.115
	stripes :height * 0.223 :height - :gap * 2 [fillColor]
	jumpRight :height * 0.293
	setPenSize 0
	polyStart
	jumpRight :height * 0.2727
	left :angle
	jumpForward :len1
	right 70.05
	jumpForward :len2
	setHeading :oldHeading - 90
	jumpForward :height * 0.246
	left 56.56
	jumpForward :height * 0.36968
	polyEnd
	setHeading :oldHeading
	jumpBackward :height * 0.03135
	stripes :height * 0.1 :height * 0.05683 [fillColor]
	setTurtleState :oldState
end

to drawR :height
	localmake "oldState turtleState
	jumpForward :height * :letterBottomGapRatio
	setPenSize 0
	polyStart
	arcLines [[90 0] [0.2245] [-90 0] [0.6731] [-90 0] [0.20455] [-90 0]
	[0.13] [160 0] [-30 0.25] [-38 0.165] [-11.8 0.47] [-83.6 0] [0.194]
	[-99.7 0] [20 0.295] [55 0.14] [30 0.25990117]] :height
	polyEnd
	setTurtleState :oldState
end

; Inspired by
; https://en.wikipedia.org/wiki/Flickr#/media/File:Flickr_wordmark.svg
to flickrLogo :height
	localmake "oldState turtleState
	jumpLeft :height * 1.409
	setFillColor "#0063db
	drawF :height
	jumpRight :height * 0.5013
	drawL :height
	jumpRight :height * 0.4165
	drawI :height
	jumpRight :height * 0.55
	drawC :height
	jumpRight :height * 0.58
	drawK :height
	jumpRight :height * 0.7587
	setFillColor "#FF0084
	drawR :height
	setTurtleState :oldState
end

make "height 100
make "separation :height * 6
jumpLeft :separation
flickrCirclesSymbol :height
jumpRight :separation
flickrLogo :height
jumpRight :separation
flickrCirclesOnBlackBackground :height