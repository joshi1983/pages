; Inspired by logo at:
; https://www.thecanadianencyclopedia.ca/
;
; Here are some comments on differences from that logo:
;
; 1. The original maple leaf was not very symmetrical.
; The following code draws with the left side matching the original logo's 
; shape fairly well but reflects that over the right side 
; which does not line up well with the original logo.
; The original 
; 2. The original logo included lots of text in multiple 
; languages in the red parts.  The text says stuff like:
;  "NORTH", "O'CANADA", "EH", "ZED", "ART", "CULTURE"...
; The code below does not render any text.
; The reason is basically to keep the code simpler.
to canadianEncyclopediaLogo :height
	localmake "oldState turtleState
	localmake "innerRadius :height * 0.341
	localmake "arcsInfo [
		[153 1] [145 0.09] [50 0.2] [-25 0.05]
		[0.08] [15 0.15] [-35 0.1] [-98 0] [0.08]
		[40 0] [61 0.1] [0.1] [-50 0.04] [10 0.2]
		[34 0.05] [0.16] [-10 1.05] [-146 0.007]
		[-30 0.15] [23 0.16] [25 0.53] [40 0.06]
		[20 0.13] [-76 0.015] [0.14] [50 0.02]
		[0.03] [70 0.025] [20 0.15] [-20 0.2] [20 0.2]
		[0.05] [-11 0.2] [0.095] [-143 0.01] [30 0.1]
		[50 0.045] [38 0.1] [4 1] [-24 0.07] [0.03]
		[25 0.05] [0.1] [13 0.6] [-12 0.32] [-40 0.02]
		[-36.190518 0.005]
	]
	jumpForward :height / 2
	setPenSize :height * 0.074
	setPenColor "#EB2126
	setFillColor transparent
	circle (:height - penSize) / 2
	left 35.5
	jumpForward :innerRadius
	left 90
	setFillColor penColor
	setPenSize 0
	polyStart
	arcLines :arcsInfo :innerRadius
	arcLines reverse :arcsInfo :innerRadius
	polyEnd
	setTurtleState :oldState
end

canadianEncyclopediaLogo 100