to dynatraceMove :height :xSign :rotation
	jumpForward :height * 0.69543235
	jumpRight :xSign * :height * 0.16333569
	left 84.5 - :rotation
end

to dynatraceShape :height :xSign :rotation
	localmake "oldState turtleState
	localmake "arcLinesInfo [
		[3 2.5] [4 2.1] [6.54 2.65] [65.5 0.0958]
		[-164 0] [0.4 2.5] [-46.4 0.09] [0.404]
		[-47 0.095] [-7.19 4.1615]
	]
	dynatraceMove :height :xSign :rotation
	setPenSize 0
	polyStart
	ifelse :xSign > 0 [
		arcLines :arcLinesInfo :height
	] [
		arcLines :arcLinesInfo -:height
	]
	polyEnd
	setTurtleState :oldState
end

to dynatraceShadow :height :xSign :rotation
	localmake "oldState turtleState
	localmake "arcsInfo [
		[4 0] [6 2.7] [-126 0]
		[-34.5 0.76] [-22 0] [-6 0.1] [-162 0]
		[39 0.0375] [20 0.115] [10 0.53] [7.3 1.16]
	]
	dynatraceMove :height :xSign :rotation
	right -3.6 * :xSign
	jumpForward :height * 0.3
	setFillColor "#4000
	setPenSize 0
	polyStart
	ifelse :xSign > 0 [
		arcsLeft :arcsInfo :height
	] [
		arcsRight :arcsInfo :height
	]
	polyEnd
	setTurtleState :oldState
end

; Inspired by:
; https://en.wikipedia.org/wiki/File:Dynatrace_company_logo.png
; Also used at:
; https://www.dynatrace.com/
to dynatraceLogo :height
	localmake "oldState turtleState
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "sidesInfo [
		["#1496ff 0.492 0.51 -1 171.9 true]
		["#6f2da8 -0.002 1.001 1 -0.2 true]
		["#73be28 -0.493 0.493 -1 171.9 false]
		["#b4dc00 0 0 1 0 false]
	]
	localmake "numSides 4
	repeat :numSides [
		localmake "sideInfo item repcount :sidesInfo
		localmake "xSign item 4 :sideInfo
		localmake "rotation item 5 :sideInfo
		localmake "useShadow item 6 :sideInfo
		setHeading :oldHeading
		jumpRight :height * item 2 :sideInfo 
		jumpForward :height * item 3 :sideInfo 
		setHeading :oldHeading + 360 - repcount * 360 / :numSides
		setFillColor first :sideInfo
		dynatraceShape :height :xSign :rotation
		if :useShadow [
			dynatraceShadow :height :xSign :rotation
		]
		jumpTo :oldPos
	]
	setTurtleState :oldState
end

dynatraceLogo 100