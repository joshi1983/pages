; Inspired by a 2023 version of the Computerphile youtube channel logo.
; Referenced from:
; https://www.youtube.com/@Computerphile

; The ComputerPhile youtube channel has lots
; of great educational videos about computers,
; computer history, .

make "bracketAngle 60

to bracket :size
	localmake "oldState turtleState
	left 90 - :bracketAngle / 2
	forward :size
	arcRight 180 - :bracketAngle penSize / 2
	forward :size
	setTurtleState :oldState	
end

to computerPhileC :height
	localmake "arcs1 [[0.218] [-62 0.16] [0.292] [-28 0.2]]
	jumpRight :height * 0.255
	jumpForward penSize / 2
	left 90
	arcLines combine :arcs1 reverse :arcs1 :height
end

to computerPhile2023Logo :height
	localmake "oldState turtleState
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "bracketSeparation :height * 1.15
	localmake "bracketSize :height * 0.55
	setLineCap "round
	setLineJoinStyle "round
	setPenColor "#14d70e
	setPenSize :height * 0.125
	jumpLeft :bracketSeparation / 2
	jumpForward :height * 0.17
	bracket :bracketSize
	jumpRight :bracketSeparation
	jumpForward (:bracketSize * 2 * sin :bracketAngle / 2) +
		penSize * sin (180 - :bracketAngle) / 2
	right 180
	bracket :bracketSize
	jumpTo :oldPos
	setHeading :oldHeading
	computerPhileC :height
	setTurtleState :oldState
end

to computerPhile2024Logo :height
	localmake "oldState turtleState
	localmake "oldPos pos
	localmake "x 0.55
	localmake "sideY 0.15
	localmake "len1 0.69
	localmake "len2 0.47
	localmake "sideAngle 65
	localmake "sideLinesInfo [[-:sideAngle 0] [:len1] [:sideAngle 0] [0.1] 
			[:sideAngle 0] [:len1] [180 - :sideAngle 0] [0.14] [:sideAngle 0]
			[:len2] [-2 * :sideAngle 0] [:len2]]
	localmake "cLines [[-40 0.38] [-30 0.2] [-50 0.3] [-5 0.5] [96 0]
		[0.2] [90 0] [70 0.532] [49 0.4] [0.06735137087172]]
	localmake "shapes [
		[:x :sideY :sideLinesInfo]
		[-:x :sideY flipAngles :sideLinesInfo]
		[-0.2 0.5 combine :cLines reverse :cLines]
	]
	localmake "color "#4bd110
	localmake "color2 mix :color "black 0.3
	localmake "colorStops createPList2 [
		[0 :color] [0.3 :color] [0.4 :color2] [0.7 :color2] [0.8 :color] [1 :color]
	]
	jumpLeft :height * 0.01
	localmake "fromPos pos
	jumpRight :height * 0.118
	setFillGradient createLinearGradient :fromPos pos :colorStops "repeat
	setPenSize 0
	jumpTo :oldPos
	drawArcLineShapes :shapes :height
	arcLines :cLines :height
	setTurtleState :oldState
end

setScreenColor "black
make "height 100
make "separation :height * 5
jumpLeft :separation / 2
computerPhile2023Logo :height
jumpRight :separation
computerPhile2024Logo :height