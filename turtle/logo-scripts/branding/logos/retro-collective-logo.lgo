to drawR :height
	localmake "oldState turtleState
	localmake "angle1 31
	jumpForward :height * 0.015
	jumpLeft :height * 0.295
	stripes :height * 0.175 :height * 0.96 [fillColor]
	drawArcLineShape [0.473 0
		[[:angle1 0] [0.44] [90 - :angle1 0] [0.3]
		[-90 0] [0.15] [-90 0] [0.36] [180 0.141]
		[0.36] [-90 0] [0.15084638] [-90 0] [0.395]
		[-162 0.286] [105 0] [0.48]]
	] :height
	setTurtleState :oldState
end

to drawC :height
	localmake "oldState turtleState
	localmake "arcLines1 [
		[-20 0.57] [-40 0.21] [-75 0.27]
		[-29 0.2] [90 0] [0.18] [90 0]
		[50 0.37] [60 0.49] [40 0.4] [14 0.75799013]
	]
	drawArcLineShape [-0.25 0.5
		combine :arcLines1 reverse :arcLines1
	] :height
	setTurtleState :oldState
end

; Inspired by logo at:
; https://www.youtube.com/@TheRetroCollective
to retroCollectiveLogo :height
	localmake "oldState turtleState
	localmake "colors [
		"#5ccaeb "#57bb65 "#ed1e24
	]
	localmake "squareSize :height * 0.238
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "fontHeight :height * 0.44
	stripes :height :height ["white]
	jumpForward :height * 0.13 + :squareSize / 2
	jumpLeft :squareSize / 2 * (count :colors)
	right 90
	stripes :squareSize :squareSize * (count :colors) :colors

	jumpTo :oldPos
	setHeading :oldHeading
	jumpForward :height * 0.395
	jumpLeft :height * 0.197
	setPenSize 0
	setFillColor "black
	drawR :fontHeight
	jumpRight :height * 0.35
	drawC :fontHeight
	setTurtleState :oldState
end

setScreenColor "#ddd
retroCollectiveLogo 100