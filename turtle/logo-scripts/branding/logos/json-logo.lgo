; Inspired by
; https://en.wikipedia.org/wiki/JSON#/media/File:JSON_vector_logo.svg
; Referenced from:
; https://en.wikipedia.org/wiki/JSON

to jsonShape :diameter
	localmake "oldState turtleState
	localmake "oldPos pos
	localmake "colorStops createPList2 [
		[0 "black] [1 "#fff]
	]
	left 45
	jumpBackward :diameter / 2
	localmake "fromPos pos
	jumpForward :diameter
	setFillGradient createLinearGradient pos :fromPos :colorStops "pad
	jumpTo :oldPos
	left 45
	setPenSize 0
	drawArcLineShape [-0.5 0
		[[-180 0.5] [-15 0.24] [-20 0.3] [-20 0.4] [-20 0.35]
		[-10 0.75] [-20 0.43] [-20 0.32] [-20 0.3] [-20 0.27] [-20 0.17]
		[-17.3 0.18] [-132 0] [30 0.263] [30 0.25] [30 0.32] [30 0.255]
		[60 0.165] [40 0.29] [20 0.37] [20.2 0.6] [50 0.38] [12 0.35]]
		] :diameter
	setTurtleState :oldState
end

to jsonLogo :diameter
	localmake "oldState turtleState
	jumpForward :diameter / 2
	jsonShape :diameter
	right 180
	jsonShape :diameter
	setTurtleState :oldState
end

jsonLogo 100