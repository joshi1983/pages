; Inspired by logo at:
; https://en.wikipedia.org/wiki/Cochrane_Library#/media/File:Cochrane_Library_logo.png
;
; It is also shown at:
; https://www.cochranelibrary.com
to cochraneLibraryLogo :height :innerColor :outerColor
	localmake "oldState turtleState
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "thickness :height * 0.025
	localmake "dashes [
		[-0.065 0.055] [0.005 0.1] [-0.115 0.15]
		[-0.06 0.2] [0.005 0.18] [-0.07 0.16]
		[-0.07 0.21] [-0.05 0.055]
	]
	localmake "outerGap :height * 0.228
	localmake "innerStep (:height + :thickness * 2 - :outerGap * 2) / count :dashes
	localmake "outerArcRadius :height / 2
	localmake "outerArcAngle 166
	localmake "innerArcRadius :height / 3
	stripes :thickness :height [:innerColor]
	jumpForward :outerGap
	repeat count :dashes [
		localmake "pair item repcount :dashes
		localmake "width :height * last :pair
		localmake "offset :height * first :pair
		jumpRight :offset
		stripes :width :thickness [:innerColor]
		if repcount = 1 [
			localmake "offset2 (:width - :thickness) / 2
			jumpBackward :offset2
			stripes :thickness :width [:innerColor]
			jumpForward :offset2
		]
		jumpLeft :offset
		jumpForward :innerStep
	]
	jumpTo :oldPos
	setFillColor :outerColor
	jumpForward :height / 2
	localmake "center pos
	setPenSize 0
	repeat 2 [
		localmake "heading1 :oldHeading + 90 + repcount * 360 / 2
		setHeading :heading1
		left 90 - (180 - :outerArcAngle) / 2
		jumpForward :outerArcRadius
		localmake "pos1 pos
		polyStart
		right 90
		arcRight :outerArcAngle :outerArcRadius
		localmake "pos2 pos
		setHeading :heading1 - 90
		jumpForward distanceToCircle :center :innerArcRadius
		setHeading (towards :center) + 90
		arcLeft 1 :innerArcRadius
		localmake "innerArcAngle getArcLeftAngleToLine :innerArcRadius :pos1 :pos2
		arcLeft :innerArcAngle :innerArcRadius
		polyEnd
		jumpTo :center
	]
	setTurtleState :oldState
end

; Shown in the footer at:
; https://www.cochranelibrary.com
to cochraneWhiteOnVioletLogo :height
	localmake "oldState turtleState
	localmake "innerHeight :height * 0.9
	stripes :height :height ["#962d91]
	jumpForward (:height - :innerHeight) / 2
	cochraneLibraryLogo :innerHeight "white "white
	setTurtleState :oldState
end

make "height 100
make "separation :height * 4
jumpLeft :separation / 2
cochraneLibraryLogo :height "#962d91 "#002d64
jumpRight :separation
cochraneWhiteOnVioletLogo :height