to drawWafer :height
	localmake "oldState turtleState
	localmake "oldHeading heading
	localmake "radius :height * 0.472
	localmake "arcAngle 310.5
	localmake "chipWidth :radius / 10
	localmake "chipHeight :radius / 6.55
	localmake "bottomHeight -:radius * cos :arcAngle / 2
	localmake "numVerticalLines 2 * int (:radius / :chipWidth)
	localmake "numHorizontalLines 1 + int ((:radius + :bottomHeight) / :chipHeight)

	; Information on which chips are to be drawn black.
	; Each sublist represents a row of chips.
	localmake "blackSpots [
		[-1]
		[-5]
		[-4 4]
		[] [] [] [] [] []
		[-4 0 5]
		[-5 -3 4]
		[-4]
	]
	setPenColor "black
	setPenSize :height * 0.005
	jumpForward :height - :radius - penSize / 2
	localmake "center pos
	right :arcAngle / 2
	jumpForward :radius
	left 90
	setLineCap "butt
	arcLeft :arcAngle :radius

	jumpTo :center
	setHeading :oldHeading
	jumpLeft :numVerticalLines * :chipWidth / 2
	repeat :numVerticalLines [
		localmake "len distanceToCircle :center :radius
		localmake "len2 min :len :bottomHeight
		jumpBackward :len2
		forward :len + :len2
		jumpBackward :len
		jumpRight :chipWidth
	]
	jumpTo :center
	setHeading :oldHeading + 90
	jumpRight :bottomHeight
	localmake "basePos pos
	repeat :numHorizontalLines [
		localmake "len distanceToCircle :center :radius
		jumpBackward :len
		forward :len * 2
		jumpBackward :len		
		jumpLeft :chipHeight
	]
	setHeading :oldHeading
	repeat count :blackSpots [
		localmake "rowInfo item repcount :blackSpots
		localmake "rowIndex :numHorizontalLines - repcount - 1
		jumpTo :basePos
		jumpForward :chipHeight * :rowIndex
		localmake "rowMiddlePos pos
		repeat count :rowInfo [
			jumpRight :chipWidth * (0.5 + item repcount :rowInfo)
			stripes :chipWidth :chipHeight ["black]
			jumpTo :rowMiddlePos
		]
	]	
	setTurtleState :oldState
end

to drawT :fontHeight
	localmake "oldState turtleState
	jumpRight :fontHeight * 0.04
	polyStart
	arcLines [[90 0] [-35 0.29] [-30 0.14] [-25 0.3]
	[0.443] [90 0.023] [0.105] [-90 0] [0.067] [-66 0] [0.23]
	[17 0.07] [16 0.2] [6.9 0.8] [-63.9 0] [0.112] [-90 0]
	[0.174] [90 0.015] [0.231] [-90 0] [0.104] [-90 0] [0.236]
	[90 0.015] [0.38] [20 0.28] [40 0.11] [40 0.15] [33 0.125]
	[-116 0] [0.058] [-60 0] [-47 0.365]] :fontHeight
	polyEnd
	setTurtleState :oldState
end

to drawS :fontHeight
	localmake "oldState turtleState
	jumpRight :fontHeight * 0.04
	jumpForward :fontHeight * 0.007
	polyStart
	arcLines [[90 0] [-12.8 1.3] [39 0.1] [-106 0] [0.2335] [-100 0]
	[0.077] [-51 0] [24 0.52] [30 0.17] [20 0.4] [35 0.07] [50 0.05]
	[40 0.1] [10 0.8] [0.07] [-15 0.8] [-30 0.24] [-30 0.1] [-40 0.2]
	[-40 0.37] [-14 0.7] [14.6 0.63] [-82 0] [0.221] [-90 0] [0.08]
	[-51 0] [0.24] [40 0.1] [50 0.14] [28 0.1] [58 0.035] [20 0.32]
	[-2 7] [-25 0.44] [-45 0.17] [-50 0.2] [-20 0.3] [-14.8 0.6]] :fontHeight
	polyEnd
	setTurtleState :oldState
end

to drawM :fontHeight
	localmake "oldState turtleState
	localmake "oldHeading heading
	localmake "pillarArcSide [
		[0.34] [-20 0.2] [-37 0.15] [-39 0.066] [96 0] [0.025] [90 0]
		[0.24]
	]
	localmake "pillarArcsInfo combine :pillarArcSide reverse :pillarArcSide
	localmake "singleArch combine :pillarArcsInfo [
		[20 0.5] [20 0.08] [10 0.4] [20 0.2] [30 0.25] [15 0.8] [0.11]
		[-80 0.036] [153 0] [0.2] [176 0] [-35 0.155] [-30 0.09] [-40 0.26]
		[-30 0.13] [-30 0.17] [-18 0.0880766]
	]
	jumpRight :fontHeight * 0.436
	jumpForward :fontHeight * 0.52
	localmake "rightPos pos
	right 180
	polyStart
	arcLines :singleArch :fontHeight
	polyEnd
	jumpTo :rightPos
	setHeading :oldHeading
	jumpLeft :fontHeight * 0.58
	localmake "middlePos pos
	right 180
	polyStart
	arcLines :singleArch :fontHeight
	polyEnd
	jumpTo :middlePos
	setHeading :oldHeading
	jumpLeft :fontHeight * 0.59
	right 180
	polyStart
	arcLines :pillarArcsInfo :fontHeight
	arcLines [[0.32] [90 0] [0.07] [30 0] [0.04] [-30 0.4] [0.05]
	[90 0] [0.08] [81 0] [0.105]] :fontHeight
	polyEnd
	setTurtleState :oldState
end

to drawC :fontHeight
	localmake "oldState turtleState
	polyStart
	arcLines [[90 0] [-45 0.54] [-40 0.29] [-30 0.5] [-30 0.25]
	[-30 0.73] [-30 0.6] [-32 0.3] [-50 0.065] [-60 0.09] [-15 0.4]
	[-40 0.08] [-25 0.3] [25 0.13] [40 0.11] [35 0.25] [30 0.38] [30 0.2]
	[30 0.3] [40 0.25] [40 0.55] [27 0.22] [-177 0.035] [-25 0.65] [-28 0.44]
	] :fontHeight
	polyEnd
	setTurtleState :oldState
end

to drawTSMCText :fontHeight
	localmake "oldState turtleState
	setPenSize 0
	jumpLeft :fontHeight * 1.742
	drawT :fontHeight
	jumpRight :fontHeight * 0.75
	drawS :fontHeight
	jumpRight :fontHeight * 1.22
	drawM :fontHeight
	jumpRight :fontHeight * 1.22
	drawC :fontHeight
	setTurtleState :oldState
end

; Inspired by
; https://en.wikipedia.org/wiki/TSMC#/media/File:Tsmc.svg
to tsmcLogo :height
	localmake "oldState turtleState
	localmake "tsmcRed "#E60012
	stripes :height * 1.04 :height * 0.038 [:tsmcRed]
	drawWafer :height
	jumpForward :height * 0.3836
	setFillColor :tsmcRed
	drawTSMCText :height * 0.319
	setTurtleState :oldState
end
tsmcLogo 100