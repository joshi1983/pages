; Inspired by a few different company logos using the name 
; "Everest Transport"
; These examples look like different levels of abstraction and minimalism
; while all depict Mount Everest.

; You can learn more about minimalism at:
; https://en.wikipedia.org/wiki/Minimalism

; A similar process of simplification is explained at:
; https://youtu.be/22osQzxenl8?t=193

make "everestTransportLogo1Angle 57

to triangle :height :isUp
	localmake "oldState turtleState
	localmake "angle2 :everestTransportLogo1Angle / 2
	localmake "width :height * 2 * tan :angle2
	ifelse :isUp [
		jumpForward :height
		right 180
		isoTriangle :width :height
	] [
		isoTriangle :width :height
	]
	setTurtleState :oldState
end

; This is inspired by a logo for an 
; Everest Transport company from Belgium.
; https://everesttransport.be
to everestTransportLogo1 :height
	localmake "oldState turtleState
	localmake "colors [
						"#d0111a
				"#aa0e16 "#cf1019 "#840b11
		"#5e080c "#aa0e16 "white "#840b11 "#1f1b4b
"#18153d "#362f87 "#1e1a4a "#2b246a "#1f1b4b "#1f1b4b "#2b2569
]
	localmake "angle2 :everestTransportLogo1Angle / 2
	localmake "size1 :height * tan :angle2
	localmake "triangleHeight :height / 4
	localmake "triangleWidth :size1 / 2
	setPenSize 0
	jumpForward :height
	right 180
	localmake "colorIndex 1
	for ["row 0 10] [
		localmake "maxCol :row * 2 + 1
		localmake "pos1 pos
		for ["col 1 :maxCol] [
			setFillColor item :colorIndex :colors
			triangle :triangleHeight odd? :col
			jumpLeft :triangleWidth / 2
			localmake "colorIndex :colorIndex + 1
			if :colorIndex > count :colors [
				setTurtleState :oldState
				stop
			]
		]
		jumpTo :pos1
		jumpRight :triangleWidth / 2
		jumpForward :triangleHeight
	]
end

; Inspired by logo at:
; https://everest-ts.com/
to everestTransportSystemsLogo :height
	localmake "oldState turtleState
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "penSize1 :height * 0.06
	localmake "radius :height / 2
	localmake "radius1 :radius - :penSize1 / 2
	localmake "radius2 :radius1 - :penSize1 / 2
	localmake "angle1 137
	setLineCap "butt
	setLineJoinStyle "miter
	setPenColor "#06a44e
	setPenSize :penSize1
	jumpLeft :height * 0.217
	jumpForward :height * 0.42
	left :angle1
	forward :height * 0.25
	left 77
	arcLeft 131 :radius1
	setPenSize 0
	jumpRight :penSize1 / 2
	setFillColor penColor
	polyStart
	jumpLeft :penSize1
	arcLeft 3.5 :radius2
	polyEnd

	setPenColor "#15286d
	setPenSize :penSize1
	jumpTo :oldPos
	setHeading :oldHeading
	jumpForward :height * 0.48
	jumpLeft :height * 0.168
	right 180 - :angle1
	forward :height * 0.122
	right 90
	forward :height * 0.17
	jumpRight :height * 0.23
	left 90
	forward :height * 0.56
	right 90
	forward :height * 0.25
	setPenSize 0
	setFillColor penColor
	jumpRight :penSize1 / 2
	polyStart
	arcLines [[0.095] [126 0] [211 0.5] [115 0]
	[:penSize1 * 1.2 / :height] [62 0] [-199 :radius2 / :height]
	] :height
	polyEnd

	setTurtleState :oldState
end

; Inspired by logo at:
; https://www.facebook.com/Everesttrasport/
to everestTransportSystemsLogo2 :height
	localmake "oldState turtleState
	localmake "oldPos pos
	localmake "oldHeading heading
	jumpLeft :height
	right 90
	setLineJoinStyle "round
	setFillColor "#fd0002
	setPenSize :height * 0.005
	setPenColor "#800
	polyStart
	jumpForward :height * 1.8
	arcsLeft [[20 0.5] [40 0.1] [60 0.05]
	[25 0.1] [20 0.5] [160 0] [-100 0.055] [-40 0.1]] :height
	polyEnd

	jumpTo :oldPos
	setHeading :oldHeading
	jumpLeft :height * 0.89
	jumpForward :height * 0.07
	right 44.5
	setFillColor "#0580ff
	setPenSize 0
	polyStart
	arcLines [[0.52] [-45 0] [0.12] [26 0]
	[0.33] [28 0] [0.22] [-35 0] [0.08]
	[33 0] [0.258] [-105 0] [0.29] [35 0] [0.05]
	[-60 0] [0.13] [27 0] [0.5] [-177 0] [0.35]
	[160 0] [0.15] [-174 0] [0.22] [10 0] [0.1]
	[-15 0] [0.12] [50 0] [0.04] [-40 0] [0.12]
	[135 0] [0.1] [-155 0] [0.2] [145 0] [0.11]
	[70 0] [0.02] [-50 0] [0.05] [-20 0] [0.1]
	[-28 0] [0.08] [50 0] [0.05] [-25 0] [0.08]
	[20 0] [0.1] [-112 0] [0.16] [30 0] [0.12]
	[95 0] [0.07] [-80 0] [0.22] [80 0] [0.05]
	[-44 0] [0.19] [-150 0] [0.11] [95 0] [0.05]
	] :height
	polyEnd

	jumpTo :oldPos
	setHeading :oldHeading
	jumpForward :height * 0.31
	jumpLeft :height * 0.242
	right 42
	polyStart
	arcLines [[0.13] [-60 0] [0.1] [-60 0] [0.08]
	[-135 0] [0.11]] :height
	polyEnd

	jumpTo :oldPos
	setHeading :oldHeading
	jumpForward :height * 0.22
	jumpRight :height * 0.08
	polyStart
	arcLines [[-10 0] [0.15] [-35 0] [0.06] [32 0]
	[0.08] [-60 0] [0.035] [-120 0] [0.14] [-35 0]
	[0.07]
	] :height
	polyEnd

	jumpTo :oldPos
	setHeading :oldHeading
	jumpForward :height * 0.23
	jumpRight :height * 0.65
	left 27
	polyStart
	arcLines [[0.1] [3 0] [0.15] [8 0] [0.25] [165 0] [0.2]] :height
	polyEnd

	setTurtleState :oldState
end

make "size 100
make "separation :size * 4
jumpLeft :separation
everestTransportLogo1 :size
jumpRight :separation
everestTransportSystemsLogo :size
jumpRight :separation
everestTransportSystemsLogo2 :size