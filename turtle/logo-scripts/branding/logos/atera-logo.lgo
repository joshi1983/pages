; Inspired by logo at
; https://www.atera.com
to ateraLogo :height
	localmake "oldState turtleState
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "angle1 40
	localmake "angle2 90 + :angle1
	localmake "angle3 30.2
	localmake "arcAngle1 59.7
	localmake "arcAngle2 180 - 2 * :angle3
	localmake "arcRadius1 :height * 0.055
	localmake "arcRadius2 :height * 0.0413
	localmake "triArcRadius :height * 0.21
	localmake "triWidth :height * 1.31
	localmake "triHeight :triWidth / 2 * tan 60
	localmake "size1 :height * 0.135
	localmake "size2 :height * 0.107 + :size1 * sin :angle1
	localmake "size3 :height * 0.2
	localmake "size4 :height * 0.2724965
	localmake "size5 :height * 0.18
	localmake "size6 :height * 0.288
	localmake "centerOffset :height * 0.4185
	jumpForward :centerOffset
	localmake "center pos
	setPenSize :height * 0.075
	setPenColor "white
	jumpTo :oldPos
	setFillColor transparent
	jumpForward penSize / 2
	roundIsoTriangle :triWidth :triHeight :triArcRadius
	jumpTo :center
	setPenSize 0
	setFillColor penColor
	repeat 3 [
		setHeading :oldHeading - :angle1 + (repcount - 1) * 360 / 3
		jumpForward :size1
		right :angle2
		polyStart
		jumpForward :size2
		arcLeft :arcAngle1 :arcRadius1
		jumpForward :size3
		right :arcAngle1 / 2		
		jumpRight :size4
		left 180 - :arcAngle1 / 2
		jumpForward :size3
		arcLeft :arcAngle1 :arcRadius1
		jumpForward :size2
		polyEnd
		jumpTo :center
	]
	repeat 3 [
		jumpTo :center
		setHeading :oldHeading + repcount * 360 / 3
		jumpForward :height * 0.49
		jumpLeft :size5
		right 180 - :angle3
		polyStart
		jumpForward :size6
		arcLeft :arcAngle2 :arcRadius2
		jumpForward :size6
		left 95
		jumpForward :height * 0.2
		polyEnd
	]
	setTurtleState :oldState
end

setScreenColor "black
ateraLogo 100