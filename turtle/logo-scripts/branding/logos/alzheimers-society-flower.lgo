make "darkBlueColor "#002075

to drawBackgroundGradient :radius
	localmake "colorStops createPList
	setProperty "colorStops 0 :darkBlueColor
	setProperty "colorStops 0.2 :darkBlueColor
	setProperty "colorStops 1 "white
	setPenSize 0
	setFillGradient createRadialGradient pos :radius * 0.8 :colorStops
	circle :radius
end

to drawLargeBluePedal :size
	localmake "oldState turtleState
	localmake "colorStops createPList
	setProperty "colorStops 0 :darkBlueColor
	setProperty "colorStops 0.2 :darkBlueColor
	setProperty "colorStops 1 "#0283fa
	localmake "fromPos pos
	jumpForward :size * 0.9

	setFillGradient createLinearGradient :fromPos pos :colorStops "pad
	jumpTo :fromPos
	localmake "arcsInfo [[0.12] [-30 0] [0.35] [109 0.387] [60 0.14]
	[-35.0375385788 0.03]]
	setPenSize 0
	polyStart
	arcLines :arcsInfo :size
	arcLines reverse :arcsInfo :size
	polyEnd
	setTurtleState :oldState
end

to drawSmallYellowPedal :size
	localmake "oldState turtleState
	localmake "arcsInfo [[-90 0] [0.06] [-80 0] [0.7] [90 0.33] [80 0.22]
	 [-41.2033422313732 0.1]]
	setPenSize :size * 0.01
	setPenColor "black
	polyStart
	arcLines :arcsInfo :size
	arcLines reverse :arcsInfo :size
	polyEnd
	setTurtleState :oldState
end

; Inspired by logo at:
; https://www.alzheimers.org.uk/
; This is also called the "forget me not flower".
to alzheimersOrganizationFlower :height
	localmake "oldState turtleState
	jumpForward :height * 0.494
	drawBackgroundGradient :height * 0.27
	setLineJoinStyle "round
	repeat 5 [
		drawLargeBluePedal :height * 0.5065
		right 360 / 5
	]
	localmake "colorStops createPList
	setProperty "colorStops 0 "#f3da66
	setProperty "colorStops 0.4 "#f3da66
	setProperty "colorStops 1 "#ff980c
	setFillGradient createRadialGradient pos :height * 0.12 :colorStops
	repeat 5 [
		drawSmallYellowPedal :height * 0.117
		right 360 / 5
	]
	setPenSize :height * 0.003
	setPenColor "black
	setFillColor :darkBlueColor
	right 5
	regularPolygon :height * 0.065 10
	setTurtleState :oldState
end

alzheimersOrganizationFlower 100