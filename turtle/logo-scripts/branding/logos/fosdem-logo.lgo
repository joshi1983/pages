; Inspired by logo at:
; https://fosdem.org
to fosdemLogo :height :color
	localmake "oldState turtleState
	localmake "oldHeading heading
	localmake "numTeeth 13
	localmake "lines1 [
		[90 0] [3 1] [65 0] [15 1]
		[97 0] [0.1177]
	]
	localmake "outerRadius :height * 0.5
	jumpForward :height * 0.4985
	localmake "center pos
	setPenSize 0
	setFillColor :color
	repeat 2 [
		jumpRight :height * 0.09
		circle :height * 0.074
		jumpTo :center
		right 180
	]
	setFillColor transparent
	setPenSize :height * 0.14
	setPenColor :color
	circle :height * 0.33
	setPenSize 0
	setFillColor :color
	repeat :numTeeth [
		setHeading :oldHeading + 21.5 + repcount * 360 / :numTeeth
		drawArcLineShape [0 1
			combine :lines1 reverse :lines1
		] :outerRadius
		jumpTo :center
	]
	setTurtleState :oldState
end

to fosdemWhiteOnBlackLogo :height
	localmake "oldState turtleState
	localmake "innerHeight :height * 0.8
	stripes :height :height ["black]
	jumpForward (:height - :innerHeight) / 2
	fosdemLogo :innerHeight "white
	setTurtleState :oldState
end

make "height 100
make "separation :height * 4
jumpLeft :separation
fosdemLogo :height "black
jumpRight :separation
fosdemLogo :height "#ad3092
jumpRight :separation
fosdemWhiteOnBlackLogo :height