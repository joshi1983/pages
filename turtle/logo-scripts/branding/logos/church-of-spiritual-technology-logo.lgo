; Inspired by
; https://commons.wikimedia.org/wiki/File:ChurchOfSpritualTechnologyLogo.svg
to churchOfSpiritualTechnologyLogo :height
	localmake "oldState turtleState
	localmake "oldHeading heading
	localmake "separation :height * 0.744
	localmake "diamondSize :height * 0.3
	localmake "angle1 33
	jumpLeft :separation / 2
	jumpForward :height / 2
	setPenSize :height * 0.035
	setPenColor "black
	setLineJoinStyle "miter
	setFillColor transparent
	repeat 2 [
		localmake "pos1 pos
		circle (:height - penSize) / 2

		; Offset because diamonds are not exactly centered in the circles.
		; The diamonds have a slightly larger separation than the circle centers.
		jumpLeft :height * 0.012 * sign (1.5 - repcount)

		jumpBackward :diamondSize * cos :angle1
		right 90 - :angle1
		jumpLeft :diamondSize / 2
		parallelogram :diamondSize :diamondSize :angle1 * 2 - 90
		setHeading :oldHeading
		jumpTo :pos1
		jumpRight :separation
	]
	setTurtleState :oldState
end

churchOfSpiritualTechnologyLogo 100