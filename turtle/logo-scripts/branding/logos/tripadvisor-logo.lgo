; Inspired by logo at
; https://www.tripadvisor.com
to tripAdvisorLogo :height
	localmake "oldState turtleState
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "thickness :height * 0.06
	localmake "eyeSeparation :height * 0.37
	localmake "tripadvisorGreen "#34E0A1
	localmake "radii [0.155 0]
	localmake "topArcLength :height * 0.52
	localmake "topArcAngle 90
	localmake "topArcRadius :topArcLength / 2 / sin :topArcAngle / 2
	jumpForward :height / 2
	setPenSize 0
	setFillColor :tripadvisorGreen
	circle :height / 2
	jumpForward :height * 0.133
	right 180
	setLineCap "butt
	setLineJoinStyle "miter
	setPenColor "black
	setPenSize :thickness
	isoTriangle :height * 0.6 :height * 0.327

	jumpTo :oldPos
	setHeading :oldHeading
	setFillColor :tripadvisorGreen
	jumpLeft :topArcLength / 2
	jumpForward :height * 0.6
	right 90 - :topArcAngle / 2
	polyStart
	arcRight :topArcAngle :topArcRadius
	polyEnd
	setHeading :oldHeading
	jumpLeft :topArcLength / 2
	jumpBackward penSize
	stripes :height * 0.27 :height * 0.1 [:tripadvisorGreen]

	jumpTo :oldPos
	setHeading :oldHeading
	jumpForward :height * 0.32
	stripes :height * 0.1 :height * 0.142 ["black]
	jumpForward :height * 0.142
	jumpLeft :eyeSeparation / 2
	repeat 2 [
		repeat count :radii [
			localmake "radius :height * item repcount :radii
			if :radius <= 0 [
				localmake "radius :height * 0.065
				setFillColor penColor
				setPenSize 0
			]
			circle :radius
		]
		setPenSize :thickness
		setFillColor :tripadvisorGreen
		jumpRight :eyeSeparation
	]
	setTurtleState :oldState
end

tripAdvisorLogo 100