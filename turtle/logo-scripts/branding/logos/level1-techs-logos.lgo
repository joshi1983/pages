make "l1DarkBlue "#19475e

to drawL1 :height
	localmake "oldState turtleState
	localmake "bottom 0.2
	localmake "fontHeight 0.6
	localmake "fontHeight1 0.2
	localmake "fontHeight2 0.422
	localmake "fontHeight3 :fontHeight - :fontHeight2
	localmake "angle 58
	localmake "slantedHeight :fontHeight3 / sin :angle
	localmake "shapes [
		[-0.341 :bottom [[:fontHeight] [-90 0] [:fontHeight1] [-90 0]
			[:fontHeight2] [90 0] [0.177] [-90 0]
			[:fontHeight3]]]
		[0.345 :bottom [[:fontHeight] [90 0] [0.265] [:angle 0]
			[:slantedHeight] [180 - :angle 0] [0.177] [-90 0]
			[:fontHeight2]]]
	]
	setPenSize 0
	setFillColor "#14bbc3
	drawArcLineShapes :shapes :height	
	setTurtleState :oldState
end

; Inspired by logo at:
; https://www.youtube.com/c/Level1Techs
to level1TechsLogo :height
	localmake "oldState turtleState
	stripes :height :height [:l1DarkBlue]
	drawL1 :height
	setTurtleState :oldState
end

to level1TechsRoundLogo :height
	localmake "oldState turtleState
	jumpForward :height / 2
	setFillColor :l1DarkBlue
	circle :height / 2
	jumpBackward :height / 2
	drawL1 :height
	setTurtleState :oldState
end

make "height 100
make "separation :height * 5

jumpLeft :separation / 2
level1TechsLogo :height
jumpRight :separation
level1TechsRoundLogo :height