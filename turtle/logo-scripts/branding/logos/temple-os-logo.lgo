to createWeightGradient :baseHeight
	localmake "oldState turtleState
	localmake "colorStops createPList2 [
		[0 mix "black fillColor 0.5]
		[1 fillColor]
	]
	right 70
	jumpBackward :baseHeight * 0.4
	localmake "fromPos pos
	jumpForward :baseHeight * 0.6
	localmake "result createLinearGradient :fromPos pos :colorStops "pad	
	setTurtleState :oldState
	output :result
end

to drawWeight :height
	localmake "oldState turtleState
	localmake "oldPos pos
	localmake "oldPenSize penSize
	localmake "oldHeading heading
	localmake "baseWidth :height * 0.12
	localmake "topWidth :height * 0.35
	localmake "baseHeight :height * 0.125
	localmake "triangleHeight :height - :baseHeight
	setFillGradient createWeightGradient :baseHeight
	isoTrapezoid :baseWidth :topWidth :baseHeight

	jumpTo :oldPos
	setHeading :oldHeading
	setPenSize :oldPenSize
	jumpForward :baseHeight
	setFillColor transparent
	setLineJoinStyle "round
	setPenSize penSize * 0.8
	isoTriangle :topWidth :triangleHeight
	setTurtleState :oldState
end

to drawJusticeBalance :height
	localmake "oldState turtleState
	localmake "oldPos pos
	localmake "separation :height * 1.4
	localmake "triangleHeight :height * 0.28
	localmake "weightHeight :height - :triangleHeight
	jumpForward :height - :triangleHeight
	isoTriangle :separation :triangleHeight
	jumpTo :oldPos
	jumpLeft :separation / 2
	repeat 2 [
		drawWeight :weightHeight
		jumpRight :separation
	]
	setTurtleState :oldState
end

to drawSword :height
	localmake "oldState turtleState
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "oldFillColor fillColor
	localmake "arcsInfo [
		[90 0] [0.014] [-85.5 0] [0.15] [-5.6739682 0] [0.817] [-20 0] [0.025]
	]
	localmake "yellowShapeArcs [
		[0.07] [-80 0] [0.037] [-120 0] [20 0.2234517]
	]
	localmake "gap :height * 0.19
	setPenSize :height * 0.007
	setFillColor "#eee
	polyStart
	arcLines :arcsInfo :height
	right 137.6520635
	arcLines reverse :arcsInfo :height
	polyEnd
	jumpTo :oldPos
	setHeading :oldHeading
	jumpForward :gap
	stripes penSize * 0.8 :height - :gap - penSize * 2 [penColor]

	jumpTo :oldPos
	jumpForward :height * 0.135
	left 90
	setFillColor :oldFillColor
	polyStart
	arcLines :yellowShapeArcs :height
	arcLines reverse :yellowShapeArcs :height
	polyEnd
	setTurtleState :oldState
end

; Inspired by
; https://en.wikipedia.org/wiki/TempleOS#/media/File:TempleOS.jpg
to templeOSLogo :height
	localmake "oldState turtleState
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "swordHeight :height * 0.89
	setLineJoinStyle "miter
	setPenSize :height * 0.0627
	localmake "squareSize :height - penSize
	setPenColor "#010080
	setFillColor "#01ffff
	jumpForward :height / 2
	rect :squareSize :squareSize
	setFillColor "#ffff01

	setPenColor "black
	jumpTo :oldPos
	jumpForward :height * 0.127
	jumpLeft :height * 0.225
	right 31.2
	drawSword :swordHeight

	setPenSize :height * 0.005
	jumpTo :oldPos
	setHeading :oldHeading
	jumpForward :height * 0.455
	drawJusticeBalance :height * 0.262
	setTurtleState :oldState
end

templeOSLogo 100