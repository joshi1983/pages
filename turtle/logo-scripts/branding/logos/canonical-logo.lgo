make "canonicalRed "#e95420

; Inspired by
; https://en.wikipedia.org/wiki/Canonical_%28company%29#/media/File:Canonical_logo_2023.svg
to canonicalLogo :height
	localmake "oldState turtleState
	localmake "oldHeading heading
	localmake "largeRadius :height * 0.427
	localmake "largeRadius2 :height * 0.407
	localmake "smallRadius :height * 0.148
	localmake "thickness :height * 0.125
	localmake "midRadius :height * 0.2368415
	jumpForward :height / 2
	setPenColor "white
	setPenSize :thickness
	circle :largeRadius

	localmake "center pos
	setFillColor :canonicalRed
	setPenSize 0
	repeat 3 [
		setHeading :oldHeading + 30 + repcount * 360 / 3
		jumpForward :largeRadius
		circle :midRadius
		jumpTo :center
	]
	setFillColor "white
	repeat 3 [
		setHeading :oldHeading + 30 + repcount * 360 / 3
		jumpForward :largeRadius2
		circle :smallRadius
		jumpTo :center
	]
	setTurtleState :oldState
end

setScreenColor :canonicalRed
canonicalLogo 100