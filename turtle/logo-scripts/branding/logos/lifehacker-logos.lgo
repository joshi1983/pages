to createBackgroundGradient :width
	localmake "oldPos pos
	localmake "colorStops createPList2 [
		[0 "#6cb63d] [1 "#38b588]
	]
	jumpLeft :width / 2
	localmake "fromPos pos
	jumpRight :width
	localmake "result createLinearGradient :fromPos pos :colorStops "pad
	jumpTo :oldPos
	output :result
end

to drawLifeHackerLetters :height
	localmake "oldState turtleState
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "hSeparation :height * 0.136
	jumpTo :oldPos
	jumpForward :height * 0.701
	jumpLeft :height * 0.128
	setLineCap "butt
	setLineJoinStyle "miter
	setPenSize :height * 0.076
	setPenColor "white
	backward :height * 0.522
	setPenSize :height * 0.07
	jumpForward penSize / 2
	right 90
	forward :height * 0.14

	jumpTo :oldPos
	setHeading :oldHeading
	jumpForward :height * 0.309
	jumpRight :height * 0.01
	repeat 2 [
		stripes :height * 0.076 :height * 0.524 [penColor]
		jumpRight :hSeparation
	]
	jumpLeft :hSeparation * 1.5
	jumpForward :height * 0.229
	stripes :hSeparation :height * 0.078 [penColor]
	setTurtleState :oldState
end

; Inspired by favicon at
; https://lifehacker.com/
to roundedLifeHackerLogo :height
	localmake "oldState turtleState
	setFillGradient createBackgroundGradient :height
	setPenSize 0
	jumpForward :height / 2
	roundRect :height :height :height * 0.168
	jumpBackward :height / 2
	drawLifeHackerLetters :height
	setTurtleState :oldState
end

to squareLifeHackerLogo :height
	localmake "oldState turtleState
	setPenGradient createBackgroundGradient :height
	setPenSize :height
	setLineCap "butt
	forward :height
	jumpBackward :height
	drawLifeHackerLetters :height
	setTurtleState :oldState
end

make "height 100
make "separation :height * 4
jumpLeft :separation / 2
roundedLifeHackerLogo :height
jumpRight :separation
squareLifeHackerLogo :height