; Inspired by logo for:
; https://www.youtube.com/@TheLinuxEXP
; The channel name is The Linux Experiment.
to theLinuxExperimentLogo :height
	localmake "oldState turtleState
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "angle 57.4
	localmake "penguinColor "#ffd9b8
	localmake "penguinShape [0.2 0
		[[0.33] [30 0.5] [0.02] [-15 0.4] [-70 0.1] [0.02] [-18 0.2]
		[-20 0.1] [0.005] [150 0] [30 0.1] [4 0.7] [-25 0.1] [40 0.11]
		[34 0.05] [30 1]
		]
	]
	localmake "colorStops createPList2 [
		[0.2 mix :penguinColor transparent 0]
		[1 :penguinColor]
	]
	stripes :height :height ["#ed1852]

	setPenSize 0
	jumpForward :height * 0.87
	setFillGradient createLinearGradient :oldPos pos :colorStops "pad
	jumpTo :oldPos
	drawArcLineShape :penguinShape :height

	jumpTo :oldPos
	jumpRight :height / 2
	localmake "bottomRightPos pos
	setFillColor "#630460
	polyStart
	jumpForward :height * 0.05
	left :angle
	jumpForward :height / sin :angle
	setHeading :oldHeading + 180
	jumpForward distanceToLine :oldPos :bottomRightPos
	polyEnd
	setTurtleState :oldState
end

setScreenColor "black
theLinuxExperimentLogo 100