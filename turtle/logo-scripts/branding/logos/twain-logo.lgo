to setTwainGradient :height
	localmake "oldPos pos
	localmake "colorStops createPList2 [
		[0 "#5f84bd] [1 "#3f025c]
	]
	jumpLeft :height * 0.27
	localmake "fromPos pos
	jumpRight :height * 0.77
	setFillGradient createLinearGradient :fromPos pos :colorStops "pad
	jumpTo :oldPos
end

; Inspired by logo at:
; https://www.twain.org
to twainLogo :height
	localmake "oldState turtleState
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "arrowWidth :height * 0.32
	localmake "arrowHeight :arrowWidth * 0.65
	localmake "arrowLine [[140 0] [40 0.33] [50 0.45] [30 0.3] [15 0.5]
			[90 0] [0.23] [89 0] [-50 0.38] [-71 0.238]]
	setPenSize 0
	setFillColor "#ddd
	jumpForward :height * 0.47
	left 90
	jumpForward :height * 0.165
	isoTriangle :arrowWidth :arrowHeight
	right 180
	jumpForward :height * 0.33
	jumpRight :height * 0.01
	isoTriangle :arrowWidth :arrowHeight
	
	jumpTo :oldPos
	setHeading :oldHeading
	right 180
	drawArcLineShape [-0.15 0
		:arrowLine
	] :height * 0.9

	jumpTo :oldPos
	setHeading :oldHeading
	jumpForward :height / 2
	localmake "center pos
	repeat 2 [
		setHeading :oldHeading + repcount * 180
		setTwainGradient :height
		drawArcLineShape [-0.165 0.5
			:arrowLine
		] :height
	]
	repeat 2 [
		setHeading :oldHeading + repcount * 180
		setTwainGradient :height
		jumpBackward :height * 0.01
		right 90
		jumpForward :height * 0.165
		isoTriangle :arrowWidth :arrowHeight
		jumpTo :center
	]
	setTurtleState :oldState
end

twainLogo 100