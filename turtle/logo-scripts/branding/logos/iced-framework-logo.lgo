to createBackgroundGradient :height
	localmake "oldState turtleState
	localmake "oldPos pos
	localmake "colorStops createPList2 [
		[0 "#3203ff] [1 "#0693ff]
	]
	right 45
	jumpForward :height
	localmake "result createLinearGradient :oldPos pos :colorStops "pad	
	setTurtleState :oldState
	output :result
end

to drawShadow :shadowSize
	localmake "oldState turtleState
	localmake "fadeStepCount 10
	localmake "step :shadowSize * 0.15 / :fadeStepCount
	localmake "cornerStep :step * 0.7
	jumpForward :shadowSize / 2
	localmake "shadowSize1 :shadowSize
	localmake "cornerRadius :shadowSize * 0.2
	setFillColor mix "black transparent 0.1 / :fadeStepCount
	repeat :fadeStepCount [
		roundRect :shadowSize1 :shadowSize1 :cornerRadius
		localmake "cornerRadius :cornerRadius - :cornerStep
		localmake "shadowSize1 :shadowSize1 - :step
	]	
	setTurtleState :oldState
end

to drawWhiteShapes :height
	localmake "oldState turtleState
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "lines [
		[-0.29 0.53 0.3515] [-0.088 0.5895 0.33]
		[0.134 0.669 0.22] [0.0575 0.446 0.291]
		[-0.067 0.465 0.2387] [0 0.24 0.384] 
	]
	localmake "pSize :height * 0.23
	localmake "angle 44.6
	localmake "angle2 57.2033
	localmake "len :height * 0.26
	localmake "len2 :len + :height * 0.000657
	localmake "thickness :height * 0.047
	setPenColor "white
	setLineCap "butt
	setPenSize :thickness
	repeat count :lines [
		localmake "lineInfo item repcount :lines
		setHeading :oldHeading
		jumpRight :height * first :lineInfo
		jumpForward :height * item 2 :lineInfo
		right :angle
		forward :height * last :lineInfo
		jumpTo :oldPos
	]
	setHeading :oldHeading
	jumpForward :height * 0.287
	jumpLeft :height * 0.105
	setFillColor transparent
	right 15
	setLineJoinStyle "miter
	setMiterLimit 1.4
	parallelogram :pSize :pSize -30
	setMiterLimit 10
	localmake "point1 vectorScale butLast (first :lines) :height
	jumpTo :oldPos
	setHeading :oldHeading
	jumpRight first :point1
	jumpForward item 2 :point1
	right :angle
	jumpLeft :thickness / 2
	setHeading :oldHeading + 163.6
	setPenSize 0
	setFillColor "white
	polyStart
	jumpForward :len
	left :angle2
	jumpForward :len2
	jumpLeft :thickness
	jumpBackward :len2 - :thickness * cos :angle2
	left 180 - :angle2
	jumpForward :len - :thickness
	polyEnd
	setTurtleState :oldState
end

; Inspired by
; https://zh.wikipedia.org/wiki/OK%E8%B6%85%E5%95%86#/media/File:OK_LOGO-%E6%96%B9%E5%BD%A2-01.png
to icedFrameworkLogo :height
	localmake "oldState turtleState
	localmake "oldPos pos
	localmake "squareSize :height * 0.932
	setPenSize 0
	drawShadow :height * 0.8
	setFillGradient createBackgroundGradient :height
	jumpForward :height - :squareSize / 2
	roundRect :squareSize :squareSize :height * 0.27
	jumpTo :oldPos
	drawWhiteShapes :height
	setTurtleState :oldState
end

icedFrameworkLogo 100