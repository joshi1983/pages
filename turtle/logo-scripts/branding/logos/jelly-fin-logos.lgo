; Inspired by logo at:
; https://jellyfin.org/
to jellyFinLogo :height
	localmake "oldState turtleState
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "innerArcsSide [
		[-30 0.016] [-10 0.08] [-12 0.13] [-18 0.78]
		[-12 0.17] [-15 0.06] [-53 0.02]
	]
	localmake "innerBottomArcsInfo [
		[-10 0.1] [-11 0.15] [-9 0.7150702]
	]
	localmake "outerScaleFactor :height * 3.01
	localmake "outerScaleFactor2 :height * 1.98
	localmake "innerArcs combine :innerArcsSide :innerBottomArcsInfo
	localmake "colorStops createPList2 [
		[0 "#00A4DC] [1 "#AA5CC3]
	]
	setPenSize 0
	jumpRight :height * 0.5
	jumpForward :height * 0.1
	localmake "fromPos pos
	left 60
	jumpForward :height * 0.96
	setFillGradient createLinearGradient :fromPos pos :colorStops "pad
	jumpTo :oldPos
	setHeading :oldHeading
	jumpForward :height * 0.6168
	right 90
	polyStart
	arcLines :innerArcs :height
	arcLines reverse :innerArcs :height
	polyEnd

	jumpTo :oldPos
	setHeading :oldHeading + 90
	polyStart
	arcLines reverse :innerArcs -:outerScaleFactor
	arcLines :innerArcs -:outerScaleFactor
	arcLeft 180 :height * 0.073
	arcLines reverse :innerArcs :outerScaleFactor2
	arcLines :innerArcs :outerScaleFactor2
	polyEnd
	setTurtleState :oldState
end

to jullyFinBlackBackgroundLogo :height
	stripes :height * 3 :height ["black]
	jellyFinLogo :height
end

make "height 100
make "separation :height * 5
jumpLeft :separation / 2
jellyFinLogo :height
jumpRight :separation
jullyFinBlackBackgroundLogo :height