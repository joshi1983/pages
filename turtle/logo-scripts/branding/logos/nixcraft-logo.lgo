; Inspired by logo at
; https://www.facebook.com/nixcraft
to nixCraftLogo :height
	localmake "oldState turtleState
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "largeAngle 295
	localmake "largeRadius :height * 0.44
	localmake "nixCraftBlue "#3b5bdb
	setLineCap "round
	setPenSize :height * 0.021
	setPenColor :nixCraftBlue
	jumpForward :height - penSize / 2
	right 90
	penUp
	arcRight :largeAngle / 2 :largeRadius
	right 180
	penDown
	arcLeft :largeAngle :largeRadius
	jumpTo :oldPos
	setHeading :oldHeading
	jumpForward :height * 0.512
	jumpRight :height * 0.187
	right 155
	arcsRight [[40 0.55] [15 0.3] [24.155 0.175]] :height

	; draw eye
	jumpTo :oldPos
	setHeading :oldHeading
	jumpForward :height * 0.7134
	jumpRight :height * 0.117
	setPenSize 0
	setFillColor :nixCraftBlue
	circle :height * 0.025

	; draw dark part of body
	jumpTo :oldPos
	setHeading :oldHeading
	jumpRight :height * 0.140786
	right 25
	polyStart
	arcsLeft [[30 0.33] [20 0.7] [6.6 2] [-60 0.18]
	[-78 0.095] [-53 0.178] [-22 0.03] [2 2]
	[108 0.008] [15 0.31] [110 0.008] [9 0.4]
	[-50 0.02] [30 0.28] [38 0.17] [35 0.3] [50 0.18]
	[15 0.4] [-15 0.9] [10 0.8] [34 0.22] [75 0]
	[30 0.011] [-30 0.24] [-56 0.1] [-20.95 0.7161]] :height
	polyEnd
	setTurtleState :oldState
end

nixCraftLogo 100