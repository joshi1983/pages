; Inspired by
; https://commons.wikimedia.org/wiki/File:WordPress_logo.svg
to wordPressLogo :height :color
	localmake "oldState turtleState
	localmake "oldHeading heading
	localmake "innerRadius :height * 0.4288
	localmake "shapes [
		[42.4 99.04 [[123 0] [0.1] [4 1.3] [0.14]
			[-176.5 0.0414] [-5.1 1.3] [106.5 0] [1.143] [142.98 0]
			[0.692] [38.55 0] [0.4547] [64.7 0] [0.105]
			[-180 0.0415] [10.435 2.9] [-179.4 0.0415]
			[-2.28 2.9] [105.07 0] [1.134] [144.6 0]
			[0.5] [10 0.8] [20 0.42] [18 0.55] [0.1] [-15 0.42]
			[-40 0.208] [-70 0.162]]]
		[-66 88.1 []]
		[-163.6 35.78 [[104 0] [-13.3 0.08] [0.893]]]
		[149.7 88.3 [[160 0] [-20 0.7] [-10 1.3]]]
	]
	jumpForward :height / 2
	localmake "center pos
	setPenSize :height * 0.0229
	setFillColor transparent
	setPenColor :color
	circle (:height - penSize) / 2
	setFillColor penColor
	setPenSize 0
	repeat count :shapes [
		localmake "shapeInfo item repcount :shapes
		localmake "arcsInfo last :shapeInfo
		right first :shapeInfo
		jumpForward :innerRadius
		setHeading (towards :center) + 90
		polyStart
		arcLeft item 2 :shapeInfo :innerRadius
		arcLines :arcsInfo :innerRadius
		polyEnd
		jumpTo :center
		setHeading :oldHeading
	]
	setTurtleState :oldState
end

to wordPressBlackBackgroundLogo :height
	localmake "oldState turtleState
	localmake "innerHeight :height * 0.9
	stripes :height * 3 :height ["black]
	jumpForward (:height - :innerHeight) / 2
	wordPressLogo :innerHeight "white
	setTurtleState :oldState
end

make "height 100
make "separation :height * 4
jumpLeft :separation
wordPressLogo :height "#464342
jumpRight :separation
wordPressLogo :height "#0f8cc5
jumpRight :separation
wordPressBlackBackgroundLogo :height