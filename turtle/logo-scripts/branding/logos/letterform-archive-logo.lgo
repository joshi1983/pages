to drawA :fontHeight :weight
	localmake "oldState turtleState
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "innerThicknessRatio mix 0.19 0.078 :weight
	localmake "innerThickness :fontHeight * :innerThicknessRatio
	localmake "innerOffsetRatio 0.36
	localmake "innerOffset :fontHeight * :innerOffsetRatio
	localmake "innerTopRatio mix 0.79 0.92 :weight
	localmake "topInnerWidthRatio mix 0.14 0.18 :weight
	localmake "baseWidthRatio1 mix 0.3 0.68 :weight
	localmake "pointsInfo [
		[0 :innerTopRatio]
		[:topInnerWidthRatio / 2 :innerTopRatio]
		[:baseWidthRatio1 / 2 mix 0 0.015 :weight]
		[mix 0.515 0.425 :weight 0.03]
		[mix 0.325 0.15 :weight 1]
		[0 1]
	]
	localmake "innerWidth :fontHeight * 1.1 * mix :topInnerWidthRatio :baseWidthRatio1 :innerOffsetRatio
	jumpForward :innerOffset - :innerThickness / 2
	stripes :innerWidth :innerThickness [fillColor]
	jumpTo :oldPos
	repeat count :pointsInfo [
		localmake "point item repcount :pointsInfo
		jumpRight :fontHeight * first :point
		jumpForward :fontHeight * last :point
		setItem repcount "pointsInfo  pos 
		jumpTo :oldPos
	]
	localmake "arcLinesInfo []
	jumpTo first :pointsInfo
	for ["i 2 count :pointsInfo] [
		localmake "next item :i :pointsInfo
		localmake "len distance :next
		localmake "turnAngle heading - towards :next
		if :i <> 2 [
			queue2 "arcLinesInfo [:turnAngle 0]
		]
		queue2 "arcLinesInfo [:len / :fontHeight]
		left :turnAngle
		jumpForward :len
	]
	jumpTo first :pointsInfo
	setHeading :oldHeading + 90
	polyStart
	arcLines :arcLinesInfo :fontHeight
	arcLines reverse :arcLinesInfo :fontHeight
	polyEnd
	setTurtleState :oldState
end

; Inspired by logo at:
; https://letterformarchive.org/
to letterformArchiveLogo :height
	localmake "oldState turtleState
	jumpForward :height * 0.496
	localmake "center pos
	setPenSize 0
	setFillColor "#FF1919
	repeat 9 [
		jumpBackward :height * 0.5
		drawA :height * 0.2457 repRatio
		jumpTo :center
		left 360 / 9
	]	
	setTurtleState :oldState
end

letterformArchiveLogo 100