; Inspired by logo at
; https://en.wikipedia.org/wiki/Fidelity_Investments#/media/File:Fidelity_Investments_Logo.svg
to fidelityInvestmentsLogoCustomized :height :color
	localmake "oldState turtleState
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "radius1 :height * 0.519365
	localmake "numSunRays 16
	localmake "pyramidAngle1 162.33

	; move to center
	jumpForward :height - :radius1
	localmake "center pos
	right 141
	setFillColor :color
	setPenSize 0

	; Draw almost complete circle.
	polyStart
	jumpForward :radius1
	left 90
	arcLeft 295 :radius1
	polyEnd

	; Draw starburst.
	setHeading :oldHeading
	jumpTo :center
	setFillColor "white
	isotoxalStar :height * 0.155 (distance :oldPos) :numSunRays
	
	; Draw pyramid shadows.
	jumpTo :oldPos
	setHeading :oldHeading
	jumpForward :height * 0.61776
	localmake "pyramidTopPos pos
	localmake "distance1 distance :center
	localmake "size1 :distance1 * sin 180 - :pyramidAngle1
	localmake "size2 sqrt :radius1 * :radius1 - :size1 * :size1
	localmake "size3 :size2 + :distance1 * cos 180 - :pyramidAngle1
	setFillColor :color
	left :pyramidAngle1
	polyStart
	jumpForward :size3
	setHeading (towards :center) - 90
	arcRight 18.5 :radius1
	setHeading towards :pyramidTopPos
	jumpForward :height * 0.362

	; calculate a point that is between current position 
	;and the top of the pyramid.
	localmake "pos1 mix pos :pyramidTopPos 0.88313
	setHeading :oldHeading
	jumpRight :height * 0.054
	setHeading towards :pos1
	jumpForward distance :pos1
	polyEnd

	jumpTo :oldPos
	setHeading :oldHeading
	jumpForward :height * 0.3469
	jumpRight :height * 0.04
	localmake "width1 :height * 0.254
	parallelogram :width1 :height * 0.065 -57

	setFillColor "white
	jumpRight :width1 / 2
	polyStart
	setHeading (towards :pyramidTopPos) + 180
	jumpForward distanceToCircle :center :radius1
	setHeading (towards :center) - 90

	; This angle could be calculated but just approximating 
	; it to save a little time.
	localmake "angle2 17.353945
	arcRight :angle2 :radius1
	setHeading :oldHeading
	jumpLeft :height * 0.3935955
	setHeading towards :pyramidTopPos
	jumpForward :height * 0.3641
	polyEnd
	setTurtleState :oldState
end

; make background a little darker than white
; to make the white in the logo look brighter
setScreenColor "#ddd
make "height 100
make "separation :height * 3
make "colors ["black "#b38d44 "green]
jumpLeft :separation * ((count :colors) - 1) / 2
repeat count :colors [
	make "color item repcount :colors
	fidelityInvestmentsLogoCustomized :height :color
	jumpRight :separation
]