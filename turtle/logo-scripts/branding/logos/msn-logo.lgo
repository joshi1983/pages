; Inspired by logo at:
; https://en.wikipedia.org/wiki/MSN#/media/File:2024_new_msn_logo.svg
to msnLogo :height
	localmake "oldState turtleState
	localmake "oldPos pos
	localmake "blueStops createPList2 [
		[0 "#2bc4ea] [1 "#2052cb]
	]
	localmake "greenStops createPList2 [
		[0 "#b1ba23] [0.4 "#50b370] [1 "#006523]
	]
	localmake "orangeStops createPList2 [
		[0 "#ffc12e] [0.7 "#ff865c] [1 "#d4343c]
	]
	localmake "pinkStops createPList2 [
		[0 "#c106f2] [1 "#ff5f3d]
	]
	localmake "blueHeight :height * 0.7962
	setPenSize 0
	jumpLeft :height * 0.253
	setFillGradient createRadialGradient pos :height * 0.39 :greenStops
	drawArcLineShape [0 0
		[[90 0] [-80 0.121] [-40 0.18] [-28 0.35] [-25.7 0.7]
		[-105.2 0] [-16 0.95] [-9 0.27] [-15 0.56] [-20 0.17]
		[-15 0.24]]
	] :height
	jumpTo :oldPos
	jumpForward :height * 0.31
	setFillGradient createLinearGradient :oldPos pos :orangeStops "pad	

	jumpTo :oldPos
	drawArcLineShape [0.147 0
		[[90 0] [-30 0.082] [-35 0.065] [-25 0.18] [-39.7 0.48]
		[-109.5 0] [-20 0.64] [-25 0.29] [-30 0.14] [-28 0.08]
		[-10 0.13]]
	] :height
	
	jumpForward :height * 0.204
	localmake "pointPos pos
	jumpForward :height * 0.55
	setFillGradient createLinearGradient :pointPos pos :pinkStops "pad
	jumpTo :oldPos
	drawArcLineShape [0.0703 0.2012
		[[-5 0] [-17 1.26] [-21 0.43] [-35 0.165] [-20 0.24]
		[-50 0.125] [-35 0.18] [-40 0.225] [-24 0.36] [-6 1]
		[35 0.353]]
	] :height

	jumpTo :oldPos
	jumpForward :height
	localmake "top pos
	jumpBackward :blueHeight
	setFillGradient createLinearGradient :top pos :blueStops "pad
	jumpForward :blueHeight
	drawArcLineShape [-0.26 0
		[[90 0] [75 0.21] [50 0.33] [20 0.45] [9 0.9]
		[-24 0.45] [-26 0.115] [-15 0.06] [-7 0.05]
		[164 0] [20 0.95] [50 0.52] [20 0.235] 
		[20 0.21]
		]
	] :height
	
	setTurtleState :oldState
end

msnLogo 100