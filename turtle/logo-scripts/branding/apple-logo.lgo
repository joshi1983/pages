; Inspired by
; https://upload.wikimedia.org/wikipedia/commons/8/84/Apple_Computer_Logo_rainbow.svg
; Referenced from: https://en.wikipedia.org/wiki/Apple_Inc.

; This drawing is of Apple's corporate logo but 
; the company also had an associated 
; Logo programming language interpreter.
; Apple Logo(the same type of language used by WebLogo) ran on Apple II.
; Apple Logo is simulated at:
; https://archive.org/details/Apple_Logo_II

to applePoly :height
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "arcAngle1 26
	localmake "arcAngle2 20
	localmake "arcAngle3 10
	localmake "arcAngle4 15
	localmake "arcAngle5 15
	localmake "arcAngle6 14
	localmake "arcAngle7 18
	localmake "arcAngle8 28
	localmake "arcAngle9 10
	localmake "arcAngle10 40
	localmake "arcAngle11 4.5
	localmake "arcAngle12 10
	localmake "arcAngle13 21
	localmake "arcAngle14 25
	localmake "arcAngle15 7
	localmake "arcAngle16 13
	localmake "arcAngle17 20
	localmake "arcAngle18 13
	localmake "arcAngle19 12
	localmake "arcAngle20 14.5348
	localmake "arcRadius1 :height * 0.12
	localmake "arcRadius2 :height * 0.195
	localmake "arcRadius3 :height * 0.53
	localmake "arcRadius4 :height * 0.7
	localmake "arcRadius5 :height * 0.54
	localmake "arcRadius6 :height * 0.48
	localmake "arcRadius7 :height * 0.28
	localmake "arcRadius8 :height * 0.27
	localmake "arcRadius9 :height * 0.32
	localmake "arcRadius10 :height * 0.207
	localmake "arcRadius11 :height * 0.8
	localmake "arcRadius12 :height * 0.28
	localmake "arcRadius13 :height * 0.13
	localmake "arcRadius14 :height * 0.12
	localmake "arcRadius15 :height * 0.4
	localmake "arcRadius16 :height * 0.18
	localmake "arcRadius17 :height * 0.225
	localmake "arcRadius18 :height * 0.2
	localmake "arcRadius19 :height * 0.22
	localmake "arcRadius20 :height * 0.13345895
	jumpLeft :height * 0.152
	setPenSize 0
	left 90
	polyStart
	arcRight :arcAngle1 :arcRadius1
	arcRight :arcAngle2 :arcRadius2
	arcRight :arcAngle3 :arcRadius3
	arcRight :arcAngle4 :arcRadius4
	arcRight :arcAngle5 :arcRadius5
	arcRight :arcAngle6 :arcRadius6
	arcRight :arcAngle7 :arcRadius7
	arcRight :arcAngle8 :arcRadius8
	arcRight :arcAngle9 :arcRadius9
	arcRight :arcAngle10 :arcRadius10
	arcRight :arcAngle11 :arcRadius11
	arcLeft :arcAngle12 :arcRadius12
	arcLeft :arcAngle13 :arcRadius13 ; point of symmetry
	arcLeft :arcAngle12 :arcRadius12
	arcRight :arcAngle11 :arcRadius11
	arcRight :arcAngle10 :arcRadius10
	arcRight :arcAngle9 :arcRadius9
	arcRight 18.65 :arcRadius8
	right 100
	arcLeft 134.3 :height * 0.213
	right 93
	arcRight 14.1152202 :arcRadius4
	arcRight :arcAngle3 :arcRadius3
	arcRight :arcAngle2 :arcRadius2
	arcRight :arcAngle1 :arcRadius1
	arcRight :arcAngle14 :arcRadius14
	arcLeft :arcAngle15 :arcRadius15
	arcLeft :arcAngle16 :arcRadius16
	arcLeft :arcAngle17 :arcRadius17
	arcLeft :arcAngle18 :arcRadius18
	arcRight :arcAngle19 :arcRadius19
	arcRight :arcAngle20 :arcRadius20
	polyEnd

	; Top leaf
	jumpTo :oldPos
	setHeading :oldHeading
	jumpForward :height * 0.7552
	jumpLeft :height * 0.0169
	localmake "leafArcAngle 94
	localmake "leafArcRadius :height * 0.2185
	right 40 + :leafArcAngle / 2
	polyStart
	arcLeft :leafArcAngle :leafArcRadius
	left 180 - :leafArcAngle
	arcLeft :leafArcAngle :leafArcRadius
	polyEnd

end

to appleRainbowLogo :height
	localmake "oldState turtleState
	localmake "oldPos pos
	localmake "colorIntervals [
		["#009ddc 0.1738]
		["#963d97 0.3379]
		["#e03a3e 0.50338]
		["#f5821f 0.6689]
		["#fdb827 0.83235]
		["#61bb46 1]
	]
	localmake "colorStops createPList
	localmake "prevRatio 0
	repeat count :colorIntervals [
		localmake "intervalInfo item repcount :colorIntervals
		localmake "color item 1 :intervalInfo
		localmake "ratio item 2 :intervalInfo
		setProperty "colorStops :prevRatio + 0.001 :color
		setProperty "colorStops :ratio :color
		localmake "prevRatio :ratio
	]
	jumpForward :height * 0.767
	setFillGradient createLinearGradient :oldPos pos :colorStops "pad
	jumpTo :oldPos
	applePoly :height
	setTurtleState :oldState
end

to appleBlackLogo :height
	localmake "oldState turtleState
	setFillColor "black
	setPenSize 0
	applePoly :height
	setTurtleState :oldState
end

to appleWhiteLogo :height
	localmake "oldState turtleState
	localmake "oldPos pos
	localmake "backgroundHeight :height * 1.2
	setPenColor "black
	setPenSize :height * 3
	jumpBackward (:backgroundHeight - :height) / 2
	forward :backgroundHeight
	jumpTo :oldPos
	setFillColor "white
	setPenSize 0
	applePoly :height
	setTurtleState :oldState
end

make "size 100
make "separation :size * 4
jumpLeft :separation
appleWhiteLogo :size
jumpRight :separation
appleRainbowLogo :size
jumpRight :separation
appleBlackLogo :size