; Inspired by logo and favicon at:
; https://www.reddit.com/

make "redditRed "#FF4500

to redditAntenna :size
	localmake "oldState turtleState
	jumpForward :size * 0.61
	setLineJoinStyle "round
	setLineCap "round
	right 12
	forward :size * 0.1794
	right 90
	forward :size * 0.123
	left 90
	setPenSize 0
	setFillColor penColor
	circleRight :size * 0.05
	setTurtleState :oldState
end

to redditEars :size
	localmake "oldState turtleState
	localmake "earSeparation1 :size * 0.27
	localmake "earRadius :size * 0.067
	jumpForward :size * 0.5035
	jumpRight :earSeparation1
	setFillColor penColor
	setPenSize 0
	circle :earRadius
	jumpLeft :earSeparation1 * 2
	circle :earRadius
	setTurtleState :oldState
end

to redditFace :size
	localmake "oldState turtleState
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "width :size * 0.29
	localmake "height :size * 0.2
	localmake "arcAngle1 20
	localmake "arcRadius1 :size * 0.19
	localmake "eyeSeparation1 :size * 0.116
	localmake "eyeRadius :size * 0.0495
	jumpForward :size * 0.412
	setPenSize 0
	ellipse :width :height
	jumpBackward :size * 0.091
	jumpLeft :size * 0.113
	right 125
	setLineCap "round

	; lips
	setPenSize :size * 0.027
	setPenColor :redditRed
	arcLeft :arcAngle1 :arcRadius1
	arcLeft 30 :size * 0.21
	arcLeft :arcAngle1 :arcRadius1

	setFillColor "#000
	jumpTo :oldPos
	setHeading :oldHeading
	jumpForward :size * 0.4505
	jumpRight :eyeSeparation1
	setPenSize 0
	setFillColor :redditRed
	; eyes
	circle :eyeRadius
	jumpLeft :eyeSeparation1 * 2
	circle :eyeRadius
	setTurtleState :oldState
end

to redditAlienHead :size
	localmake "oldPos pos
	setPenSize 0
	setFillColor :redditRed
	jumpForward :size / 2
	circle :size / 2
	jumpTo :oldPos
	setPenSize :size * 0.028
	setPenColor "white
	setFillColor "white
	redditAntenna :size
	redditEars :size
	redditFace :size
end

setScreenColor transparent
redditAlienHead 100