to ironManForehead :size
	localmake "oldState turtleState
	localmake "size1 :size * 0.07
	localmake "size2 :size * 0.03
	localmake "size3 :size * 0.04
	localmake "size4 :size * 0.05
	localmake "size5 :size * 0.07
	localmake "size6 :size * 0.22
	localmake "size7 :size * 0.12652744
	
	localmake "arcRadius1 :size * 0.1
	localmake "arcRadius2 :size * 3
	localmake "arcRadius3 :size * 0.13
	localmake "arcRadius4 :size * 1.1
	localmake "arcRadius5 :size * 0.03
	localmake "arcRadius6 :size * 0.333
	localmake "arcRadius7 :size * 0.05
	localmake "arcRadius8 :size * 0.02
	localmake "angle1 14
	localmake "angle2 3.45
	localmake "angle3 77
	localmake "angle4 110
	localmake "angle5 60
	localmake "angle6 22
	localmake "angle7 9
	localmake "angle8 42
	localmake "angle9 35
	localmake "angle10 92.5
	localmake "angle11 30
	localmake "angle12 50.05
	setPenSize 0
	jumpBackward :size * 0.008
	right 90
	polyStart
	jumpForward :size1
	arcLeft :angle1 :arcRadius1
	arcRight :angle2 :arcRadius2
	right :angle3
	jumpForward :size2
	left :angle4
	jumpForward :size3
	left :angle5
	jumpForward :size4
	arcRight :angle6 :arcRadius3
	arcLeft :angle7 :arcRadius4
	jumpForward :size5
	arcLeft :angle8 :arcRadius5 ; upper right corner of helmet
	arcLeft :angle9 :arcRadius6
	left :angle10
	jumpForward :size6
	arcRight :angle11 :arcRadius7
	arcRight :angle12 :arcRadius8

	jumpForward :size7 ; reflection point

	arcRight :angle12 :arcRadius8
	arcRight :angle11 :arcRadius7
	jumpForward :size6
	left :angle10
	arcLeft :angle9 :arcRadius6
	arcLeft :angle8 :arcRadius5 ; upper left corner of helmet
	jumpForward :size5
	arcLeft :angle7 :arcRadius4
	arcRight :angle6 :arcRadius3
	jumpForward :size4
	left :angle5
	jumpForward :size3
	left :angle4
	jumpForward :size2
	right :angle3
	arcRight :angle2 :arcRadius2
	arcLeft :angle1 :arcRadius1

	polyEnd
	setTurtleState :oldState
end

to ironManMiddle :size
	localmake "oldState turtleState
	localmake "size1 :size * 0.07
	localmake "size2 :size * 0.077
	localmake "size3 :size * 0.075
	localmake "size4 :size * 0.19
	localmake "size5 :size * 0.02
	localmake "size6 :size * 0.035
	localmake "size7 :size * 0.2335374
	localmake "arcRadius1 :size * 0.014
	localmake "arcRadius2 :size * 0.01
	localmake "arcRadius3 :size * 0.02
	localmake "arcRadius4 :size * 0.295
	localmake "arcRadius5 :size * 0.03
	localmake "arcRadius6 :size * 0.06
	localmake "arcRadius7 :size * 0.17
	localmake "arcRadius8 :size * 0.055
	localmake "arcRadius9 :size * 0.02
	localmake "angle1 85
	localmake "angle2 40
	localmake "angle3 33
	localmake "angle4 30
	localmake "angle5 26.5
	localmake "angle6 124
	localmake "angle7 43
	localmake "angle8 30
	localmake "angle9 53
	localmake "angle10 92
	localmake "angle11 57.5
	setPenSize 0
	jumpBackward :size * 0.02
	right 90
	jumpForward :size1
	polyStart
	arcRight :angle1 :arcRadius1
	arcLeft :angle2 :arcRadius2
	arcLeft :angle3 :arcRadius3
	arcLeft :angle4 :arcRadius4
	arcLeft :angle5 :arcRadius5
	jumpForward :size2
	right :angle6
	jumpForward :size3
	arcRight :angle7 :arcRadius6
	jumpForward :size4
	arcLeft :angle8 :arcRadius7
	arcRight :angle9 :arcRadius8
	jumpForward :size5
	right :angle10
	jumpForward :size6
	arcLeft :angle11 :arcRadius9

	jumpForward :size7 ; reflection point

	arcLeft :angle11 :arcRadius9
	jumpForward :size6
	right :angle10
	jumpForward :size5
	arcRight :angle9 :arcRadius8
	arcLeft :angle8 :arcRadius7
	jumpForward :size4
	arcRight :angle7 :arcRadius6
	jumpForward :size3
	right :angle6
	jumpForward :size2
	arcLeft :angle5 :arcRadius5
	arcLeft :angle4 :arcRadius4
	arcLeft :angle3 :arcRadius3
	arcLeft :angle2 :arcRadius2
	arcRight :angle1 :arcRadius1

	polyEnd
	setTurtleState :oldState
end

to ironManJaw :size
	localmake "oldState turtleState
	localmake "size1 :size * 0.069
	localmake "size2 :size * 0.071
	localmake "size3 :size * 0.079
	localmake "size4 :size * 0.046
	localmake "size5 :size * 0.04
	localmake "arcRadius1 :size * 0.05
	localmake "arcRadius2 :size * 0.2
	localmake "arcRadius3 :size * 0.06
	localmake "arcRadius4 :size * 0.055
	localmake "angle1 43
	localmake "angle2 12.5
	localmake "angle3 101
	localmake "angle4 122.5
	localmake "angle5 60
	localmake "angle6 82
	localmake "angle7 35
	localmake "angle8 45
	localmake "angle9 49
	setPenSize 0
	jumpBackward :size * 0.375
	right 90
	jumpForward :size * 0.09
	polyStart
	arcRight :angle1 :arcRadius1
	arcRight :angle2 :arcRadius2
	left :angle3
	jumpForward :size1
	right :angle4
	jumpForward :size2
	right :angle5
	jumpForward :size3
	right :angle6
	jumpForward :size4
	arcLeft :angle7 :arcRadius3
	jumpForward :size5
	right :angle8
	arcLeft :angle9 :arcRadius4

	jumpForward :size * 0.016697334 ; reflection point

	arcLeft :angle9 :arcRadius4
	right :angle8
	jumpForward :size5
	arcLeft :angle7 :arcRadius3
	jumpForward :size4
	right :angle6
	jumpForward :size3
	right :angle5
	jumpForward :size2
	right :angle4
	jumpForward :size1
	left :angle3
	arcRight :angle2 :arcRadius2
	arcRight :angle1 :arcRadius1

	polyEnd
	setTurtleState :oldState
end

to ironManEyes :size
	localmake "oldState turtleState

	localmake "oldHeading heading
	localmake "size1 :size * 0.029
	localmake "size2 :size * 0.09
	localmake "size3 :size * 0.022
	localmake "size4 :size * 0.091
	localmake "angle1 9
	jumpBackward :size1
	localmake "pos1 pos
	jumpRight :size2
	left :angle1
	ellipse2 :size3 :size4

	jumpTo :pos1
	setHeading :oldHeading
	jumpLeft :size2
	right 180 + :angle1
	ellipse2 :size3 :size4

	setTurtleState :oldState
end

to ironManEyeDarkBackground :size :eyeSeparation
	localmake "oldPos pos
	localmake "size1 :size * 0.32
	localmake "size2 :size * 0.2
	localmake "size3 :size * 0.15
	localmake "colorStops createPList
	setProperty "colorStops 0 "#000
	setProperty "colorStops 0.8 "#000
	setProperty "colorStops 1 "#0000
	setFillGradient createRadialGradient pos :size * 0.32 :colorStops
	ellipse :size1 :size2

	
	localmake "colorStops createPList
	setProperty "colorStops 0 "#8fff
	setProperty "colorStops 0.5 "#644f
	setProperty "colorStops 1 "#000f
	jumpRight :eyeSeparation
	setFillGradient createRadialGradient pos :size3 :colorStops
	jumpTo :oldPos
	ellipse :size1 :size2
	jumpLeft :eyeSeparation
	setFillGradient createRadialGradient pos :size3 :colorStops
	jumpTo :oldPos
	ellipse :size1 :size2
end

to ironManHelmetPath :size
	ironManForehead :size
	ironManMiddle :size
	ironManJaw :size
end

; Inspired by
; https://www.pinterest.ca/pin/129408189285676669/
to ironManHelmet :size
	localmake "oldPos pos
	localmake "width :size * 0.3
	localmake "eyeLevel :size * 0.01
	localmake "eyeSeparation :size * 0.2
	localmake "eyeGradientSize :size * 0.25
	localmake "colorStops createPList
	setProperty "colorStops 0 "#8f00
	setProperty "colorStops 0.12 "#7f00
	setProperty "colorStops 0.14 "#50ff0000
	setProperty "colorStops 0.16 "#66ff0000
	setProperty "colorStops 1 "#0f00
	setPenGradient createRadialGradient pos :size * 4 :colorStops
	setPenSize :size * 10
	setLineCap "butt
	jumpBackward :size * 0.5
	forward :size
	setPenSize 0
	jumpTo :oldPos
	ironManEyeDarkBackground :size :eyeSeparation
	localmake "colorStops createPList
	setProperty "colorStops 0 "#ffd
	setProperty "colorStops 1 "#F0F056
	setFillGradient createRadialGradient pos :size * 0.3 :colorStops
	ironManHelmetPath :size
	setProperty "colorStops 0 "#0fff
	setProperty "colorStops 0.5 "#5fff
	setProperty "colorStops 1 "#0fff
	jumpLeft :width * 0.5
	localmake "fromPos pos
	jumpRight :width
	setFillGradient createLinearGradient :fromPos pos :colorStops "pad
	jumpTo :oldPos
	ironManHelmetPath :size

	jumpBackward :eyeLevel
	localmake "pos1 pos
	jumpRight :eyeSeparation
	localmake "colorStops createPList
	setProperty "colorStops 0 "#fff
	setProperty "colorStops 0.3 "#88d8d8ff
	setProperty "colorStops 0.5 "#44d8d8ff
	setProperty "colorStops 1 "#00d8d8ff
	setFillGradient createRadialGradient pos :eyeGradientSize :colorStops
	jumpTo :oldPos
	ironManHelmetPath :size
	jumpTo :pos1
	jumpLeft :eyeSeparation
	setFillGradient createRadialGradient pos :eyeGradientSize :colorStops
	jumpTo :oldPos
	ironManHelmetPath :size

	setFillColor "white
	ironManEyes :size
end

make "size 100
setScreenColor "black
ironManHelmet :size