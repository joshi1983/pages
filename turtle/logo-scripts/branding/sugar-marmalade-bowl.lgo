; Inspired by a bowl in the logo at
; https://www.sugarmarmalade.com
to sugarMarmaladeBowl :height
	localmake "oldState turtleState
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "angle1 68
	localmake "angle2 113
	localmake "arcRadius1 :height * 0.2
	localmake "left1 :height * 0.287
	localmake "width1 (:left1 - :arcRadius1 * sin :angle1) * 2
	localmake "segments [
		[0.23 89] [0.104 -80] [0.045 77] [0.15 170]
		[0.065 -75] [0.05 172] [0.045 -87] [0.07 -85]
		[0.074 90] [0.024 -83] [0.08 165] [0.1 -75]
		[0.06 90]
	]
	setFillColor "white
	jumpForward :height / 2
	setPenSize 0
	circle :height / 2
	jumpLeft :left1
	jumpBackward :height * 0.11
	setPenSize :height * 0.01
	setLineCap "round
	setPenColor "black
	right 90 + :angle1
	arcLeft :angle1 :arcRadius1
	forward :width1
	arcLeft 90 :arcRadius1
	forward :height * 0.06
	left 90
	forward :width1 + :arcRadius1 * 2
	right :angle2
	arcRight 180 - :angle2 :arcRadius1

	jumpTo :oldPos
	setHeading :oldHeading
	jumpForward :height * 0.6
	jumpLeft :height * 0.09
	setPenSize 0
	setFillColor penColor
	right 48
	polyStart
	repeat count :segments [
		localmake "segment item repcount :segments
		jumpForward :height * first :segment
		right last :segment
	]
	polyEnd
	setTurtleState :oldState
end

setScreenColor "black
sugarMarmaladeBowl 100