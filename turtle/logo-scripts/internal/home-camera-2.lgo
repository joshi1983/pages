; inspired by
; https://www.onlinewebfonts.com/icon/370319

make "color1 "white
make "color2 "black

to camera :size
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "apertureRadius :size * 0.2
	localmake "halfWidth :size * 0.5
	localmake "cornerRadius :size * 0.1
	localmake "halfWidth2 :halfWidth - :cornerRadius
	localmake "width2 :size - :cornerRadius * 2
	localmake "height :size * 0.7
	localmake "height2 :height - :cornerRadius * 2
	setPenSize 0
	back :height * 0.5
	right 90
	forward :halfWidth2
	arcLeft 90 :cornerRadius
	setFillColor :color2
	polyStart
	repeat 2 [
		forward :height2
		arcLeft 90 :cornerRadius
		forward :width2
		arcLeft 90 :cornerRadius
	]
	polyEnd
	setPos :oldPos
	setFillColor :color1
	circle :apertureRadius
	setHeading :oldHeading
end

to crossHairs :size
	localmake "oldPos pos
	localmake "oldHeading heading
	setPenColor :color1
	setPenSize :size * 0.07
	repeat 2 [
		localmake "size1 :size
		if repcount = 1 [
			localmake "size1 :size * 0.7
		]
		penUp
		setPos :oldPos
		backward :size1 * 0.5
		penDown
		forward :size1
		right 90
	]
	penUp
	setPos :oldPos
	setHeading :oldHeading
end

to homeCamera :size
	camera :size
	crossHairs :size
end

homeCamera 32