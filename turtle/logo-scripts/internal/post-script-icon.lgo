; Inspired by
; https://fontawesome.com/icons/file-pdf?s=solid

to postScriptDocument :size
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "cornerRadius :size * 0.12
	localmake "width :size * 0.75
	localmake "height1 :size - :cornerRadius * 2
	localmake "width1 :width - :cornerRadius * 2
	localmake "width2 :width1 * 0.85
	localmake "sharpCornerSize1 :width - :width2 - :cornerRadius
	localmake "sharpCornerLength :sharpCornerSize1 / sin 45
	localmake "height2 :height1 + :width2 - :width1
	localmake "penSize1 :size * 0.06
	localmake "sharpCornerSize2 :sharpCornerSize1 + :penSize1 * 0.5
	setlineJoinStyle "round
	jumpForward :cornerRadius
	right 90
	jumpBackward :width * 0.5
	localmake "startPos pos
	setHeading :oldHeading
	setFillColor "black
	setPenSize 0
	polyStart
	jumpForward :height1
	arcRight 90 :cornerRadius
	forward :width2
	right 45
	forward :sharpCornerLength
	right 45
	forward :height2
	arcRight 90 :cornerRadius
	forward :width1
	arcRight 90 :cornerRadius
	polyEnd
	setFontSize :size * 0.44
	forward :height1 * 0.2
	setPenColor "white
	setFillColor "white
	right 90
	forward :size * 0.08
	setPenSize :size * 0.02
	label "PS
	setPenSize 0
	penUp
	setPos :startPos
	setHeading :oldHeading
	jumpForward :size - :cornerRadius
	right 90
	jumpForward :cornerRadius + :width2 - :penSize1 * 0.5
	setPenSize :penSize1
	penDown
	right 90
	forward :sharpCornerSize2
	left 90
	forward :sharpCornerSize2
	penUp
	setPos :oldPos
	setHeading :oldHeading
end

postScriptDocument 32