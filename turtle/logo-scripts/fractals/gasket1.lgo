; This drawing was inspired by:
; http://www.unariunwisdom.com/wp-content/uploads/2016/10/fractals-of-inifinity-1.png
; Also, found at:
; https://szimmetria-airtemmizs.tumblr.com/post/151527351408/fractal-based-on-steiner-chains

to circlePattern :size
	if :size > 3 [
		localmake "originalPos pos
		localmake "xDelta1 :size * 0.569
		localmake "xDelta2 :size * 0.42
		localmake "size1 :size * 0.28518
		localmake "size2 :size * 0.332
		localmake "size3 :size * 0.105
		localmake "yDelta :size / 3
		localmake "yDelta2 :size * 0.67
		localmake "yDelta3 :size * 0.79
		localmake "thickness :size * 0.05
		if :thickness < 0.2 [
			localmake "thickness 0.2
		]
		setPenSize :thickness
		penDown
		circle :size
		jumpBackward :size * 0.430

		; lower side circles
		penUp
		setX xCor - :xDelta1
		penDown
		circlePattern :size1
		penUp
		setX xCor + :xDelta1 * 2
		penDown
		circlePattern :size1

		; central circle
		jumpTo [item 1 :originalPos -:yDelta2 + item 2 :originalPos]
		circlePattern :size2

		jumpTo [xCor - :xDelta2 -:yDelta3 + item 2 :originalPos]
		circlePattern :size3
		penUp
		setX xCor + :xDelta2 * 2
		penDown
		circlePattern :size3

		jumpTo [item 1 :originalPos :yDelta + item 2 :originalPos]
		circlePattern :size * 2 / 3
		jumpTo :originalPos
	]
end

setFillColor transparent
circlePattern 150