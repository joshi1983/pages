; Inspired by:
; https://www.pinterest.ca/pin/606649012318811237/

make "kochCircleShrinkRatio1 0.3
make "kochShrinkRatio2 0.45
make "kochCircleGapRatio :kochCircleShrinkRatio1 * 1.5

to kochCircle3 :size
	if :size < 0.5 [
		stop ; stop recursion when pattern is too small to see.
	]
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "innerCircleRadius :size * :kochCircleShrinkRatio1
	localmake "size1 :size * :kochCircleGapRatio
	localmake "size2 :size * :kochShrinkRatio2
	circle :innerCircleRadius
	left 90
	repeat 3 [
		jumpForward :size1
		kochCircle3 :size2
		right 90
		jumpTo :oldPos
	]
	setHeading :oldHeading
end

to kochCircle4 :size
	localmake "oldPos pos
	localmake "innerCircleRadius :size * :kochCircleShrinkRatio1
	localmake "size1 :size * :kochCircleGapRatio
	localmake "size2 :size * :kochShrinkRatio2
	setFillColor "black
	setPenSize 0
	circle :innerCircleRadius
	repeat 4 [
		jumpForward :size1
		kochCircle3 :size2
		jumpTo :oldPos
		right 90
	]
end

kochCircle4 100