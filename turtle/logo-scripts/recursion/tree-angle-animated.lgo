to branchLengthToColor :size
	if :size < 10 [
		output [0 200 0]
	]
	if :size < 30 [
		output [70 100 30]
	]
	output [80 60 30]
end

to tree :size
	if :size > 2 [
		localmake "originalHeading heading
		localmake "originalPosition pos
		setPenColor branchLengthToColor :size
		setPenSize :size * 0.1
		forward :size
		right :angle
		tree :size * 0.7
		left :angle * 2
		tree :size * 0.7

		; restore original heading and position.
		
		jumpTo :originalPosition
		setHeading :originalHeading
	]
end

make "angle animation.time * 10
tree 30 + animation.time * 5

to animation.snapshotstyle
	output createPList2 [
		["zoom.scale 3]
		["position.y 65]
	]
end

to animation.setup
	localmake "minutes 0
	localmake "seconds 5
	; Edit this if you want to change the length of your animation.
	output createPList2 [
		["duration :minutes * 60 + :seconds]
		["thumbnailTime :seconds]
	]
end