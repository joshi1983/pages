to setBalloonFillGradient :size :color
	localmake "colorStops createPList
	setProperty "colorStops 0 mix :color "white 0.5
	setProperty "colorStops 1 [:color easeIn]
	setFillGradient createRadialGradient pos pos :size :colorStops "pad
end

to curvedString :len :arcAngle :numRepeats
	localmake "arcRadius (:len / :numRepeats) * 0.25 / sin :arcAngle / 2
	left :arcAngle / 2
	repeat :numRepeats [
		arcRight :arcAngle :arcRadius
		arcLeft :arcAngle :arcRadius
	]
end

to circleBalloon :size :color
	localmake "oldState turtleState
	setBalloonFillGradient :size :color
	setPenSize 0
	circle :size
	jumpBackward :size
	setPenSize :size * 0.05
	setPenColor "black
	right 180
	setLineCap "round
	curvedString :size * 2 60 3
	setTurtleState :oldState
end

to longBalloon :size :color
	localmake "oldState turtleState
	localmake "oldHeading heading
	localmake "arcAngle1 40
	localmake "arcAngle2 20
	localmake "arcAngle3 50
	localmake "arcAngle4 140
	localmake "originalSize :size
	localmake "size :size * 2 * 0.6762
	localmake "arcRadius1 :size * 0.3
	localmake "arcRadius2 :size * 0.9
	localmake "arcRadius3 :size * 1
	localmake "arcRadius4 :size * 0.49741533
	setBalloonFillGradient :size :color
	jumpBackward :originalSize
	setPenSize :size * 0.01
	left 90
	polyStart
	arcRight :arcAngle1 :arcRadius1
	arcRight :arcAngle2 :arcRadius2
	arcRight :arcAngle3 :arcRadius3
	arcRight :arcAngle4 :arcRadius4
	arcRight :arcAngle3 :arcRadius3
	arcRight :arcAngle2 :arcRadius2
	arcRight :arcAngle1 :arcRadius1
	polyEnd
	setHeading :oldHeading + 180
	setPenSize :size * 0.03
	curvedString :originalSize * 2 60 3
	setTurtleState :oldState
end

setScreenColor "skyBlue
make "size 100
make "separation :size * 4
jumpLeft :separation * 2
longBalloon :size "navy
jumpRight :separation
circleBalloon :size "lime
jumpRight :separation
circleBalloon :size "red
jumpRight :separation
circleBalloon :size "white
jumpRight :separation
longBalloon :size "black