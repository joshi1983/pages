to drawRibbonShape :ribbonHeight :width :width2 :angle
	localmake "oldState turtleState
	localmake "oldHeading heading
	localmake "width1 (:width - :width2) / 2
	localmake "sideHeight :ribbonHeight - :width1 / tan :angle 
	localmake "slantedLength hypot [:width1 :ribbonHeight - :sideHeight]
	setPenSize 0
	jumpRight :width / 2
	polyStart
	jumpForward :sideHeight
	left :angle
	jumpForward :slantedLength
	setHeading :oldHeading - 90
	jumpForward :width2
	left 90 - :angle
	jumpForward :slantedLength
	setHeading :oldHeading
	jumpBackward :sideHeight
	polyEnd
	setTurtleState :oldState
end

to drawWhiteBackground :ribbonHeight :width :width2 :angle
	localmake "oldState turtleState
	setFillColor "white
	drawRibbonShape :ribbonHeight :width :width2 :angle
	setTurtleState :oldState
end

to drawFabricShadows :ribbonHeight :width :width2 :angle
	localmake "oldState turtleState
	localmake "oldPos pos
	localmake "oldHeading heading
	localmake "numShadows 82
	localmake "shadowHeight :ribbonHeight / :numShadows
	localmake "bottomShadowSize :ribbonHeight * 0.1
	localmake "colorStops createPList
	setProperty "colorStops 0 "#0000
	setProperty "colorStops 0.7 "#3000
	setProperty "colorStops 1 "#0000
	jumpForward :shadowHeight
	setFillGradient createLinearGradient :oldPos pos :colorStops "repeat
	jumpBackward :shadowHeight
	drawRibbonShape :ribbonHeight :width :width2 :angle
	localmake "colorStops createPList
	setProperty "colorStops 0 "#0000
	setProperty "colorStops 1 "#4000
	jumpForward :ribbonHeight - :bottomShadowSize
	localmake "fromPos pos
	jumpForward :bottomShadowSize
	setFillGradient createLinearGradient :fromPos pos :colorStops "pad
	jumpTo :oldPos	
	drawRibbonShape :ribbonHeight :width :width2 :angle
	jumpLeft :width2 / 2
	jumpForward :ribbonHeight
	left 270 - :angle
	localmake "fromPos pos
	jumpForward :bottomShadowSize
	setFillGradient createLinearGradient pos :fromPos :colorStops "pad
	jumpTo :oldPos	
	setHeading :oldHeading	
	drawRibbonShape :ribbonHeight :width :width2 :angle
	setTurtleState :oldState
end

to drawRibbon :height
	localmake "oldState turtleState
	localmake "oldHeading heading
	localmake "ribbonHeight :height * 0.514
	localmake "redSeparation :height * 0.29
	localmake "height1 :height * 0.264
	localmake "height2 :height * 0.502
	localmake "shapeWidth :height * 0.53
	localmake "width :height * 0.195
	localmake "slantedLength hypot [:width :height2 - :height1]
	localmake "angle arcSin :width / :slantedLength
	localmake "width2 (:redSeparation - :width) -  2 * (:ribbonHeight - :height2) * tan :angle
	jumpForward :height
	localmake "startPos pos
	right 180
	drawWhiteBackground :ribbonHeight :shapeWidth :width2 :angle
	stripes :height * 0.052 :ribbonHeight ["#015fa5]
	setPenSize 0
	setFillColor "#d31048
	jumpRight :redSeparation / 2
	repeat 2 [
		localmake "pos1 pos
		localmake "sign sign repcount - 1.5
		jumpLeft :width / 2 * :sign
		polyStart
		jumpForward :height1
		right :angle * :sign
		jumpForward :slantedLength
		setHeading :oldHeading + 180
		jumpBackward :height2
		polyEnd
		jumpTo :pos1
		jumpLeft :redSeparation
	]
	jumpTo :startPos
	drawFabricShadows :ribbonHeight :shapeWidth :width2 :angle
	setTurtleState :oldState
end

to brightStreak :len :width
	localmake "oldState turtleState
	localmake "colorStops createPList
	setProperty "colorStops 0 "#0fff
	setProperty "colorStops 0.5 "#afff
	setProperty "colorStops 1 "#0fff
	jumpLeft :width / 2
	localmake "fromPos pos
	jumpRight :width
	setPenGradient createLinearGradient :fromPos pos :colorStops "pad
	jumpLeft :width / 2
	setLineCap "butt
	setPenSize :width
	forward :len
	setTurtleState :oldState
end

to drawConnector :height
	localmake "oldState turtleState
	jumpForward :height * 1.25
	right 180
	setPenSize 0
	setFillColor "#cdbd66
	isoTriangle :height * 0.18 :height * 0.2
	setTurtleState :oldState
end

to drawBronzeStar :height
	localmake "oldState turtleState
	localmake "yOffset :height * 0.217
	localmake "smallRadius :height * 0.105
	localmake "largeRadius1 :height * 0.274
	localmake "largeRadius2 :largeRadius1 * 0.195
	localmake "smallRadius2 :smallRadius * :largeRadius2 / :largeRadius1
	localmake "colorStops createPList
	setProperty "colorStops 0 "#cdbd66
	setProperty "colorStops 0.2 "#DBDBB8
	setProperty "colorStops 0.3 "#705e08
	setProperty "colorStops 1 "#74620c
	jumpForward :yOffset
	drawConnector :yOffset
	setPenSize 0
	setFillGradient createRadialGradient pos :height * 0.27 :colorStops
	roundIsoStar :largeRadius1 :smallRadius 5 :height * 0.003 :height * 0.003
	localmake "inletRatio 0.94
	setPenSize :height * 0.006
	setFillColor transparent
	setPenColor "#88cdbd66
	roundIsoStar :largeRadius1 * :inletRatio :smallRadius * :inletRatio 5 :height * 0.003 :height * 0.003
	setPenSize 0
	repeat 5 [
		brightStreak :largeRadius1 * 0.98 :height * 0.03
		right 180 / 5
		brightStreak :smallRadius * 0.98 :height * 0.01
		right 180 / 5
	]
	localmake "colorStops createPList
	setProperty "colorStops 0 "#cdbd66
	setProperty "colorStops 0.15 "#705e08
	setProperty "colorStops 1 "#5F5007
	setFillGradient createRadialGradient pos :height * 0.27 :colorStops
	roundIsoStar :largeRadius2 :smallRadius2 5 :height * 0.005 :height * 0.005
	repeat 5 [
		brightStreak :largeRadius2 * 0.98 :height * 0.01
		right 180 / 5
		brightStreak :smallRadius2 * 0.98 :height * 0.005
		right 180 / 5
	]
	setTurtleState :oldState
end

; Inspired by
; https://en.wikipedia.org/wiki/Bronze_Star_Medal#/media/File:Bronze_Star_medal.jpg
to bronzeStarMedal :height
	localmake "oldState turtleState
	drawBronzeStar :height
	drawRibbon :height
	setTurtleState :oldState
end

bronzeStarMedal 100
