to drawBranch1 :branchsize
	forward :branchsize
	jumpBackward :branchsize
	
end

to snowflake1 :size :c
	setPenColor :c
	setFillColor :c
	setPenSize :size * 0.1
	circle :size * 0.05
	setFillColor transparent
	circle :size * 0.2
	setPenSize :size * 0.05
	repeat 6 [
		jumpForward :size * 0.2
		
		localmake "s :size * 0.4
		repeat 4 [
			forward :size * 0.2
			right 60
			drawBranch1 :s
			left 120
			drawBranch1 :s
			right 60
			localmake "s :s - :size * 0.1
		]
		jumpBackward :size
		
		right 60
	]
end

to snowflake2 :size :c
	setPenColor :c
	setFillColor :c
	setPenSize :size * 0.1
	circle :size * 0.05
	setFillColor transparent
	circle :size * 0.2
	setPenSize :size * 0.05
	repeat 6 [
		jumpForward :size * 0.2
		
		forward :size * 0.2
		localmake "s :size * 0.4
		repeat 2 [
			forward :size * 0.2
			right 60
			drawBranch1 :s
			left 120
			drawBranch1 :s
			right 60
			localmake "s :s - :size * 0.25
		]
		jumpForward :size * 0.2
		
		circle :size * 0.1
		jumpBackward :size
		
		right 60
	]
end

to snowflake3 :size :c
	setPenColor :c
	setFillColor :c
	setPenSize :size * 0.05
	localmake "s :size * 0.2
	
	repeat 6 [
		repeat 5 [
			forward :size * 0.2
			right 45
			forward :s
			jumpBackward :s
			left 90
			forward :s
			jumpBackward :s
			right 45
		]
		jumpBackward :size
		right 60
	]
end

to snowflake4 :size
	localmake "size1 :size * 1.5
	localmake "size2 :size1 * 0.093691
	localmake "size3 :size1 * 0.53334
	localmake "size4 :size * 0.1

	
	repeat 6 [
		setPenSize :size * 0.018
		arcRight 25 :size1
		arcRight 180 :size2
		arcRight 25 :size1
		left 175
		setPenSize :size * 0.011
		forward :size3
		setPenSize :size * 0.01
		repeat 4 [
			arcLeft 90 :size4
			right 180
		]
		jumpBackward :size3
		right 5
	]
end

to drawSpiralArc :size :isRight
	localmake "originalPos pos
	localmake "a 280
	ifelse :isRight [
		arcRight :a :size
		left :a
	] [
		arcLeft :a :size
		right :a
	]
	jumpTo :originalPos
	
end

to drawSpiralArcs :size
	drawSpiralArc :size true
	drawSpiralArc :size false
end

to snowflake5 :size
	localmake "size1 :size * 0.4
	localmake "size2 :size * 0.3
	localmake "size3 :size * 0.2
	localmake "size4 :size * 0.03
	localmake "size5 :size * 0.02
	localmake "size6 :size * 0.01
	localmake "size7 :size * 0.08
	localmake "size8 :size * 0.15
	localmake "size9 :size * 0.04
	localmake "backSize :size1 + :size2 + :size3
	setPenSize :size * 0.02
	repeat 6 [
		setPenSize :size4
		forward :size1
		setPenSize :size5
		drawSpiralArcs :size7
		forward :size2
		setPenSize :size6
		drawSpiralArcs :size8
		forward :size3
		drawSpiralArcs :size9
		jumpBackward :backSize
		
		right 60
	]
end

to drawBranch :size :angle
	localmake "originalPenSize penSize
	localmake "arcRadius :size * 0.3
	localmake "size1 :size * 0.173
	localmake "originalPosition pos
	localmake "oldState turtleState
	right :angle
	forward :size
	setPenSize 0
	setFillColor penColor
	circle :originalPenSize * 0.5
	setPenSize :originalPenSize
	jumpTo :originalPosition
	left :angle
	jumpBackward :size1
	
	if (abs :angle) < 90 [
		; curve corners
		ifelse :angle > 0 [
			arcRight :angle :arcRadius
		] [
			arcLeft -:angle :arcRadius
		]
	]
	setTurtleState :oldState
end

to drawBranches :size :angle
	drawBranch :size :angle
	drawBranch :size (-:angle)
end

to snowflake6 :size
	localmake "mainLineThickness :size * 0.04
	localmake "minorLineThickness :size * 0.03						 
	localmake "size2 :size * 0.1
	localmake "size3 :size * 0.2
	localmake "size4 :size * 0.5
	localmake "size5 :size * 0.2
	localmake "size6 :size * 0.05
	localmake "backSize1 :size4 + :size5 * 2
	localmake "backSize2 :size4 + :size2
	setFillColor penColor

	repeat 6 [
		
		setPenSize :mainLineThickness
		forward :size4
		drawBranches :size3 60
		forward :size5
		drawBranches :size3 60
		forward :size5
		setPenSize 0
		circle :size2
		jumpBackward :backSize1
		right 30
		setPenSize :minorLineThickness
		forward :size4
		drawBranches :size2 140
		drawBranches :size2 60
		forward :size2
		setPenSize 0
		circle :size6
		jumpBackward :backSize2
		right 30
	]
end

make "snowFlakeSize 50
make "separation :snowFlakeSize * 3.5
right 90
jumpForward -:separation * 2.5
snowflake1 :snowFlakeSize "midnightblue
jumpForward :separation
snowflake2 :snowFlakeSize "midnightBlue
jumpForward :separation
snowflake3 :snowFlakeSize "midnightBlue
jumpForward :separation
snowflake4 :snowFlakeSize
jumpForward :separation
snowflake5 :snowFlakeSize
jumpForward :separation
snowflake6 :snowFlakeSize